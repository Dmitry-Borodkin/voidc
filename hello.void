{   v_import("printf.void"); }

{   v_import("llvm-c/Core.void");
    v_import("llvm-c/TargetMachine.void");
    v_import("llvm-c/Transform/PassManagerBuilder.void");
}

{   triple = LLVMGetDefaultTargetTriple();

    tr_v = v_alloca(LLVMTargetRef);

    LLVMGetTargetFromTriple(triple, tr_v, 0);

    tr = v_load(tr_v);

    cpu_name     = LLVMGetHostCPUName();
    cpu_features = LLVMGetHostCPUFeatures();

    target_machine =
        LLVMCreateTargetMachine
        (
            tr,
            triple,
            cpu_name,
            cpu_features,
            LLVMCodeGenLevelAggressive,
            LLVMRelocDefault,
            LLVMCodeModelDefault
        );

    LLVMDisposeMessage(cpu_features);
    LLVMDisposeMessage(cpu_name);
    LLVMDisposeMessage(triple);

    //-----------------------------------------------------------------
    module = LLVMModuleCreateWithName("hello_mod");

    main_ft = LLVMFunctionType(void, 0, 0, 0);
    main_f  = LLVMAddFunction(module, "main", main_ft);

    entry = LLVMAppendBasicBlock(main_f, "entry");

    builder = LLVMCreateBuilder();

    LLVMPositionBuilderAtEnd(builder, entry);


    printf_ft = v_find_symbol_type("printf");

    printf_f  = LLVMAddFunction(module, "printf", printf_ft);

    str = LLVMBuildGlobalStringPtr(builder, "Hello world!\n", "str");

    arg = v_alloca(LLVMValueRef);

    v_store(str, arg);

    LLVMBuildCall(builder, printf_f, arg, 1, "");


    LLVMBuildRetVoid(builder);

    LLVMClearInsertionPosition(builder);

    LLVMDisposeBuilder(builder);

    //-----------------------------------------------------------------
    pass_manager = LLVMCreatePassManager();

    pm_builder = LLVMPassManagerBuilderCreate();

    LLVMPassManagerBuilderSetOptLevel(pm_builder, 3);       //- -O3
    LLVMPassManagerBuilderSetSizeLevel(pm_builder, 2);      //- -Oz

    LLVMPassManagerBuilderPopulateModulePassManager(pm_builder, pass_manager);

    LLVMPassManagerBuilderDispose(pm_builder);

    //-----------------------------------------------------------------
    dl = LLVMCreateTargetDataLayout(target_machine);
    tr = LLVMGetTargetMachineTriple(target_machine);

    LLVMSetModuleDataLayout(module, dl);
    LLVMSetTarget(module, tr);

    LLVMDisposeMessage(tr);
    LLVMDisposeTargetData(dl);

    LLVMRunPassManager(pass_manager, module);

    LLVMDisposePassManager(pass_manager);

    //-----------------------------------------------------------------
    msg = LLVMPrintModuleToString(module);

    printf("\n%s\n", msg);

    LLVMDisposeMessage(msg);

    //-----------------------------------------------------------------
    LLVMTargetMachineEmitToFile(target_machine,
                                module,
                                "hello.o",
                                LLVMObjectFile,
                                0);

    //-----------------------------------------------------------------
    LLVMDisposeModule(module);

    LLVMDisposeTargetMachine(target_machine);
}

{   gctx = LLVMGetGlobalContext();

    opaque_FILE = LLVMStructCreateNamed(gctx, "struct.OpaqueFILE");

    v_add_symbol_type("stdout", opaque_FILE);

    arg = v_alloca(LLVMTypeRef);

    FILE_ptr = LLVMPointerType(opaque_FILE, 0);

    v_store(FILE_ptr, arg);

    fun_t = LLVMFunctionType(int, arg, 1, 0);

    v_add_symbol_type("fflush", fun_t);
}

{   fflush(stdout);     //- WTF?
}

{   arg = v_alloca(LLVMTypeRef);

    char_ptr = LLVMPointerType(char, 0);

    v_store(char_ptr, arg);

    system_ft = LLVMFunctionType(int, arg, 1, 0);

    v_add_symbol_type("system", system_ft);
}

{   system("clang hello.o -o hello");

    system("hello");
}


