//---------------------------------------------------------------------
//- Copyright (C) 2020-2021 Dmitry Borodkin <borodkin-dn@yandex.ru>
//- SDPX-License-Identifier: LGPL-3.0-or-later
//---------------------------------------------------------------------
{   voidc_guard_import("memory.void"); }

{   v_import("function_hack.void"); }


//---------------------------------------------------------------------
//- ...
//---------------------------------------------------------------------
{
    module = LLVMModuleCreateWithName("voidc_memory_intrinsics_module");

    v_set_module(module);       //- Sic!

    void_ptr = v_pointer_type(void, 0);
    v_add_local_symbol("void_ptr", voidc_opaque_type, void_ptr);
}


//---------------------------------------------------------------------
//- v_malloc
//---------------------------------------------------------------------
//- fun voidc_memory_malloc_intrinsic(visitor: voidc_visitor_t, aux: void_ptr, arg_list: v_ast_arg_list_ptr)
//- {
//-     arg_count = v_ast_arg_list_get_count(arg_list);
//-
//-     args = v_alloca(v_ast_opaque_argument_sptr, args_count);
//-     v_initialize(args, args_count);
//-
//-     v_ast_arg_list_get_args(arg_list, args);
//-
//-     num_var = v_alloca(LLVMValueRef);
//-
//-     n1 = v_cast(1, int);
//-
//-     ok = v_icmp(LLVMIntEQ, args_count, n1);
//-
//-     if (ok)
//-     {
//-         int_ = v_type_get_llvm_type(int);
//-
//-         n1_v = LLVMConstInt(int_, 1, 0);
//-
//-         v_store(n1_v, num_var);
//-     }
//-     else
//-     {
//-         arg1 = v_getelementptr(args, 1);
//-
//-         base_arg1 = v_cast(arg1, v_ast_base_ptr);
//-
//-         v_ast_accept_visitor(base_arg1, visitor, aux);
//-
//-         num = v_get_argument_value(0);
//-
//-         v_store(num, num_var);
//-     }
//-
//-     ...
//-
//---------------------------------------------------------------------
{
    //-----------------------------------------------------------------
    malloc_f = v_function_hack("voidc_memory_malloc_intrinsic", voidc_intrinsic_t);

    visitor  = v_add_parameter_name(malloc_f, 0, "visitor",  voidc_visitor_ptr);
    aux      = v_add_parameter_name(malloc_f, 1, "aux",      void_ptr);
    arg_list = v_add_parameter_name(malloc_f, 2, "arg_list", v_ast_arg_list_ptr);


    val0 = v_alloca(LLVMValueRef, 3);
    val1 = v_getelementptr(val0, 1);
    val2 = v_getelementptr(val0, 2);

    int_ = v_type_get_llvm_type(int);

    n0 = LLVMConstInt(int_, 0, 0);
    n1 = LLVMConstInt(int_, 1, 0);

    //-----------------------------------------------------------------
    builder = v_target_get_voidc_builder();


    v_store(arg_list, val0);

    arg_list_get_count_f = v_obtain_function("v_ast_arg_list_get_count");

    args_count_v = LLVMBuildCall(builder, arg_list_get_count_f, val0, 1, "args_count_v");


    v_ast_opaque_argument_sptr_ = v_type_get_llvm_type(v_ast_opaque_argument_sptr);

    args_v = LLVMBuildArrayAlloca(builder, v_ast_opaque_argument_sptr_, args_count_v, "args_v");


    v_store(args_v,       val0);
    v_store(args_count_v, val1);

    arg_init_f = v_obtain_dict_function(v_util_initialize_dict, v_ast_opaque_argument_sptr);

    LLVMBuildCall(builder, arg_init_f, val0, 2, "");


    v_store(arg_list, val0);
    v_store(args_v,   val1);

    arg_list_get_args_f = v_obtain_function("v_ast_arg_list_get_args");

    LLVMBuildCall(builder, arg_list_get_args_f, val0, 2, "");


    then_b = LLVMAppendBasicBlock(malloc_f, "then_b");
    else_b = LLVMAppendBasicBlock(malloc_f, "else_b");
    cont_b = LLVMAppendBasicBlock(malloc_f, "cont_b");


    LLVMValueRef_ = v_type_get_llvm_type(LLVMValueRef);

    num_v = LLVMBuildAlloca(builder, LLVMValueRef_, "num_v");


    ok_v = LLVMBuildICmp(builder, LLVMIntEQ, args_count_v, n1, "ok_v");

    LLVMBuildCondBr(builder, ok_v, then_b, else_b);


    LLVMPositionBuilderAtEnd(builder, then_b);


    int_v = v_obtain_global("int");

    v_store(int_v, val0);

    get_llvm_type_f = v_obtain_function("v_type_get_llvm_type");

    int__v = LLVMBuildCall(builder, get_llvm_type_f, val0, 1, "int__v");

    long_long_ = v_type_get_llvm_type(long_long);

    n1ll = LLVMBuildZExtOrBitCast(builder, n1, long_long_, "n1ll");

    v_store(int__v, val0);
    v_store(n1ll,   val1);
    v_store(n0,     val2);

    llvm_const_int_f = v_obtain_function("LLVMConstInt");

    n1_v = LLVMBuildCall(builder, llvm_const_int_f, val0, 3, "n1_v");


    LLVMBuildStore(builder, n1_v, num_v);


    LLVMBuildBr(builder, cont_b);


    LLVMPositionBuilderAtEnd(builder, else_b);


    v_store(n1, val0);

    args_v_1 = LLVMBuildGEP(builder, args_v, val0, 1, "args_v_1");


    v_ast_base_ptr_ = v_type_get_llvm_type(v_ast_base_ptr);

    base_arg1 = LLVMBuildBitCast(builder, args_v_1, v_ast_base_ptr_, "base_arg1");


    accept_visitor_f = v_obtain_function("v_ast_accept_visitor");

    v_store(base_arg1, val0);
    v_store(visitor,   val1);
    v_store(aux,       val2);

    LLVMBuildCall(builder, accept_visitor_f, val0, 3, "");


    get_arg_val_f = v_obtain_function("v_get_argument_value");

    v_store(n0, val0);

    num = LLVMBuildCall(builder, get_arg_val_f, val0, 1, "num");


    LLVMBuildStore(builder, num, num_v);


    LLVMBuildBr(builder, cont_b);


    LLVMPositionBuilderAtEnd(builder, cont_b);


    v_add_variable("args",       v_ast_argument_ptr, args_v);
    v_add_variable("args_count", int,                args_count_v);
    v_add_variable("num_var",    LLVMValueRef,       num_v);
}
{
    number = v_load(num_var);

    builder = v_target_get_builder();

    type  = v_lookup_type(args);
    type_ = v_type_get_llvm_type(type);

    ret = LLVMBuildArrayMalloc(builder, type_, number, "ret");

    v_set_return_value(ret);

    pt = v_pointer_type(type, 0);
    v_set_return_type(pt);

    v_clear_arguments();

    v_terminate(args, args_count);
}


//---------------------------------------------------------------------
//- v_mfree
//---------------------------------------------------------------------
{
    //-----------------------------------------------------------------
    f = v_function_hack("voidc_memory_mfree_intrinsic", voidc_intrinsic_t);

    v_add_parameter_name(f, 0, "visitor",  voidc_visitor_ptr);
    v_add_parameter_name(f, 1, "aux",      void_ptr);
    v_add_parameter_name(f, 2, "arg_list", v_ast_arg_list_ptr);
}
{
    builder = v_target_get_builder();

    arg_base = v_cast(arg_list, v_ast_base_ptr);

    v_ast_accept_visitor(arg_base, visitor, aux);

    arg = v_get_argument_value(0);

    LLVMBuildFree(builder, arg);

    v_clear_arguments();
}


//---------------------------------------------------------------------
//- ...
//---------------------------------------------------------------------
{
    module = v_get_module();

    v_set_module(0);

    //-----------------------------------------------------------------
//  v_debug_print_module(1);

    voidc_prepare_module_for_jit(module);

    //-----------------------------------------------------------------
    PH = v_alloca(LLVMOrcModuleHandle);

    LLVMOrcAddEagerlyCompiledIR(voidc_jit, PH, module,
                                voidc_resolver, 0
                               );
}

{   v_add_intrinsic("v_malloc", voidc_memory_malloc_intrinsic);
    v_add_intrinsic("v_mfree",  voidc_memory_mfree_intrinsic);
}



