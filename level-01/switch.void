//---------------------------------------------------------------------
//- Copyright (C) 2020-2023 Dmitry Borodkin <borodkin.dn@gmail.com>
//- SDPX-License-Identifier: LGPL-3.0-or-later
//---------------------------------------------------------------------
{   voidc_guard_target("switch.void  must be imported into the voidc target only!");

    v_import("level-00");

    v_import("llvm-c/Core.void");

    v_import("function_hack.void");
    v_import("if_then_else.void");
    v_import("arithmetic.void");
    v_import("block.void");
    v_import("loop.void");
    v_import("grammar.void");
}

{   voidc_enable_statement_if_then_else();
    voidc_enable_statement_block();
    voidc_enable_statement_loop();
    voidc_enable_statement_grammar();
}


//---------------------------------------------------------------------
{   v_add_type("void_ptr", v_pointer_type(void, 0));

    q_ref = v_reference_type(v_quark_t, 0);

    q = v_quark_ptr_from_string;

    v_add_symbol("voidc_internal_branch_target_leave_q",          q_ref, q("voidc.internal_branch_target_leave"));
    v_add_symbol("voidc_internal_branch_target_break_variable_q", q_ref, q("voidc.internal_branch_target_break_variable"));
    v_add_symbol("voidc_internal_branch_target_break_q",          q_ref, q("voidc.internal_branch_target_break"));
}

//---------------------------------------------------------------------
{   typ0 = v_alloca(v_type_ptr, 2);
    typ1 = v_getelementptr(typ0, 1);


    //-----------------------------------------------------------------
    //- ast_stmt_switch_t ...
    //-----------------------------------------------------------------
    ast_stmt_switch_t = v_struct_type_named("struct.ast_stmt_switch_t");

    v_store(v_ast_expr_t,         typ0);
    v_store(v_ast_generic_list_t, typ1);

    v_type_struct_set_body(ast_stmt_switch_t, typ0, 2, false);

    ast_stmt_switch_ptr = v_pointer_type(ast_stmt_switch_t, 0);

    v_export_type("v_ast_stmt_switch_t", ast_stmt_switch_t);

    v_add_type("ast_stmt_switch_t",   ast_stmt_switch_t);
    v_add_type("ast_stmt_switch_ptr", ast_stmt_switch_ptr);

    size_v = LLVMSizeOf(v_type_get_llvm_type(ast_stmt_switch_t));

    v_add_constant("ast_stmt_switch_t_size", size_t, size_v);


    //-----------------------------------------------------------------
    //- ast_case_block_t ...
    //-----------------------------------------------------------------
    ast_case_block_t = v_struct_type_named("struct.ast_case_block_t");

    v_store(v_ast_expr_list_t, typ0);
    v_store(v_ast_stmt_list_t, typ1);

    v_type_struct_set_body(ast_case_block_t, typ0, 2, false);

    ast_case_block_ptr = v_pointer_type(ast_case_block_t, 0);

    v_export_type("v_ast_case_block_t", ast_case_block_t);

    v_add_type("ast_case_block_t",   ast_case_block_t);
    v_add_type("ast_case_block_ptr", ast_case_block_ptr);

    size_v = LLVMSizeOf(v_type_get_llvm_type(ast_case_block_t));

    v_add_constant("ast_case_block_t_size", size_t, size_v);
}


//---------------------------------------------------------------------
{   module = LLVMModuleCreateWithName("my_stmt_switch_mod");

    v_set_module(module);

    //-----------------------------------------------------------------
    LLVMDisposeMemoryBuffer(voidc_get_unit_buffer());
    voidc_set_unit_buffer(0);
}


//---------------------------------------------------------------------
//- quark ...
//---------------------------------------------------------------------
{
    q = v_quark_ptr_from_string;

    quark_ref = v_reference_type(v_quark_t, 0);

    v_add_symbol("quark", quark_ref, q("stmt_switch"));

    v_add_symbol("case_block_q", quark_ref, q("case_block"));
    v_add_symbol("case_list_q",  quark_ref, q("case_list"));
}


//---------------------------------------------------------------------
//- stmt_switch init ...
//---------------------------------------------------------------------
{
    //-----------------------------------------------------------------
    f = v_function_hack("voidc_ast_stmt_switch_init", v_ast_generic_init_fun_t);

    LLVMSetLinkage(f, LLVMPrivateLinkage);

    v_add_parameter_name(f, 0, "void_obj", void_ptr);
}
{
    obj = v_cast(void_obj, ast_stmt_switch_ptr);

    arg  = v_getelementptr(obj, 0, 0);
    list = v_getelementptr(obj, 0, 1);

    v_initialize(arg);
    v_initialize(list);
}


//---------------------------------------------------------------------
//- stmt_switch term ...
//---------------------------------------------------------------------
{
    //-----------------------------------------------------------------
    f = v_function_hack("voidc_ast_stmt_switch_term", v_ast_generic_term_fun_t);

    LLVMSetLinkage(f, LLVMPrivateLinkage);

    v_add_parameter_name(f, 0, "void_obj", void_ptr);
}
{
    obj = v_cast(void_obj, ast_stmt_switch_ptr);

    arg  = v_getelementptr(obj, 0, 0);
    list = v_getelementptr(obj, 0, 1);

    v_terminate(arg);
    v_terminate(list);
}


//-----------------------------------------------------------------------
//- stmt_switch vtable ...
//-------------------------------------------------------------------
{
    module = v_get_module();

    val0 = v_alloca(LLVMValueRef, 3);
    val1 = v_getelementptr(val0, 1);
    val2 = v_getelementptr(val0, 2);

    init_f   = v_obtain_function("voidc_ast_stmt_switch_init", 0);
    term_f   = v_obtain_function("voidc_ast_stmt_switch_term", 0);
    quark_u  = LLVMGetUndef(v_type_get_llvm_type(v_quark_t));

    v_store(init_f,   val0);
    v_store(term_f,   val1);
    v_store(quark_u,  val2);

    v_ast_generic_vtable_ = v_type_get_llvm_type(v_ast_generic_vtable);

    vtable_v = LLVMConstNamedStruct(v_ast_generic_vtable_, val0, 3);

    vtable_g = LLVMAddGlobal(module, v_ast_generic_vtable_, "voidc_ast_stmt_switch_vtable");

    LLVMSetLinkage(vtable_g, LLVMPrivateLinkage);

    LLVMSetInitializer(vtable_g, vtable_v);

    //-----------------------------------------------------------------
    LLVMDisposeMemoryBuffer(voidc_get_unit_buffer());
    voidc_set_unit_buffer(0);
}

{   v_add_symbol("voidc_ast_stmt_switch_vtable", v_ast_generic_vtable, 0);
}


//---------------------------------------------------------------------
//- v_ast_make_stmt_switch - ...
//---------------------------------------------------------------------
{
    typ0 = v_alloca(v_type_ptr, 3);
    typ1 = v_getelementptr(typ0, 1);
    typ2 = v_getelementptr(typ0, 2);

    v_store(v_ast_stmt_ptr,         typ0);
    v_store(v_ast_expr_ptr,         typ1);
    v_store(v_ast_generic_list_ptr, typ2);

    ft = v_function_type(void, typ0, 3, false);

    //-----------------------------------------------------------------
    f = v_function_hack("v_ast_make_stmt_switch", ft);

    v_add_parameter_name(f, 0, "ret",  v_ast_stmt_ptr);
    v_add_parameter_name(f, 1, "arg",  v_ast_expr_ptr);
    v_add_parameter_name(f, 2, "list", v_ast_generic_list_ptr);
}
{
    stmp = v_alloca(v_ast_stmt_t);
    v_initialize(stmp);

    v_ast_make_generic(stmp, voidc_ast_stmt_switch_vtable, ast_stmt_switch_t_size);

    void_obj = v_ast_generic_get_object(stmp);

    obj = v_cast(void_obj, ast_stmt_switch_ptr);

    obj_arg  = v_getelementptr(obj, 0, 0);
    obj_list = v_getelementptr(obj, 0, 1);

    v_copy(obj_arg,  arg);
    v_copy(obj_list, list);

    v_copy(ret, stmp);

    v_terminate(stmp);
}


//---------------------------------------------------------------------
//- case_block init ...
//---------------------------------------------------------------------
{
    //-----------------------------------------------------------------
    f = v_function_hack("voidc_ast_case_block_init", v_ast_generic_init_fun_t);

    LLVMSetLinkage(f, LLVMPrivateLinkage);

    v_add_parameter_name(f, 0, "void_obj", void_ptr);
}
{
    obj = v_cast(void_obj, ast_case_block_ptr);

    arg_list  = v_getelementptr(obj, 0, 0);
    stmt_list = v_getelementptr(obj, 0, 1);

    v_initialize(arg_list);
    v_initialize(stmt_list);
}


//---------------------------------------------------------------------
//- case_block term ...
//---------------------------------------------------------------------
{
    //-----------------------------------------------------------------
    f = v_function_hack("voidc_ast_case_block_term", v_ast_generic_term_fun_t);

    LLVMSetLinkage(f, LLVMPrivateLinkage);

    v_add_parameter_name(f, 0, "void_obj", void_ptr);
}
{
    obj = v_cast(void_obj, ast_case_block_ptr);

    arg_list  = v_getelementptr(obj, 0, 0);
    stmt_list = v_getelementptr(obj, 0, 1);

    v_terminate(arg_list);
    v_terminate(stmt_list);
}


//-----------------------------------------------------------------------
//- case_block vtable ...
//-------------------------------------------------------------------
{
    module = v_get_module();

    val0 = v_alloca(LLVMValueRef, 3);
    val1 = v_getelementptr(val0, 1);
    val2 = v_getelementptr(val0, 2);

    init_f = v_obtain_function("voidc_ast_case_block_init", 0);
    term_f = v_obtain_function("voidc_ast_case_block_term", 0);

    v_quark_t_ = v_type_get_llvm_type(v_quark_t);

    quark_u  = LLVMGetUndef(v_quark_t_);

    v_store(init_f,   val0);
    v_store(term_f,   val1);
    v_store(quark_u,  val2);        //- Undef!

    v_ast_generic_vtable_ = v_type_get_llvm_type(v_ast_generic_vtable);

    vtable_v = LLVMConstNamedStruct(v_ast_generic_vtable_, val0, 3);

    vtable_g = LLVMAddGlobal(module, v_ast_generic_vtable_, "voidc_ast_case_block_vtable");

    LLVMSetLinkage(vtable_g, LLVMPrivateLinkage);

    LLVMSetInitializer(vtable_g, vtable_v);

    //-----------------------------------------------------------------
    LLVMDisposeMemoryBuffer(voidc_get_unit_buffer());
    voidc_set_unit_buffer(0);
}

{   v_add_symbol("voidc_ast_case_block_vtable", v_ast_generic_vtable, 0);      //- Sic!
}


//---------------------------------------------------------------------
//- v_ast_make_case_block - ...
//---------------------------------------------------------------------
{
    typ0 = v_alloca(v_type_ptr, 3);
    typ1 = v_getelementptr(typ0, 1);
    typ2 = v_getelementptr(typ0, 2);

    v_store(v_ast_base_ptr,      typ0);
    v_store(v_ast_expr_list_ptr, typ1);
    v_store(v_ast_stmt_list_ptr, typ2);

    ft = v_function_type(void, typ0, 3, false);

    //-----------------------------------------------------------------
    f = v_function_hack("v_ast_make_case_block", ft);

    v_add_parameter_name(f, 0, "ret",       v_ast_base_ptr);
    v_add_parameter_name(f, 1, "arg_list",  v_ast_expr_list_ptr);
    v_add_parameter_name(f, 2, "stmt_list", v_ast_stmt_list_ptr);
}
{
    otmp = v_alloca(v_ast_base_t);
    v_initialize(otmp);

    v_ast_make_generic(otmp, voidc_ast_case_block_vtable, ast_case_block_t_size);

    void_obj = v_ast_generic_get_object(otmp);

    obj = v_cast(void_obj, ast_case_block_ptr);

    obj_arg_list  = v_getelementptr(obj, 0, 0);
    obj_stmt_list = v_getelementptr(obj, 0, 1);

    v_copy(obj_arg_list,  arg_list);
    v_copy(obj_stmt_list, stmt_list);

    v_copy(ret, otmp);

    v_terminate(otmp);
}


//---------------------------------------------------------------------
{
    //-----------------------------------------------------------------
    f = v_function_hack("mk_case_item_grammar_action", v_peg_grammar_action_fun_t);

    LLVMSetLinkage(f, LLVMPrivateLinkage);

    v_add_parameter_name(f, 0, "ret",       v_std_any_ptr);
    v_add_parameter_name(f, 1, "aux",       void_ptr);
    v_add_parameter_name(f, 2, "any0",      v_std_any_ptr);
    v_add_parameter_name(f, 3, "any_count", size_t);
}
{
    arg = v_std_any_get_pointer(v_ast_expr_t, any0);

    if (arg)
    {
        v_std_any_set_pointer(ret, arg);    //- "Normal" case
    }
    else
    {
        arg = v_alloca(v_ast_expr_t);
        v_initialize(arg);

        v_std_any_set_pointer(ret, arg);    //- Empty - "default"

        v_terminate(arg);
    }
}


//---------------------------------------------------------------------
{
    //-----------------------------------------------------------------
    f = v_function_hack("mk_case_block_grammar_action", v_peg_grammar_action_fun_t);

    LLVMSetLinkage(f, LLVMPrivateLinkage);

    v_add_parameter_name(f, 0, "ret",       v_std_any_ptr);
    v_add_parameter_name(f, 1, "aux",       void_ptr);
    v_add_parameter_name(f, 2, "any0",      v_std_any_ptr);
    v_add_parameter_name(f, 3, "any_count", size_t);
}
{
    any1 = v_getelementptr(any0, 1);

    case_list = v_std_any_get_pointer(v_ast_expr_list_t, any0);
    stmt_list = v_std_any_get_pointer(v_ast_stmt_list_t, any1);

    case_block = v_alloca(v_ast_base_t);
    v_initialize(case_block);

    v_ast_make_case_block(case_block, case_list, stmt_list);

    v_std_any_set_pointer(ret, case_block);
    v_terminate(case_block);
}


//---------------------------------------------------------------------
{
    //-----------------------------------------------------------------
    f = v_function_hack("mk_case_list_grammar_action", v_peg_grammar_action_fun_t);

    LLVMSetLinkage(f, LLVMPrivateLinkage);

    v_add_parameter_name(f, 0, "ret",       v_std_any_ptr);
    v_add_parameter_name(f, 1, "aux",       void_ptr);
    v_add_parameter_name(f, 2, "any0",      v_std_any_ptr);
    v_add_parameter_name(f, 3, "any_count", size_t);
}
{
    any1 = v_getelementptr(any0, 1);

    list = v_std_any_get_pointer(v_ast_generic_list_t, any0);
    item = v_std_any_get_pointer(v_ast_base_t,         any1);

    ret_list = v_alloca(v_ast_generic_list_t);
    v_initialize(ret_list);

    if (list)   v_list_append(ret_list, list, item);
    else        v_make_list(ret_list, case_list_q, item, 1);

    v_std_any_set_pointer(ret, ret_list);

    v_terminate(ret_list);
}


//---------------------------------------------------------------------
{
    //-----------------------------------------------------------------
    f = v_function_hack("mk_stmt_switch_grammar_action", v_peg_grammar_action_fun_t);

    LLVMSetLinkage(f, LLVMPrivateLinkage);

    v_add_parameter_name(f, 0, "ret",       v_std_any_ptr);
    v_add_parameter_name(f, 1, "aux",       void_ptr);
    v_add_parameter_name(f, 2, "any0",      v_std_any_ptr);
    v_add_parameter_name(f, 3, "any_count", size_t);
}
{
    any1 = v_getelementptr(any0, 1);

    arg  = v_std_any_get_pointer(v_ast_expr_t,         any0);
    list = v_std_any_get_pointer(v_ast_generic_list_t, any1);

    stmt = v_alloca(v_ast_stmt_t);
    v_initialize(stmt);

    v_ast_make_stmt_switch(stmt, arg, list);

    v_std_any_set_pointer(ret, stmt);

    v_terminate(stmt);
}


//---------------------------------------------------------------------
//- compile_ast_stmt_switch_t - visitor method
//---------------------------------------------------------------------
{
    //-----------------------------------------------------------------
    f = v_function_hack("compile_ast_stmt_switch_t", voidc_visitor_method_t);

    LLVMSetLinkage(f, LLVMPrivateLinkage);

    v_add_parameter_name(f, 0, "visitor", voidc_visitor_ptr);
    v_add_parameter_name(f, 1, "aux",     void_ptr);
    v_add_parameter_name(f, 2, "self",    v_ast_base_ptr);
}
{
    void_obj = v_ast_generic_get_object(v_cast(self, v_ast_stmt_ptr));

    obj = v_cast(void_obj, ast_stmt_switch_ptr);

    arg  = v_getelementptr(obj, 0, 0);
    list = v_getelementptr(obj, 0, 1);


    v_save_variables();


    //- Compile switch argument

    v_set_result_type(UNREFERENCE_TAG);
    v_set_result_value(0);

    arg_base = v_cast(arg, v_ast_base_ptr);

    v_push_temporaries();

    v_ast_accept_visitor(arg_base, visitor);

    v_pop_temporaries();

    typ0 = v_get_result_type();         //- switch argument...
    arg0 = v_get_result_value();        //- switch argument...


    //- Count switch cases

    case_num = v_alloca(size_t);    case_num = v_reference(case_num);
    v_assign(case_num, 0);

    list_count = v_list_get_size(list);

    case_idx = v_alloca(size_t);    case_idx = v_reference(case_idx);
    v_assign(case_idx, 0);

    loop
    {
        fin = v_icmp(LLVMIntEQ, case_idx, list_count);
        if (fin)  v_break();

        ast_base = v_list_get_item(list, case_idx);

        obj = v_ast_generic_get_object(ast_base);

        case_block = v_cast(obj, ast_case_block_ptr);

        case_list = v_getelementptr(case_block, 0, 0);

        n = v_list_get_size(case_list);

        num = v_binop(LLVMAdd, case_num, n);

        v_assign(case_num, num);

        idx = v_binop(LLVMAdd, case_idx, v_cast(1, size_t));

        v_assign(case_idx, idx);
    }


    //- Start...

    lltyp = v_alloca(LLVMTypeRef);

    builder = v_target_get_builder();

    llvm_stacksave_f   = v_obtain_function("llvm.stacksave", lltyp);
    llvm_stacksave_ft_ = v_load(lltyp);

    switch = LLVMBuildCall2(builder, llvm_stacksave_ft_, llvm_stacksave_f, 0, 0, "switch");


    i8_ptr  = v_pointer_type(v_int_type(8), 0);
    i8_ptr_ = v_type_get_llvm_type(i8_ptr);

    switch_leave_var_v = LLVMBuildAlloca(builder, i8_ptr_, "switch_leave_var_v");

    leave_bv = v_get_variable_value_q(voidc_internal_branch_target_leave_q);
    leave_b  = LLVMValueAsBasicBlock(leave_bv);

    cur_f = LLVMGetBasicBlockParent(leave_b);

    leave_b_addr = LLVMBlockAddress(cur_f, leave_b);

    LLVMBuildStore(builder, leave_b_addr, switch_leave_var_v);


    switch_leave_b  = LLVMAppendBasicBlock(cur_f, "switch_leave_b");
    switch_leave_bv = LLVMBasicBlockAsValue(switch_leave_b);

    v_add_variable_q(voidc_internal_branch_target_leave_q, 0, switch_leave_bv);         //- Sic!


    switch_exit_b = LLVMAppendBasicBlock(cur_f, "switch_exit_b");

    switch_exit_b_addr = LLVMBlockAddress(cur_f, switch_exit_b);


    v_add_variable_q(voidc_internal_branch_target_break_variable_q, 0, switch_leave_var_v);
    v_add_variable_q(voidc_internal_branch_target_break_q,          0, switch_exit_b_addr);


    //- Generate llvm's switch statement...

    switch_default_b = LLVMAppendBasicBlock(cur_f, "switch_default_b");

    switch_v = LLVMBuildSwitch(builder, arg0, switch_default_b, v_cast(case_num, unsigned));


    //- Compile case blocks...

    case_i = v_alloca(size_t);              case_i = v_reference(case_i);

    case_b = v_alloca(LLVMBasicBlockRef);   case_b = v_reference(case_b);

    v_assign(case_idx, 0);

    loop
    {
        fin = v_icmp(LLVMIntEQ, case_idx, list_count);
        if (fin)  v_break();

        ast_base = v_list_get_item(list, case_idx);

        obj = v_ast_generic_get_object(ast_base);

        case_block = v_cast(obj, ast_case_block_ptr);

        case_list = v_getelementptr(case_block, 0, 0);

        n = v_list_get_size(case_list);

        v_assign(case_b, 0);

        cur_b = LLVMGetInsertBlock(builder);

        v_assign(case_i, 0);

        loop
        {
            fin = v_icmp(LLVMIntEQ, case_i, n);
            if (fin)  v_break();

            case_arg_i = v_list_get_item(case_list, case_i);

            emp = v_empty(case_arg_i);

            if (emp)        //- default case
            {
                v_assign(case_b, switch_default_b);

                LLVMMoveBasicBlockAfter(switch_default_b, cur_b);
            }

            i = v_binop(LLVMAdd, case_i, v_cast(1, size_t));

            v_assign(case_i, i);
        }

        if (case_b) ;       //- Have default case
        else                //- All "normal" cases
        {
            b = LLVMAppendBasicBlock(cur_f, "case_b");

            v_assign(case_b, b);
        }

        trm_v = LLVMGetBasicBlockTerminator(cur_b);
        if (trm_v) ;
        else
        {
            LLVMBuildBr(builder, case_b);
        }


        LLVMPositionBuilderAtEnd(builder, case_b);

        stmt_list = v_getelementptr(case_block, 0, 1);

        base_ast = v_cast(stmt_list, v_ast_base_ptr);

        v_ast_accept_visitor(base_ast, visitor);


        v_assign(case_i, 0);

        loop
        {
            fin = v_icmp(LLVMIntEQ, case_i, n);
            if (fin)  v_break();

            case_arg_i = v_list_get_item(case_list, case_i);

            i = v_binop(LLVMAdd, case_i, v_cast(1, size_t));

            v_assign(case_i, i);

            emp = v_empty(case_arg_i);

            if (emp)  v_continue();     //- default case

            //- "Normal" case

            v_set_result_type(typ0);
            v_set_result_value(0);

            base_ast = v_cast(case_arg_i, v_ast_base_ptr);

            v_ast_accept_visitor(base_ast, visitor);

            val = v_get_result_value();

            LLVMAddCase(switch_v, val, case_b);
        }

        idx = v_binop(LLVMAdd, case_idx, v_cast(1, size_t));

        v_assign(case_idx, idx);
    }


    //- Finish...

    cur_b = LLVMGetInsertBlock(builder);

    if (LLVMGetBasicBlockTerminator(cur_b)) ;
    else
    {
        voidc_intrinsic_break_helper(switch_leave_var_v, switch_exit_b_addr);
    }

    if (LLVMGetBasicBlockTerminator(switch_default_b)) ;
    else
    {
        LLVMPositionBuilderAtEnd(builder, switch_default_b);

        voidc_intrinsic_break_helper(switch_leave_var_v, switch_exit_b_addr);
    }


    LLVMMoveBasicBlockAfter(switch_leave_b, cur_b);


    LLVMPositionBuilderAtEnd(builder, switch_leave_b);


    leave_addr = LLVMBuildLoad2(builder, i8_ptr_, switch_leave_var_v, "leave_addr");


    val0 = v_alloca(LLVMValueRef);

    v_store(switch, val0);

    llvm_stackrestore_f   = v_obtain_function("llvm.stackrestore", lltyp);
    llvm_stackrestore_ft_ = v_load(lltyp);

    LLVMBuildCall2(builder, llvm_stackrestore_ft_, llvm_stackrestore_f, val0, 1, "");


    ibr_insn = LLVMBuildIndirectBr(builder, leave_addr, 2);

    LLVMAddDestination(ibr_insn, leave_b);
    LLVMAddDestination(ibr_insn, switch_exit_b);


    LLVMMoveBasicBlockAfter(switch_exit_b, switch_leave_b);


    LLVMPositionBuilderAtEnd(builder, switch_exit_b);


    v_restore_variables();
}


//---------------------------------------------------------------------
//- Enable statement switch compilation
//---------------------------------------------------------------------
{
    break_pq = v_quark_ptr_from_string("v_break");

    v_add_symbol("break_q", v_reference_type(v_quark_t, 0), break_pq);

    break_intrinsic = v_get_intrinsic_q(v_load(break_pq), 0);

    v_add_symbol("v_break_intrinsic", voidc_intrinsic_t, break_intrinsic);


    ft = v_function_type(void, 0, 0, false);

    //-----------------------------------------------------------------
    v_function_hack("voidc_enable_statement_switch_compiler", ft);
}
{
    vis = v_get_compiler();

    v_return_if(voidc_visitor_get_void_method(vis, quark, 0));


    v_store(quark,        v_getelementptr(voidc_ast_stmt_switch_vtable, 0, 2));
    v_store(case_block_q, v_getelementptr(voidc_ast_case_block_vtable,  0, 2));

    voidc_visitor_set_void_method(vis, vis, quark, compile_ast_stmt_switch_t, 0);

    v_export_intrinsic_q(break_q, v_break_intrinsic, 0);                        //- Re-export from "stmt_block" (?)
}


//---------------------------------------------------------------------
//- Enable statement switch
//---------------------------------------------------------------------
{
    ft = v_function_type(void, 0, 0, false);

    //-----------------------------------------------------------------
    v_function_hack("voidc_enable_statement_switch", ft);
}
{
    voidc_enable_statement_switch_compiler();


    v_return_if_not(voidc_has_grammar());
    v_return_if(voidc_grammar_has_parser("stmt_switch"));


    gr0 = v_alloca(v_peg_grammar_t);
    v_initialize(gr0);

    v_peg_get_grammar(gr0);


    grammar gr0
    {
    actions:
        mk_case_item   = mk_case_item_grammar_action;
        mk_case_block  = mk_case_block_grammar_action;
        mk_case_list   = mk_case_list_grammar_action;
        mk_stmt_switch = mk_stmt_switch_grammar_action;

    parsers:

        stmt_switch = "switch" _ '(' _ e:expr _ ')' _ '{' _ l:case_list _ '}'    { mk_stmt_switch(e, l) };


        case_list = l:case_list _ b:case_block      { mk_case_list(l, b) }
                  / b:case_block                    { mk_case_list(0, b) };

        case_list is left-recursive;


        case_block = c:case_item_list _ s:case_stmt_list    { mk_case_block(c, s) };


        case_item_list = l:case_item_list _ i:case_item     { mk_expr_list(l, i) }
                       / i:case_item                        { mk_expr_list(0, i) };

        case_item_list is left-recursive;


        case_item = "case" !ident_cont _ e:expr _ ':'   { mk_case_item(e) }
                  / "default" _ ':'                     { mk_case_item(0) };


        case_stmt_list = l:case_stmt_list _ i:case_stmt     { mk_stmt_list(l, i) }
                       / i:case_stmt                        { mk_stmt_list(0, i) };

        case_stmt_list is left-recursive;

        case_stmt = !("default" _ ':')  stmt;


        stmt += stmt_switch;
    }


    v_peg_set_grammar(gr0);


    v_terminate(gr0);
}


//---------------------------------------------------------------------
//- ...
//---------------------------------------------------------------------
{
    module = v_get_module();

    //-----------------------------------------------------------------
//  v_debug_print_module(1);

    voidc_unit_load_module_to_jit(module, true);

    LLVMDisposeModule(module);
    v_set_module(0);
}


//---------------------------------------------------------------------
{
    v_make_export_symbol("v_ast_make_stmt_switch");
    v_make_export_symbol("v_ast_make_case_block");

    v_make_export_symbol("voidc_enable_statement_switch_compiler");
    v_make_export_symbol("voidc_enable_statement_switch");
}


