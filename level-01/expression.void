//---------------------------------------------------------------------
//- Copyright (C) 2020-2022 Dmitry Borodkin <borodkin.dn@gmail.com>
//- SDPX-License-Identifier: LGPL-3.0-or-later
//---------------------------------------------------------------------
{   voidc_guard_target("expression.void  must be imported into the voidc target only!");

    v_import("level-00");

    v_import("llvm-c/Core.void");

    v_import("function_hack.void");
    v_import("memory.void");
    v_import("if_then_else.void");
    v_import("arithmetic.void");
    v_import("block.void");
    v_import("loop.void");
    v_import("grammar.void");

    v_import("expr_util.void");
}

{   voidc_enable_statement_if_then_else();
    voidc_enable_statement_block();
    voidc_enable_statement_loop();
    voidc_enable_statement_grammar();
}

//{ v_import("printf.void"); }


//---------------------------------------------------------------------
{   v_add_type("void_ptr", v_pointer_type(void, 0));

    v_add_type("quark_ptr", v_pointer_type(v_quark_t, 0));
}


//---------------------------------------------------------------------
{   module = LLVMModuleCreateWithName("voidc_expression_mod");

    v_set_module(module);

    //-----------------------------------------------------------------
    LLVMDisposeMemoryBuffer(voidc_get_unit_buffer());
    voidc_set_unit_buffer(0);
}


//---------------------------------------------------------------------
//- make_expr_op_helper - ...
//---------------------------------------------------------------------
{
    typ0 = v_alloca(v_type_ptr, 3);
    typ1 = v_getelementptr(typ0, 1);
    typ2 = v_getelementptr(typ0, 2);

    v_store(v_ast_expr_ptr,      typ0);
    v_store(v_quark_t,           typ1);
    v_store(v_ast_expr_list_ptr, typ2);

    ft = v_function_type(void, typ0, 3, false);

    //-----------------------------------------------------------------
    f = v_function_hack("make_expr_op_helper", ft);

    LLVMSetLinkage(f, LLVMPrivateLinkage);

    v_add_parameter_name(f, 0, "ret",   v_ast_expr_ptr);
    v_add_parameter_name(f, 1, "quark", v_quark_t);
    v_add_parameter_name(f, 2, "args",  v_ast_expr_list_ptr);
}
{
    sstr = v_alloca(v_std_string_t);
    v_initialize(sstr);

    v_std_string_set(sstr, "v.");

    etmp = v_alloca(v_ast_expr_t);
    v_initialize(etmp);

    v_std_string_append(sstr, v_quark_to_string(quark));

    v_ast_make_expr_identifier(etmp, v_std_string_get(sstr));

    v_ast_make_expr_call(etmp, etmp, args);

    v_copy(ret, etmp);

    v_terminate(etmp);
    v_terminate(sstr);
}

//---------------------------------------------------------------------
//- v_ast_make_expr_op_unary - ...
//---------------------------------------------------------------------
{
    typ0 = v_alloca(v_type_ptr, 3);
    typ1 = v_getelementptr(typ0, 1);
    typ2 = v_getelementptr(typ0, 2);

    v_store(v_ast_expr_ptr, typ0);
    v_store(v_ast_expr_ptr, typ1);
    v_store(v_ast_expr_ptr, typ2);

    ft = v_function_type(void, typ0, 3, false);

    //-----------------------------------------------------------------
    f = v_function_hack("v_ast_make_expr_op_unary", ft);

    v_add_parameter_name(f, 0, "ret",   v_ast_expr_ptr);
    v_add_parameter_name(f, 1, "op",    v_ast_expr_ptr);
    v_add_parameter_name(f, 2, "arg",   v_ast_expr_ptr);
}
{
    lst = v_alloca(v_ast_expr_list_t);
    v_initialize(lst);

    v_make_list(lst, op, 1);

    v_list_append(lst, lst, arg, 1);

    void_obj = v_ast_generic_get_object(op);

    obj = v_cast(void_obj, v_ast_operator_unary_ptr);

    quark = v_load(v_getelementptr(obj, 0, 0));

    make_expr_op_helper(ret, quark, lst);

    v_terminate(lst);
}

//---------------------------------------------------------------------
//- v_ast_make_expr_op_binary - ...
//---------------------------------------------------------------------
{
    typ0 = v_alloca(v_type_ptr, 4);
    typ1 = v_getelementptr(typ0, 1);
    typ2 = v_getelementptr(typ0, 2);
    typ3 = v_getelementptr(typ0, 3);

    v_store(v_ast_expr_ptr, typ0);
    v_store(v_ast_expr_ptr, typ1);
    v_store(v_ast_expr_ptr, typ2);
    v_store(v_ast_expr_ptr, typ3);

    ft = v_function_type(void, typ0, 4, false);

    //-----------------------------------------------------------------
    f = v_function_hack("v_ast_make_expr_op_binary", ft);

    v_add_parameter_name(f, 0, "ret",   v_ast_expr_ptr);
    v_add_parameter_name(f, 1, "op",    v_ast_expr_ptr);
    v_add_parameter_name(f, 2, "lhs",   v_ast_expr_ptr);
    v_add_parameter_name(f, 3, "rhs",   v_ast_expr_ptr);
}
{
    lst = v_alloca(v_ast_expr_list_t);
    v_initialize(lst);

    v_make_list(lst, op, 1);

    v_list_append(lst, lst, lhs, 1);
    v_list_append(lst, lst, rhs, 1);

    void_obj = v_ast_generic_get_object(op);

    obj = v_cast(void_obj, v_ast_operator_binary_ptr);

    quark = v_load(v_getelementptr(obj, 0, 0));

    make_expr_op_helper(ret, quark, lst);

    v_terminate(lst);
}


//---------------------------------------------------------------------
//- Precedence climbing algorithm...
//---------------------------------------------------------------------
//- compute_expr(min_prec):
//-   result = compute_atom()
//-
//-   while cur token is a binary operator with precedence >= min_prec:
//-     prec, assoc = precedence and associativity of current token
//-     if assoc is left:
//-       next_min_prec = prec + 1
//-     else:
//-       next_min_prec = prec
//-     rhs = compute_expr(next_min_prec)
//-     result = compute operator(result, rhs)
//-
//-   return result
//---------------------------------------------------------------------


//---------------------------------------------------------------------
//- Global variable(s)
//---------------------------------------------------------------------
{
    module = v_get_module();

    //-----------------------------------------------------------------
    //- precedence_stack_node_t ...
    //-----------------------------------------------------------------
    precedence_stack_node_t = v_struct_type_named("struct.precedence_stack_node_t");

    precedence_stack_node_ptr  = v_pointer_type(precedence_stack_node_t, 0);
    precedence_stack_node_ptr_ = v_type_get_llvm_type(precedence_stack_node_ptr);

    typ0 = v_alloca(v_type_ptr, 2);
    typ1 = v_getelementptr(typ0, 1);

    v_store(int,                       typ0);       //- Precedence value
    v_store(precedence_stack_node_ptr, typ1);       //- Next node ...

    v_type_struct_set_body(precedence_stack_node_t, typ0, 2, 0);

    //-----------------------------------------------------------------
    stack_g = LLVMAddGlobal(module, precedence_stack_node_ptr_, "precedence_stack");

    stack_u = LLVMGetUndef(precedence_stack_node_ptr_);

    LLVMSetInitializer(stack_g, stack_u);

    //-----------------------------------------------------------------
    v_add_type("precedence_stack_node_t",   precedence_stack_node_t);
    v_add_type("precedence_stack_node_ptr", precedence_stack_node_ptr);

    v_add_symbol("precedence_stack", precedence_stack_node_ptr, 0);       //- Sic!

    //-----------------------------------------------------------------
    LLVMDisposeMemoryBuffer(voidc_get_unit_buffer());
    voidc_set_unit_buffer(0);
}


//---------------------------------------------------------------------
//- Precedence stack utility...
//---------------------------------------------------------------------
{
    typ0 = v_alloca(v_type_ptr);

    v_store(int, typ0);

    ft = v_function_type(void, typ0, 1, false);

    //-----------------------------------------------------------------
    f = v_function_hack("precedence_stack_push", ft);

    LLVMSetLinkage(f, LLVMPrivateLinkage);

    v_add_parameter_name(f, 0, "prec", int);
}
{
//  printf("precedence_stack_push(%d)\n", prec);

    node = v_malloc(precedence_stack_node_t);

    p_prec = v_getelementptr(node, 0, 0);
    p_next = v_getelementptr(node, 0, 1);

    cur = v_load(precedence_stack);

    v_store(prec, p_prec);
    v_store(cur,  p_next);

    v_store(node, precedence_stack);
}

//---------------------------------------------------------------------
{
    ft = v_function_type(int, 0, 0, false);

    //-----------------------------------------------------------------
    f = v_function_hack("precedence_stack_pop", ft);

    LLVMSetLinkage(f, LLVMPrivateLinkage);
}
{
    node = v_load(precedence_stack);

    p_prec = v_getelementptr(node, 0, 0);
    p_next = v_getelementptr(node, 0, 1);

    next = v_load(p_next);

    v_store(next, precedence_stack);

    prec = v_load(p_prec);

    v_free(node);

//  printf("precedence_stack_pop: %d\n", prec);

    v_return(prec);
}

//---------------------------------------------------------------------
{
    ft = v_function_type(int, 0, 0, false);

    //-----------------------------------------------------------------
    f = v_function_hack("precedence_stack_peek", ft);

    LLVMSetLinkage(f, LLVMPrivateLinkage);
}
{
    node = v_load(precedence_stack);

    p_prec = v_getelementptr(node, 0, 0);

    prec = v_load(p_prec);

    v_return(prec);
}


//---------------------------------------------------------------------
//- expr_start - grammar action
//---------------------------------------------------------------------
{
    //-----------------------------------------------------------------
    f = v_function_hack("expr_start_grammar_action", v_peg_grammar_action_fun_t);

    LLVMSetLinkage(f, LLVMPrivateLinkage);

    v_add_parameter_name(f, 0, "ret",       v_std_any_ptr);
    v_add_parameter_name(f, 1, "any0",      v_std_any_ptr);
    v_add_parameter_name(f, 2, "any_count", size_t);
}
{
//  printf("expr_start_grammar_action\n");

    precedence_stack_push(0);

    v_std_any_set_value(ret, 1);
}

//---------------------------------------------------------------------
//- expr_stop - grammar action
//---------------------------------------------------------------------
{
    //-----------------------------------------------------------------
    f = v_function_hack("expr_stop_grammar_action", v_peg_grammar_action_fun_t);

    LLVMSetLinkage(f, LLVMPrivateLinkage);

    v_add_parameter_name(f, 0, "ret",       v_std_any_ptr);
    v_add_parameter_name(f, 1, "any0",      v_std_any_ptr);
    v_add_parameter_name(f, 2, "any_count", size_t);
}
{
//  printf("expr_stop_grammar_action\n");

    precedence_stack_pop();

    v_copy(ret, any0);
}

//---------------------------------------------------------------------
//- expr_cleanup - grammar action
//---------------------------------------------------------------------
{
    //-----------------------------------------------------------------
    f = v_function_hack("expr_cleanup_grammar_action", v_peg_grammar_action_fun_t);

    LLVMSetLinkage(f, LLVMPrivateLinkage);

    v_add_parameter_name(f, 0, "ret",       v_std_any_ptr);
    v_add_parameter_name(f, 1, "any0",      v_std_any_ptr);
    v_add_parameter_name(f, 2, "any_count", size_t);
}
{
//  printf("expr_cleanup_grammar_action\n");

    precedence_stack_pop();

    //- Just fail!
}


//---------------------------------------------------------------------
//- expr_check_infix - grammar action
//---------------------------------------------------------------------
{
    //-----------------------------------------------------------------
    f = v_function_hack("expr_check_infix_grammar_action", v_peg_grammar_action_fun_t);

    LLVMSetLinkage(f, LLVMPrivateLinkage);

    v_add_parameter_name(f, 0, "ret",       v_std_any_ptr);
    v_add_parameter_name(f, 1, "any0",      v_std_any_ptr);
    v_add_parameter_name(f, 2, "any_count", size_t);
}
{
//  printf("expr_check_infix_grammar_action\n");

    min_prec = precedence_stack_peek();

    op = v_std_any_get_pointer(v_ast_expr_t, any0);

    void_obj = v_ast_generic_get_object(op);

    obj = v_cast(void_obj, v_ast_operator_binary_ptr);

    prec = v_load(v_getelementptr(obj, 0, 1));
    left = v_load(v_getelementptr(obj, 0, 2));


    if (v_icmp(LLVMIntULT, prec, min_prec)) v_return();     //- NOK

    precedence_stack_push(v_binop(LLVMAdd, prec, left));

    v_std_any_set_value(ret, 1);                            //- OK
}


//---------------------------------------------------------------------
//- expr_infix - grammar action
//---------------------------------------------------------------------
//{   v_debug_print_module(1); }
{
    //-----------------------------------------------------------------
    f = v_function_hack("expr_infix_grammar_action", v_peg_grammar_action_fun_t);

    LLVMSetLinkage(f, LLVMPrivateLinkage);

    v_add_parameter_name(f, 0, "ret",       v_std_any_ptr);
    v_add_parameter_name(f, 1, "any0",      v_std_any_ptr);
    v_add_parameter_name(f, 2, "any_count", size_t);
}
{
//  printf("expr_infix_grammar_action\n");

    any1 = v_getelementptr(any0, 1);
    any2 = v_getelementptr(any0, 2);

    op  = v_std_any_get_pointer(v_ast_expr_t, any0);
    lhs = v_std_any_get_pointer(v_ast_expr_t, any1);
    rhs = v_std_any_get_pointer(v_ast_expr_t, any2);

    expr = v_alloca(v_ast_expr_t);
    v_initialize(expr);

    v_ast_make_expr_op_binary(expr, op, lhs, rhs);

    v_std_any_set_pointer(ret, expr);

    v_terminate(expr);

    precedence_stack_pop();
}


//---------------------------------------------------------------------
//- expr_make_postfix - grammar action
//---------------------------------------------------------------------
{
    quark_ref = v_reference_type(v_quark_t, 0);

    q = v_quark_ptr_from_string;

    v_add_symbol("op_postfix_call_q",  quark_ref, q("op_postfix_call"));
    v_add_symbol("op_postfix_index_q", quark_ref, q("op_postfix_index"));


    //-----------------------------------------------------------------
    f = v_function_hack("expr_make_postfix_grammar_action", v_peg_grammar_action_fun_t);

    LLVMSetLinkage(f, LLVMPrivateLinkage);

    v_add_parameter_name(f, 0, "ret",       v_std_any_ptr);
    v_add_parameter_name(f, 1, "any0",      v_std_any_ptr);
    v_add_parameter_name(f, 2, "any_count", size_t);
}
{
//  printf("expr_make_postfix_grammar_action\n");

    min_prec = precedence_stack_peek();

    any1 = v_getelementptr(any0, 1);

    op = v_std_any_get_pointer(v_ast_expr_t, any1);

    void_obj = v_ast_generic_get_object(op);

    obj = v_cast(void_obj, v_ast_operator_unary_ptr);

    prec = v_load(v_getelementptr(obj, 0, 1));

    if (v_icmp(LLVMIntULT, prec, min_prec))
    {
        v_return();                                         //- NOK
    }

    expr = v_alloca(v_ast_expr_t);
    v_initialize(expr);

    lhs = v_std_any_get_pointer(v_ast_expr_t, any0);

    q = v_load(v_getelementptr(obj, 0, 0));

    if (v_icmp(LLVMIntEQ, q, op_postfix_call_q))
    {
        //- Special case!

        pay = v_getelementptr(obj, 0, 2);

        arg_list = v_cast(pay, v_ast_expr_list_ptr);

        v_ast_make_expr_call(expr, lhs, arg_list);
    }
    else
    {
        v_ast_make_expr_op_unary(expr, op, lhs);
    }

    v_std_any_set_pointer(ret, expr);                       //- OK

    v_terminate(expr);
}


//---------------------------------------------------------------------
//- expr_push_prefix - grammar action
//---------------------------------------------------------------------
{
    //-----------------------------------------------------------------
    f = v_function_hack("expr_push_prefix_grammar_action", v_peg_grammar_action_fun_t);

    LLVMSetLinkage(f, LLVMPrivateLinkage);

    v_add_parameter_name(f, 0, "ret",       v_std_any_ptr);
    v_add_parameter_name(f, 1, "any0",      v_std_any_ptr);
    v_add_parameter_name(f, 2, "any_count", size_t);
}
{
//  printf("expr_push_prefix_grammar_action\n");

    op = v_std_any_get_pointer(v_ast_expr_t, any0);

    void_obj = v_ast_generic_get_object(op);

    obj = v_cast(void_obj, v_ast_operator_unary_ptr);

    prec = v_load(v_getelementptr(obj, 0, 1));

    precedence_stack_push(prec);

    v_std_any_set_value(ret, 1);
}


//---------------------------------------------------------------------
//- expr_prefix - grammar action
//---------------------------------------------------------------------
{
    //-----------------------------------------------------------------
    f = v_function_hack("expr_prefix_grammar_action", v_peg_grammar_action_fun_t);

    LLVMSetLinkage(f, LLVMPrivateLinkage);

    v_add_parameter_name(f, 0, "ret",       v_std_any_ptr);
    v_add_parameter_name(f, 1, "any0",      v_std_any_ptr);
    v_add_parameter_name(f, 2, "any_count", size_t);
}
{
//  printf("expr_prefix_grammar_action\n");

    any1 = v_getelementptr(any0, 1);

    op  = v_std_any_get_pointer(v_ast_expr_t, any0);
    rhs = v_std_any_get_pointer(v_ast_expr_t, any1);

    expr = v_alloca(v_ast_expr_t);
    v_initialize(expr);

    v_ast_make_expr_op_unary(expr, op, rhs);

    v_std_any_set_pointer(ret, expr);

    v_terminate(expr);

    precedence_stack_pop();
}


//---------------------------------------------------------------------
//- mk_bracketed - grammar action
//---------------------------------------------------------------------
{
    //-----------------------------------------------------------------
    f = v_function_hack("mk_bracketed_grammar_action", v_peg_grammar_action_fun_t);

    LLVMSetLinkage(f, LLVMPrivateLinkage);

    v_add_parameter_name(f, 0, "ret",       v_std_any_ptr);
    v_add_parameter_name(f, 1, "any0",      v_std_any_ptr);
    v_add_parameter_name(f, 2, "any_count", size_t);
}
{
//  printf("mk_bracketed_grammar_action\n");

    any1 = v_getelementptr(any0, 1);

    op  = v_std_any_get_pointer(v_ast_expr_t, any0);
    rhs = v_std_any_get_pointer(v_ast_expr_t, any1);

    expr = v_alloca(v_ast_expr_t);
    v_initialize(expr);

    v_ast_make_expr_op_unary(expr, op, rhs);

    v_std_any_set_pointer(ret, expr);

    v_terminate(expr);
}


//---------------------------------------------------------------------
//- mk_op_apply - grammar action
//---------------------------------------------------------------------
{
    //-----------------------------------------------------------------
    f = v_function_hack("mk_op_apply_grammar_action", v_peg_grammar_action_fun_t);

    LLVMSetLinkage(f, LLVMPrivateLinkage);

    v_add_parameter_name(f, 0, "ret",       v_std_any_ptr);
    v_add_parameter_name(f, 1, "any0",      v_std_any_ptr);
    v_add_parameter_name(f, 2, "any_count", size_t);
}
{
//  printf("mk_op_apply_grammar_action\n");

    any1 = v_getelementptr(any0, 1);

    op0  = v_std_any_get_pointer(v_ast_expr_t,      any0);
    args = v_std_any_get_pointer(v_ast_expr_list_t, any1);


    void_obj = v_ast_generic_get_object(op0);

    obj = v_cast(void_obj, v_ast_operator_unary_ptr);

    quark = v_load(v_getelementptr(obj, 0, 0));
    prec  = v_load(v_getelementptr(obj, 0, 1));


    pay = v_cast(args, v_ast_base_ptr);


    op1 = v_alloca(v_ast_expr_t);
    v_initialize(op1);

    v_ast_make_operator_unary(op1, quark, prec, pay);


    v_std_any_set_pointer(ret, op1);

    v_terminate(op1);
}


//---------------------------------------------------------------------
//- Enable expression
//---------------------------------------------------------------------
{
    ft = v_function_type(void, 0, 0, false);

    //-----------------------------------------------------------------
    v_function_hack("voidc_enable_expression_compiler", ft);
}
{
    v_return_if(voidc_visitor_get_intrinsic(voidc_compiler, "v.op_infix_assign", 0));

    voidc_ast_add_operator_methods();
}


//---------------------------------------------------------------------
//- Enable expression
//---------------------------------------------------------------------
{
    ft = v_function_type(void, 0, 0, false);

    //-----------------------------------------------------------------
    v_function_hack("voidc_enable_expression", ft);
}
{
    voidc_enable_expression_compiler();


    v_return_if_not(voidc_has_grammar());
    v_return_if(voidc_grammar_has_parser("expr_lr"));


    gr0 = v_alloca(v_peg_grammar_t);
    v_initialize(gr0);

    v_peg_get_grammar(gr0);

    {   any = v_alloca(v_std_any_t);
        v_initialize(any);


        count = v_list_get_size(voidc_ast_expression_operator_list);
        count = v_cast(count, int);

        i = v_reference(v_alloca(int));

        v_assign(i, 0);

        loop
        {
            if (v_icmp(LLVMIntEQ, i, count))  v_break();


            op = v_list_get_item(voidc_ast_expression_operator_list, i);

            void_obj = v_ast_generic_get_object(op);

            quark = v_load(v_cast(void_obj, quark_ptr));        //- Sic!!!

            name = v_quark_to_string(quark);

            v_std_any_set_pointer(any, op);


            v_peg_grammar_set_value(gr0, gr0, name, any);


            v_assign(i, v_binop(LLVMAdd, i, 1));
        }

        v_terminate(any);
    }

    grammar gr0
    {
    actions:
        expr_start        = expr_start_grammar_action;
        expr_stop         = expr_stop_grammar_action;
        expr_cleanup      = expr_cleanup_grammar_action;
        expr_check_infix  = expr_check_infix_grammar_action;
        expr_infix        = expr_infix_grammar_action;
        expr_make_postfix = expr_make_postfix_grammar_action;
        expr_push_prefix  = expr_push_prefix_grammar_action;
        expr_prefix       = expr_prefix_grammar_action;

        mk_bracketed = mk_bracketed_grammar_action;

        mk_op_apply = mk_op_apply_grammar_action;

    parsers:

        expr = { expr_start() }  e:expr_ck  { expr_stop(e) };

        expr_ck = expr_lr / { expr_cleanup() };

        expr_lr = l:expr_lr _ op:infix _ { expr_check_infix(op) }  r:expr_ck  { expr_infix(op, l, r) }

                / l:expr_lr _ op:postfix { expr_make_postfix(l, op) }

                / op:prefix _ { expr_push_prefix(op) }  r:expr_ck  { expr_prefix(op, r) }

                / prim;

        expr_lr is left-recursive;


        prim += bracketed
              ;

        infix = "+="        { op_infix_add_assign }
              / '+' !'+'    { op_infix_add }
              / "-="        { op_infix_sub_assign }
              / '-' !'-'    { op_infix_sub }
              / "*="        { op_infix_mul_assign }
              / '*'         { op_infix_mul }
              / "/="        { op_infix_div_assign }
              / '/'         { op_infix_div }
              / "%="        { op_infix_rem_assign }
              / '%'         { op_infix_rem }
              / ":="        { op_infix_assign }
              / "<="        { op_infix_le }
              / "<<="       { op_infix_shl_assign }
              / "<<"        { op_infix_shl }
              / '<'         { op_infix_lt }
              / ">="        { op_infix_ge }
              / ">>="       { op_infix_shr_assign }
              / ">>"        { op_infix_shr }
              / '>'         { op_infix_gt }
              / "=="        { op_infix_eq }
              / "!="        { op_infix_ne }
              / "&&"        { op_infix_logical_and }
              / "&="        { op_infix_and_assign }
              / '&'         { op_infix_and }
              / "^="        { op_infix_xor_assign }
              / '^'         { op_infix_xor }
              / "||"        { op_infix_logical_or }
              / "|="        { op_infix_or_assign }
              / '|'         { op_infix_or }
              ;

        postfix = "++"      { op_postfix_inc }
                / "--"      { op_postfix_dec }

                / '(' _ a:expr_list _ ')'   { mk_op_apply(op_postfix_call, a) }
                / '[' _ a:expr_list _ ']'   { mk_op_apply(op_postfix_index, a) }
                ;

        prefix = "++"       { op_prefix_inc }
               / '+' ![0-9] { op_prefix_plus }
               / "--"       { op_prefix_dec }
               / '-' ![0-9] { op_prefix_minus }
               / '*'        { op_prefix_star }
               / '&'        { op_prefix_amp }
               / '!'        { op_prefix_bang }
               / '~'        { op_prefix_tilda }
               ;

        bracketed = '(' _ e:expr _ ')'      { mk_bracketed(op_parentheses, e) }     //- TODO...
                  ;
    }

    v_peg_set_grammar(gr0);


    v_terminate(gr0);
}


//---------------------------------------------------------------------
//- ...
//---------------------------------------------------------------------
{
    module = v_get_module();

    //-----------------------------------------------------------------
//  v_debug_print_module(1);

    voidc_unit_load_module_to_jit(module);
    voidc_add_module_to_jit(module);

    LLVMDisposeModule(module);
    v_set_module(0);
}


//---------------------------------------------------------------------
{
    v_export_type("v_ast_operator_unary_t",    v_ast_operator_unary_t);         //- From "expr_util.void"
    v_export_type("v_ast_operator_unary_ptr",  v_ast_operator_unary_ptr);       //- From "expr_util.void"
    v_export_type("v_ast_operator_binary_t",   v_ast_operator_binary_t);        //- From "expr_util.void"
    v_export_type("v_ast_operator_binary_ptr", v_ast_operator_binary_ptr);      //- From "expr_util.void"

    v_make_export_symbol("v_ast_make_operator_binary");                         //- From "expr_util.void"
    v_make_export_symbol("v_ast_make_operator_unary");                          //- From "expr_util.void"

    v_make_export_symbol("v_ast_make_expr_op_binary");
    v_make_export_symbol("v_ast_make_expr_op_unary");

    v_make_export_symbol("voidc_enable_expression_compiler");
    v_make_export_symbol("voidc_enable_expression");
}



