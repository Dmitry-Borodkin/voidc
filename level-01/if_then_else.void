//---------------------------------------------------------------------
//- Copyright (C) 2020-2022 Dmitry Borodkin <borodkin.dn@gmail.com>
//- SDPX-License-Identifier: LGPL-3.0-or-later
//---------------------------------------------------------------------
{   voidc_guard_target("if_then_else.void  must be imported into the voidc target only!");

    v_import("level-00");

    v_import("llvm-c/Core.void");

    v_import("function_hack.void");
}


{   v_add_type("void_ptr", v_pointer_type(void, 0));
}


//---------------------------------------------------------------------
{   typ0 = v_alloca(v_type_ptr, 3);
    typ1 = v_getelementptr(typ0, 1);
    typ2 = v_getelementptr(typ0, 2);


    //-----------------------------------------------------------------
    //- ast_stmt_if_then_else_t ...
    //-----------------------------------------------------------------
    ast_stmt_if_then_else_t = v_struct_type_named("struct.ast_stmt_if_then_else_t");

    v_store(v_ast_expr_t, typ0);
    v_store(v_ast_stmt_t, typ1);
    v_store(v_ast_stmt_t, typ2);

    v_type_struct_set_body(ast_stmt_if_then_else_t, typ0, 3, false);

    ast_stmt_if_then_else_ptr = v_pointer_type(ast_stmt_if_then_else_t, 0);

    v_export_type("v_ast_stmt_if_then_else_t", ast_stmt_if_then_else_t);

    v_add_type("ast_stmt_if_then_else_t",   ast_stmt_if_then_else_t);
    v_add_type("ast_stmt_if_then_else_ptr", ast_stmt_if_then_else_ptr);

    size_v = LLVMSizeOf(v_type_get_llvm_type(ast_stmt_if_then_else_t));

    v_add_constant("ast_stmt_if_then_else_t_size", size_t, size_v);
}


//---------------------------------------------------------------------
{   module = LLVMModuleCreateWithName("my_stmt_if_then_else_mod");

    v_set_module(module);

    //-----------------------------------------------------------------
    LLVMDisposeMemoryBuffer(voidc_get_unit_buffer());
    voidc_set_unit_buffer(0);
}


//---------------------------------------------------------------------
//- quark ...
//---------------------------------------------------------------------
{
    quark_name = "stmt_if_then_else";

    quark_p = v_quark_ptr_from_string(quark_name);

    v_add_symbol("quark", v_reference_type(v_quark_t, 0), quark_p);
}


//---------------------------------------------------------------------
//- init ...
//---------------------------------------------------------------------
{
    //-----------------------------------------------------------------
    f = v_function_hack("voidc_ast_stmt_if_then_else_init", v_ast_generic_init_fun_t);

    LLVMSetLinkage(f, LLVMPrivateLinkage);

    v_add_parameter_name(f, 0, "void_obj", void_ptr);
}
{
    obj = v_cast(void_obj, ast_stmt_if_then_else_ptr);

    condition = v_getelementptr(obj, 0, 0);
    stmt_then = v_getelementptr(obj, 0, 1);
    stmt_else = v_getelementptr(obj, 0, 2);

    v_initialize(condition);
    v_initialize(stmt_then);
    v_initialize(stmt_else);
}


//---------------------------------------------------------------------
//- term ...
//---------------------------------------------------------------------
{
    //-----------------------------------------------------------------
    f = v_function_hack("voidc_ast_stmt_if_then_else_term", v_ast_generic_term_fun_t);

    LLVMSetLinkage(f, LLVMPrivateLinkage);

    v_add_parameter_name(f, 0, "void_obj", void_ptr);
}
{
    obj = v_cast(void_obj, ast_stmt_if_then_else_ptr);

    condition = v_getelementptr(obj, 0, 0);
    stmt_then = v_getelementptr(obj, 0, 1);
    stmt_else = v_getelementptr(obj, 0, 2);

    v_terminate(condition);
    v_terminate(stmt_then);
    v_terminate(stmt_else);
}


//-----------------------------------------------------------------------
//- vtable ...
//-------------------------------------------------------------------
{
    module = v_get_module();

    val0 = v_alloca(LLVMValueRef, 3);
    val1 = v_getelementptr(val0, 1);
    val2 = v_getelementptr(val0, 2);

    init_f   = v_obtain_function("voidc_ast_stmt_if_then_else_init");
    term_f   = v_obtain_function("voidc_ast_stmt_if_then_else_term");
    quark_u  = LLVMGetUndef(v_type_get_llvm_type(v_quark_t));

    v_store(init_f,  val0);
    v_store(term_f,  val1);
    v_store(quark_u, val2);

    v_ast_generic_vtable_ = v_type_get_llvm_type(v_ast_generic_vtable);

    vtable_v = LLVMConstNamedStruct(v_ast_generic_vtable_, val0, 3);

    vtable_g = LLVMAddGlobal(module, v_ast_generic_vtable_, "voidc_ast_stmt_if_then_else_vtable");

    LLVMSetLinkage(vtable_g, LLVMPrivateLinkage);

    LLVMSetInitializer(vtable_g, vtable_v);

    //-----------------------------------------------------------------
    LLVMDisposeMemoryBuffer(voidc_get_unit_buffer());
    voidc_set_unit_buffer(0);
}

{   v_add_symbol("voidc_ast_stmt_if_then_else_vtable", v_ast_generic_vtable, 0);
}


//---------------------------------------------------------------------
//- v_ast_make_stmt_if_then_else - ...
//---------------------------------------------------------------------
{
    typ0 = v_alloca(v_type_ptr, 4);
    typ1 = v_getelementptr(typ0, 1);
    typ2 = v_getelementptr(typ0, 2);
    typ3 = v_getelementptr(typ0, 3);

    v_store(v_ast_stmt_ptr, typ0);
    v_store(v_ast_expr_ptr, typ1);
    v_store(v_ast_stmt_ptr, typ2);
    v_store(v_ast_stmt_ptr, typ3);

    ft = v_function_type(void, typ0, 4, false);

    //-----------------------------------------------------------------
    f = v_function_hack("v_ast_make_stmt_if_then_else", ft);

    v_add_parameter_name(f, 0, "ret",       v_ast_stmt_ptr);
    v_add_parameter_name(f, 1, "condition", v_ast_expr_ptr);
    v_add_parameter_name(f, 2, "stmt_then", v_ast_stmt_ptr);
    v_add_parameter_name(f, 3, "stmt_else", v_ast_stmt_ptr);
}
{
    stmp = v_alloca(v_ast_stmt_t);
    v_initialize(stmp);

    v_ast_make_generic(stmp, voidc_ast_stmt_if_then_else_vtable, ast_stmt_if_then_else_t_size);

    void_obj = v_ast_generic_get_object(stmp);

    obj = v_cast(void_obj, ast_stmt_if_then_else_ptr);

    obj_condition = v_getelementptr(obj, 0, 0);
    obj_stmt_then = v_getelementptr(obj, 0, 1);
    obj_stmt_else = v_getelementptr(obj, 0, 2);

    v_copy(obj_condition, condition);
    v_copy(obj_stmt_then, stmt_then);
    v_copy(obj_stmt_else, stmt_else);

    v_copy(ret, stmp);

    v_terminate(stmp);
}


//---------------------------------------------------------------------
//- mk_stmt_if_then_else - grammar action
//---------------------------------------------------------------------
//- fun mk_stmt_if_then_else_grammar_action(ret:       v_std_any_ptr,
//-                                         any0:      v_std_any_ptr,
//-                                         any_count: size_t
//-                                        )
//- {
//-     par_else = v_getelementptr(any0, 2);
//-
//-     stmt_else_ptr = v_util_std_any_get_pointer(v_ast_stmt_t, par_else);
//-
//-     stmt_else_v = v_alloca(v_ast_stmt_ptr);
//-
//-     stmt = v_alloca(v_ast_stmt_t);
//-     v_initialize(stmt);
//-
//-     if (stmt_else_ptr)
//-     {
//-         v_store(stmt_else_ptr, stmt_else_v);
//-     }
//-     else
//-     {
//-         v_store(stmt, stmt_else_v);     //- empty...
//-     }
//-
//-     ...
//-
//---------------------------------------------------------------------
{
    //-----------------------------------------------------------------
    f = v_function_hack("mk_stmt_if_then_else_grammar_action", v_peg_grammar_action_fun_t);

    LLVMSetLinkage(f, LLVMPrivateLinkage);

    ret       = v_add_parameter_name(f, 0, "ret",       v_std_any_ptr);
    any0      = v_add_parameter_name(f, 1, "any0",      v_std_any_ptr);
    any_count = v_add_parameter_name(f, 2, "any_count", size_t);


    val0 = v_alloca(LLVMValueRef, 3);
    val1 = v_getelementptr(val0, 1);
    val2 = v_getelementptr(val0, 2);

    int_ = v_type_get_llvm_type(int);

    n1 = LLVMConstInt(int_, 1, 0);
    n2 = LLVMConstInt(int_, 2, 0);

    builder = v_target_get_voidc_builder();


    v_store(n2, val0);

    par_else = LLVMBuildGEP2(builder, v_type_get_llvm_type(v_std_any_t), any0, val0, 1, "par_else");


    get_arg_ft_ = v_type_get_llvm_type(v_find_symbol_type("v_ast_std_any_get_pointer_stmt_impl"));
    get_arg_f   = v_obtain_function("v_ast_std_any_get_pointer_stmt_impl");

    v_store(par_else, val0);

    stmt_else_ptr = LLVMBuildCall2(builder, get_arg_ft_, get_arg_f, val0, 1, "stmt_else_ptr");


    v_ast_stmt_ptr_ = v_type_get_llvm_type(v_ast_stmt_ptr);

    stmt_else_v = LLVMBuildAlloca(builder, v_ast_stmt_ptr_, "stmt_else_v");


    v_ast_stmt_t_ = v_type_get_llvm_type(v_ast_stmt_t);

    stmt = LLVMBuildAlloca(builder, v_ast_stmt_t_, "stmt");

    stmt_init_ft_ = v_type_get_llvm_type(v_find_symbol_type("v_ast_initialize_stmt_impl"));
    stmt_init_f   = v_obtain_function("v_ast_initialize_stmt_impl");

    v_store(stmt, val0);
    v_store(n1,   val1);

    LLVMBuildCall2(builder, stmt_init_ft_, stmt_init_f, val0, 2, "");


    then_b = LLVMAppendBasicBlock(f, "then_b");
    else_b = LLVMAppendBasicBlock(f, "else_b");
    cont_b = LLVMAppendBasicBlock(f, "cont_b");


    ok_v = LLVMBuildIsNotNull(builder, stmt_else_ptr, "ok_v");

    LLVMBuildCondBr(builder, ok_v, then_b, else_b);


    LLVMPositionBuilderAtEnd(builder, then_b);

    LLVMBuildStore(builder, stmt_else_ptr, stmt_else_v);

    LLVMBuildBr(builder, cont_b);


    LLVMPositionBuilderAtEnd(builder, else_b);

    LLVMBuildStore(builder, stmt, stmt_else_v);

    LLVMBuildBr(builder, cont_b);


    LLVMPositionBuilderAtEnd(builder, cont_b);


    stmt_ptr_ptr = v_pointer_type(v_ast_stmt_ptr, 0);

    v_add_variable("stmt",        v_ast_stmt_ptr, stmt);
    v_add_variable("stmt_else_v", stmt_ptr_ptr,   stmt_else_v);
}
{
    par_then = v_getelementptr(any0, 1);

    condition = v_std_any_get_pointer(v_ast_expr_t, any0);
    stmt_then = v_std_any_get_pointer(v_ast_stmt_t, par_then);
    stmt_else = v_load(stmt_else_v);

    v_ast_make_stmt_if_then_else(stmt, condition, stmt_then, stmt_else);

    v_std_any_set_pointer(ret, stmt);

    v_terminate(stmt);
}


//---------------------------------------------------------------------
//- compile_ast_stmt_if_then_else_branch_helper
//---------------------------------------------------------------------
//- fun compile_ast_stmt_if_then_else_branch_helper(visitor:  voidc_visitor_ptr,
//-                                                 stmt:     v_ast_stmt_ptr,
//-                                                 bblock_v: bblock_ptr
//-                                                )
//- {
//-     builder = v_target_get_builder();
//-
//-     no_stmt = v_empty(stmt);
//-
//-     if (no_stmt) ;
//-     else
//-     {
//-         base_ast = v_cast(stmt, v_ast_base_ptr);
//-
//-         v_ast_accept_visitor(base_ast, visitor);
//-
//-         cur_b = LLVMGetInsertBlock(builder);
//-
//-         term = LLVMGetBasicBlockTerminator(cur_b);
//-
//-         if (term)   v_return();
//-     }
//-
//-     bblock = v_load(bblock_v);
//-
//-     if (bblock) ;
//-     else
//-     {
//-         cur_b = LLVMGetInsertBlock(builder);
//-
//-         fun = LLVMGetBasicBlockParent(cur_b);
//-
//-         fin_bb = LLVMAppendBasickBlock(fun, "fin_bb");
//-
//-         v_store(fin_bb, bblock_v);
//-     }
//-
//-     ...
//-
//---------------------------------------------------------------------
{
    typ0 = v_alloca(v_type_ptr, 3);
    typ1 = v_getelementptr(typ0, 1);
    typ2 = v_getelementptr(typ0, 2);

    bblock_ptr = v_pointer_type(LLVMBasicBlockRef, 0);

    v_store(voidc_visitor_ptr,  typ0);
    v_store(v_ast_stmt_ptr,     typ1);
    v_store(bblock_ptr,         typ2);

    ft = v_function_type(void, typ0, 3, false);

    //-----------------------------------------------------------------
    f = v_function_hack("compile_ast_stmt_if_then_else_branch_helper", ft);

    LLVMSetLinkage(f, LLVMPrivateLinkage);

    visitor  = LLVMGetParam(f, 0);
    stmt     = LLVMGetParam(f, 1);
    bblock_v = LLVMGetParam(f, 2);


    val0 = v_alloca(LLVMValueRef, 3);
    val1 = v_getelementptr(val0, 1);
    val2 = v_getelementptr(val0, 2);

    builder = v_target_get_voidc_builder();


    get_builder_ft_ = v_type_get_llvm_type(v_find_symbol_type("v_target_get_builder"));
    get_builder_f   = v_obtain_function("v_target_get_builder");

    builder_v = LLVMBuildCall2(builder, get_builder_ft_, get_builder_f, val0, 0, "builder_v");


    stmt_empty_ft_ = v_type_get_llvm_type(v_find_symbol_type("v_ast_empty_stmt_impl"));
    stmt_empty_f   = v_obtain_function("v_ast_empty_stmt_impl");

    v_store(stmt, val0);

    no_stmt = LLVMBuildCall2(builder, stmt_empty_ft_, stmt_empty_f, val0, 1, "no_stmt");


    stmt_b = LLVMAppendBasicBlock(f, "stmt_b");
    term_b = LLVMAppendBasicBlock(f, "term_b");
    cont_b = LLVMAppendBasicBlock(f, "cont_b");

    LLVMBuildCondBr(builder, no_stmt, cont_b, stmt_b);


    LLVMPositionBuilderAtEnd(builder, stmt_b);

    v_ast_base_ptr_ = v_type_get_llvm_type(v_ast_base_ptr);

    base_ast = LLVMBuildBitCast(builder, stmt, v_ast_base_ptr_, "base_ast");

    v_store(base_ast, val0);
    v_store(visitor,  val1);

    accept_visitor_ft_ = v_type_get_llvm_type(v_find_symbol_type("v_ast_accept_visitor"));
    accept_visitor_f   = v_obtain_function("v_ast_accept_visitor");

    LLVMBuildCall2(builder, accept_visitor_ft_, accept_visitor_f, val0, 2, "");


    v_store(builder_v, val0);

    llvm_get_insert_block_ft_ = v_type_get_llvm_type(v_find_symbol_type("LLVMGetInsertBlock"));
    llvm_get_insert_block_f   = v_obtain_function("LLVMGetInsertBlock");

    cur_bv = LLVMBuildCall2(builder, llvm_get_insert_block_ft_, llvm_get_insert_block_f, val0, 1, "cur_bv");


    v_store(cur_bv, val0);

    llvm_get_basic_block_terminator_ft_ = v_type_get_llvm_type(v_find_symbol_type("LLVMGetBasicBlockTerminator"));
    llvm_get_basic_block_terminator_f   = v_obtain_function("LLVMGetBasicBlockTerminator");

    term_v = LLVMBuildCall2(builder, llvm_get_basic_block_terminator_ft_, llvm_get_basic_block_terminator_f, val0, 1, "term_v");


    ok_v = LLVMBuildIsNotNull(builder, term_v, "ok_v");

    LLVMBuildCondBr(builder, ok_v, term_b, cont_b);


    LLVMPositionBuilderAtEnd(builder, term_b);


    LLVMBuildRetVoid(builder);


    LLVMPositionBuilderAtEnd(builder, cont_b);


    bbl_b = LLVMAppendBasicBlock(f, "bbl_b");
    fin_b = LLVMAppendBasicBlock(f, "fin_b");


    bblock = LLVMBuildLoad2(builder, v_type_get_llvm_type(LLVMBasicBlockRef), bblock_v, "bblock");

    bb_v = LLVMBuildIsNotNull(builder, bblock, "bb_v");

    LLVMBuildCondBr(builder, bb_v, fin_b, bbl_b);


    LLVMPositionBuilderAtEnd(builder, bbl_b);


    v_store(builder_v, val0);

    cur_bv = LLVMBuildCall2(builder, llvm_get_insert_block_ft_, llvm_get_insert_block_f, val0, 1, "cur_bv");


    llvm_get_basic_block_parent_ft_ = v_type_get_llvm_type(v_find_symbol_type("LLVMGetBasicBlockParent"));
    llvm_get_basic_block_parent_f   = v_obtain_function("LLVMGetBasicBlockParent");

    v_store(cur_bv, val0);

    fun_v = LLVMBuildCall2(builder, llvm_get_basic_block_parent_ft_, llvm_get_basic_block_parent_f, val0, 1, "fun_v");


    llvm_append_basic_block_ft_ = v_type_get_llvm_type(v_find_symbol_type("LLVMAppendBasicBlock"));
    llvm_append_basic_block_f   = v_obtain_function("LLVMAppendBasicBlock");

    fin_bb_str = LLVMBuildGlobalStringPtr(builder, "fin_bb", "fin_bb_str");

    v_store(fun_v,      val0);
    v_store(fin_bb_str, val1);

    fin_bb_v = LLVMBuildCall2(builder, llvm_append_basic_block_ft_, llvm_append_basic_block_f, val0, 2, "fin_bb_v");

    LLVMBuildStore(builder, fin_bb_v, bblock_v);


    LLVMBuildBr(builder, fin_b);


    LLVMPositionBuilderAtEnd(builder, fin_b);

    _LLVMBuilderRef = v_find_type("LLVMBuilderRef");

    v_add_variable("builder",  LLVMBuilderRef, builder_v);
    v_add_variable("bblock_v", bblock_ptr,     bblock_v);
}
{
    bblock = v_load(bblock_v);

    LLVMBuildBr(builder, bblock);
}


//---------------------------------------------------------------------
//- compile_ast_stmt_if_then_else_join_helper
//---------------------------------------------------------------------
//- fun compile_ast_stmt_if_then_else_join_helper(bblock: LLVMBasicBlockRef)
//- {
//-     if (bblock) ;
//-     else
//-     {
//-         v_return();
//-     }
//-
//-     ...
//-
//---------------------------------------------------------------------
{
    typ = v_alloca(v_type_ptr);

    _LLVMBasicBlockRef = v_find_type("LLVMBasicBlockRef");

    v_store(_LLVMBasicBlockRef, typ);

    ft = v_function_type(void, typ, 1, false);

    //-----------------------------------------------------------------
    f = v_function_hack("compile_ast_stmt_if_then_else_join_helper", ft);

    LLVMSetLinkage(f, LLVMPrivateLinkage);

    bblock = v_add_parameter_name(f, 0, "bblock", _LLVMBasicBlockRef);


    builder = v_target_get_voidc_builder();


    then_b = LLVMAppendBasicBlock(f, "then_b");
    else_b = LLVMAppendBasicBlock(f, "else_b");


    ok_v = LLVMBuildIsNull(builder, bblock, "ok_v");

    LLVMBuildCondBr(builder, ok_v, then_b, else_b);


    LLVMPositionBuilderAtEnd(builder, then_b);

    LLVMBuildRetVoid(builder);


    LLVMPositionBuilderAtEnd(builder, else_b);
}
{
    builder = v_target_get_builder();

    LLVMPositionBuilderAtEnd(builder, bblock);
}


//---------------------------------------------------------------------
//- compile_ast_stmt_if_then_else_t - visitor method
//---------------------------------------------------------------------
{
    //-----------------------------------------------------------------
    f = v_function_hack("compile_ast_stmt_if_then_else_t", voidc_visitor_method_t);

    LLVMSetLinkage(f, LLVMPrivateLinkage);

    v_add_parameter_name(f, 0, "visitor", voidc_visitor_ptr);
    v_add_parameter_name(f, 1, "aux",     void_ptr);
    v_add_parameter_name(f, 2, "self",    v_ast_base_ptr);
}
{
    void_obj = v_ast_generic_get_object(v_cast(self, v_ast_stmt_ptr));

    obj = v_cast(void_obj, ast_stmt_if_then_else_ptr);

    condition = v_getelementptr(obj, 0, 0);
    stmt_then = v_getelementptr(obj, 0, 1);
    stmt_else = v_getelementptr(obj, 0, 2);

    builder = v_target_get_builder();

    v_set_result_type(UNREFERENCE_TAG);

    base_cond = v_cast(condition, v_ast_base_ptr);

    v_push_temporaries();

    v_ast_accept_visitor(base_cond, visitor);

    v_pop_temporaries();

    cond_a = v_get_result_value();

    cond = LLVMBuildIsNotNull(builder, cond_a, "cond");

    cur_b = LLVMGetInsertBlock(builder);
    fun_v = LLVMGetBasicBlockParent(cur_b);

    then_b = LLVMAppendBasicBlock(fun_v, "then_b");
    else_b = LLVMAppendBasicBlock(fun_v, "else_b");

    LLVMBuildCondBr(builder, cond, then_b, else_b);

    bblock_v = v_alloca(LLVMBasicBlockRef);
    v_store(0, bblock_v);

    LLVMPositionBuilderAtEnd(builder, then_b);

    compile_ast_stmt_if_then_else_branch_helper(visitor, stmt_then, bblock_v);

    LLVMPositionBuilderAtEnd(builder, else_b);

    compile_ast_stmt_if_then_else_branch_helper(visitor, stmt_else, bblock_v);

    bblock = v_load(bblock_v);

    compile_ast_stmt_if_then_else_join_helper(bblock);
}


//---------------------------------------------------------------------
//- Enable statement if_then_else compilation
//---------------------------------------------------------------------
{
    ft = v_function_type(void, 0, 0, false);

    //-----------------------------------------------------------------
    v_function_hack("voidc_enable_statement_if_then_else_compiler", ft);
}
{
    v_return_if(voidc_visitor_get_void_method(voidc_compiler, quark, 0));


    v_store(quark, v_getelementptr(voidc_ast_stmt_if_then_else_vtable, 0, 2));

    voidc_visitor_set_void_method(voidc_compiler, voidc_compiler, quark, compile_ast_stmt_if_then_else_t, 0);
}


//---------------------------------------------------------------------
//- Enable statement if_then_else
//---------------------------------------------------------------------
{
    ft = v_function_type(void, 0, 0, false);

    //-----------------------------------------------------------------
    v_function_hack("voidc_enable_statement_if_then_else", ft);
}
{
    voidc_enable_statement_if_then_else_compiler();


    v_return_if_not(voidc_has_grammar());
    v_return_if(voidc_grammar_has_parser("stmt_if_then_else"));


    pp0 = v_alloca(v_peg_parser_t, 14);
    v_initialize(pp0, 14);

    pp1 = v_getelementptr(pp0, 1);
    pp2 = v_getelementptr(pp0, 2);
    pp3 = v_getelementptr(pp0, 3);
    pp4 = v_getelementptr(pp0, 4);
    pp5 = v_getelementptr(pp0, 5);
    pp6 = v_getelementptr(pp0, 6);
    pp7 = v_getelementptr(pp0, 7);
    pp8 = v_getelementptr(pp0, 8);
    pp9 = v_getelementptr(pp0, 9);
    pp10 = v_getelementptr(pp0, 10);
    pp11 = v_getelementptr(pp0, 11);
    pp12 = v_getelementptr(pp0, 12);
    pp13 = v_getelementptr(pp0, 13);


    //-----------------------------------------------------------------
    //- stmt_if_then_else <- "if" _ '(' _ c:expr _ ')' _ t:stmt
    //-                      e:(_ "else" !ident_cont _ stmt)?           { mk_stmt_if_then_else(c, t, e) }

    v_peg_make_literal_parser(pp0, "if");

    v_peg_make_identifier_parser(pp1, "_");
    v_peg_make_character_parser(pp2, '(');
    v_peg_make_identifier_parser(pp3, "_");

    v_peg_make_identifier_parser(pp4, "expr");
    v_peg_make_catch_variable_parser(pp4, "c", pp4);

    v_peg_make_identifier_parser(pp5, "_");
    v_peg_make_character_parser(pp6, ')');
    v_peg_make_identifier_parser(pp7, "_");

    v_peg_make_identifier_parser(pp8, "stmt");
    v_peg_make_catch_variable_parser(pp8, "t", pp8);


    v_peg_make_identifier_parser(pp9, "_");
    v_peg_make_literal_parser(pp10, "else");
    v_peg_make_identifier_parser(pp11, "ident_cont");
    v_peg_make_not_parser(pp11, pp11);
    v_peg_make_identifier_parser(pp12, "_");

    v_peg_make_identifier_parser(pp13, "stmt");

    v_peg_make_sequence_parser(pp9, pp9, 5);
    v_peg_make_question_parser(pp9, pp9);
    v_peg_make_catch_variable_parser(pp9, "e", pp9);


    ar0 = v_alloca(v_peg_argument_t, 3);
    v_initialize(ar0, 3);

    ar1 = v_getelementptr(ar0, 1);
    ar2 = v_getelementptr(ar0, 2);

    v_peg_make_identifier_argument(ar0, "c");
    v_peg_make_identifier_argument(ar1, "t");
    v_peg_make_identifier_argument(ar2, "e");


    act = v_alloca(v_peg_action_t);
    v_initialize(act);

    v_peg_make_call_action(act, "mk_stmt_if_then_else", ar0, 3);

    v_peg_make_action_parser(pp10, act);


    v_peg_make_sequence_parser(pp0, pp0, 11);


    gr0 = v_alloca(v_peg_grammar_t);
    v_initialize(gr0);

    v_peg_get_grammar(gr0);


    v_peg_grammar_set_action(gr0, gr0, "mk_stmt_if_then_else", mk_stmt_if_then_else_grammar_action);

    v_peg_grammar_set_parser(gr0, gr0, "stmt_if_then_else", pp0, 0);


    v_peg_make_identifier_parser(pp0, "stmt_if_then_else");


    pp_ = v_peg_grammar_get_parser(gr0, "stmt", 0);

    v_peg_choice_parser_concat(pp0, pp0, pp_);

    v_peg_grammar_set_parser(gr0, gr0, "stmt", pp0, 0);


    v_peg_set_grammar(gr0);


    v_terminate(gr0);
    v_terminate(act);
    v_terminate(ar0, 3);
    v_terminate(pp0, 14);
}


//---------------------------------------------------------------------
//- ...
//---------------------------------------------------------------------
{
    module = v_get_module();

    //-----------------------------------------------------------------
//  v_debug_print_module(1);

    voidc_unit_load_module_to_jit(module, true);

    LLVMDisposeModule(module);
    v_set_module(0);
}


//---------------------------------------------------------------------
{
    v_make_export_symbol("v_ast_make_stmt_if_then_else");

    v_make_export_symbol("voidc_enable_statement_if_then_else_compiler");
    v_make_export_symbol("voidc_enable_statement_if_then_else");
}


