{   v_import("level-00");

    v_import("function_hack.void");
    v_import("if_then_else.void");
    v_import("block.void");
    v_import("loop.void");
    v_import("switch.void");
    v_import("memory.void");

    v_import("expression.void");
}

{   voidc_enable_statement_if_then_else();
    voidc_enable_statement_block();
    voidc_enable_statement_loop();
    voidc_enable_statement_switch();
    voidc_enable_memory_intrinsics();

    voidc_enable_expression();
}

{   v_import("printf.void"); }


//---------------------------------------------------------------------
//{v_debug_print_module(1);}
{
    N = 6;

    v = *v_alloca(int(N));

    i = *v_alloca(int) := 0;

    loop
    {
        if (i == 1<<N)  v_break();

        v := v_cast(i, int(N));    v := v*v;

        printf("i: %2d,  v: %3d\n", i, v_cast(v, int));

        ++i;
    }

    printf("\n");
}


//---------------------------------------------------------------------
{   v_add_type("double", v_f64_type());
}

//---------------------------------------------------------------------
//{ v_debug_print_module(1); }
{
    N = 1000;

    flags = v_malloc(bool, N);

    i = *v_alloca(int) := 0;

    loop
    {
        if (i == N) v_break();

        flags[i] := true;

        ++i;
    }

    i := 2;

    k = *v_alloca(int);

    n = *v_alloca(double) := 0;

    loop
    {
        if (i >= N) v_break();

        if (flags[i])
        {
            n += 1;     //- ?..

            printf("%d, %g\n", i, n/i);

            k := 2*i;

            loop
            {
                if (k >= N) v_break();

                flags[k] := false;

                k += i;
            }
        }

        ++i;
    }

    v_free(flags);

    printf("\n");
}


//---------------------------------------------------------------------
{   v_export_symbol_type("rand", v_function_type(int, 0, 0, false));
}

{
    N = 20;

    i = *v_alloca(int) := 0;

    n = *v_alloca(int);
    c = *v_alloca(int);

    loop
    {
        if (i == N) v_break();

        n := rand();

        r = v_load(&n);

        c := 0;

        loop
        {
            if (n == 0) v_break();

            ++c;

            n &= n - 1;
        }

        printf("%X: %d\n", r, c);

        ++i;
    }

    printf("\n");
}


//---------------------------------------------------------------------
//{ v_debug_print_module(1); }
{
    N = 100;

    n = *v_alloca(int) := 1;

    v = *v_alloca(int);

    loop
    {
        if (n >= N) v_break();

        v := n;

        loop
        {
            printf("%d\n", v);

//          if (v < n)  v_break();
            if (v == 1) v_break();

            if (v & 1)  v := 3*v + 1;
            else        v := v/2;
        }

        printf("\n");

        ++n;
    }

}


//---------------------------------------------------------------------
//{ v_debug_print_module(1); }
{
    N = 7;

    v = *v_alloca(vec(double, N)) := N;

    i = *v_alloca(((int))) := 0;

    loop
    {
        if (i >= N) v_break();

        printf("v[%d]/%d: %g\n", i, i+1, v[i]/(i+1));

        ++i;
    }
}


//---------------------------------------------------------------------
{   typ = v_alloca(v_type_ptr);

    typ[0] := bool;

    ft = v_function_type(bool, typ, 1, false);

    //-----------------------------------------------------------------
    f = v_function_hack("F", ft);

    v_add_parameter_name(f, 0, "flag", bool);
}
{
    printf("flag: %d\n", v_cast(flag, int));

    v_return(flag);
}


//---------------------------------------------------------------------
//{ v_debug_print_module(1); }
{
    printf("\n");

    r = (F(true) && F(true) && F(false) && F(false) && F(true));

    printf("&&: %d\n", v_cast(r, int));

    r = (F(false) || F(false) || F(true) || F(false) || F(true));

    printf("||: %d\n", v_cast(r, int));
}


//---------------------------------------------------------------------
{   typ = v_alloca(v_type_ptr);

    typ[0] := int;

    ft = v_function_type(int, typ, 1, false);

    //-----------------------------------------------------------------
    f = v_function_hack("V", ft);

    v_add_parameter_name(f, 0, "v", bool);
}
{
    printf("V: %d\n", v);

    v_return(v);
}


//---------------------------------------------------------------------
//{ v_debug_print_module(1); }
{
    printf("\n");

    r = (V(0) < V(1) < V(5) < V(3) < V(4));

    printf("<: %d\n", v_cast(r, int));
}


