//---------------------------------------------------------------------
//- Copyright (C) 2020-2024 Dmitry Borodkin <borodkin.dn@gmail.com>
//- SDPX-License-Identifier: LGPL-3.0-or-later
//---------------------------------------------------------------------
{   voidc_guard_target("projections.void  must be imported into the voidc target only!");

    v_import("level-00");
    v_import("level-01");

    v_import("llvm-c/Core.void");

    v_import("literals.void");
    v_import("loops_etc.void");
    v_import("condcomp.void");
    v_import("aggregates.void");
    v_import("handlers_etc.void");
    v_import("overloading.void");

//  v_import("printf.void");
}

{   v_enable_level_01();

    voidc_enable_literals();
    voidc_enable_loops_etc();
    voidc_enable_condcomp();
    voidc_enable_aggregates();
    voidc_enable_handlers_etc();
    voidc_enable_overloading();
}


//---------------------------------------------------------------------
{   q_ref = v_reference_type(v_quark_t, 0);

    q = v_quark_ptr_from_string;

    v_add_symbol("proj_q", q_ref, q("projection_payload"));

    v_add_symbol("q_op_postfix_dot", q_ref, q("v.op_postfix_dot"));
    v_add_symbol("q_op_postfix_to",  q_ref, q("v.op_postfix_to"));

    v_add_symbol("size_t_q", q_ref, q("size_t"));

    v_add_symbol("q_static_type_t", q_ref, q("v_static_type_t"));
}


//=====================================================================
{   voidc_unit_begin_module("projections_module"); }


//---------------------------------------------------------------------
op_postfix_dot: &v_ast_expr_t := v_undef();
op_postfix_to:  &v_ast_expr_t := v_undef();

voidc_projections_internal_globals_cleaner: (*void) ~> void
{
    v_terminate(&op_postfix_dot);
    v_terminate(&op_postfix_to);
}


//---------------------------------------------------------------------
//- mk_projection - grammar action
//---------------------------------------------------------------------
private
mk_projection_grammar_action: (ret: *v_std_any_t, *void, any: *v_std_any_t, size_t) ~> void
{
    op0  = v_std_any_get_pointer(v_ast_expr_t,      any+0);
    name = v_std_any_get_pointer(v_std_string_t,    any+1);
    args = v_std_any_get_pointer(v_ast_expr_list_t, any+2);

    void_obj = v_ast_generic_get_object(op0);

    obj = *(void_obj: *v_ast_operator_unary_t);

    quark = obj[0];
    prec  = obj[1];


    pay = v_alloca(v_ast_generic_list_t);
    v_initialize(pay);
    defer v_terminate(pay);

    id = v_alloca(v_ast_expr_t);
    v_initialize(id);
    defer v_terminate(id);

    v_ast_make_expr_identifier(id, v_std_string_get(name));

    v_make_list(pay, proj_q, (id: *v_ast_base_t), 1);

    if (args) v_list_append(pay, pay, (args: *v_ast_base_t));


    op1 = v_alloca(v_ast_expr_t);
    v_initialize(op1);
    defer v_terminate(op1);

    v_ast_make_operator_unary(op1, quark, prec, (pay: *v_ast_base_t));

    v_std_any_set_pointer(ret, op1);
}


//---------------------------------------------------------------------
//- ...
//---------------------------------------------------------------------
private
overloaded_intrinsic_t = *((*void, *voidc_visitor_t, *v_ast_base_t, **v_ast_expr_t, unsigned) ~> void);

//---------------------------------------------------------------------
private
op_postfix_dot_helper: (*void, vis: *voidc_visitor_t, self: *v_ast_base_t,
                        t0: *v_type_t, v0: LLVMValueRef,
                        q0: v_quark_t, q1: v_quark_t,
                        alst: *v_ast_expr_list_t
                       ) ~> void
{
    spc_flag: &bool := false;

    quark: &v_quark_t := v_undef();

    if (alst)   quark := q1;        //- with ()
    else        quark := q0;        //- without ()

    ctx: &*void := v_undef();

    fun: &overloaded_intrinsic_t := v_util_lookup_overload(quark, t0, &ctx);

    if (!fun)
    {
        spc_flag := true;

        quark := q0;

        fun := v_util_lookup_overload(quark, t0, &ctx);             //- Sic !!!

        v_assert(!fun  ||  alst);
    }

    if (!fun)               //- Last chance...
    {
        q = q_op_postfix_dot;

        fun := v_util_lookup_overload(q, t0, &ctx);
        v_assert(fun);

        expr = v_alloca(v_ast_expr_t);
        v_initialize(expr);
        defer v_terminate(expr);

        v_ast_make_expr_compiled(expr, t0, v0);

        arg_ptr: &*v_ast_expr_t := expr;                //- Value, compiled

        fun(ctx, vis, self, &arg_ptr, 1);               //- ?!?!?!?!?!?!?

        v_return();
    }

    //- Overloading only!

    ttag: &*v_type_t    := v_undef();
    vtag: &LLVMValueRef := v_undef();

    if (spc_flag)
    {
        ttag := v_get_result_type();
        vtag := v_get_result_value();

        v_set_result_type(INVIOLABLE_TAG);
        v_set_result_value(0);
    }

    {   expr = v_alloca(v_ast_expr_t);
        v_initialize(expr);
        defer v_terminate(expr);

        arg_count: &unsigned := 1;

        if (alst && !spc_flag)  arg_count += (v_list_get_size(alst) : unsigned);

        arg_ptrs = v_malloc(*v_ast_expr_t, arg_count);
        defer v_free(arg_ptrs);

        v_ast_make_expr_compiled(expr, t0, v0);

        arg_ptrs[0] := expr;

        for (j: &int := 1; j < arg_count; ++j)
        {
            arg_ptrs[j] := v_list_get_item(alst, j-1);
        }

        fun(ctx, vis, self, arg_ptrs, arg_count);
    }

    if (!spc_flag)  v_return();

    tr = v_get_result_type();
    vr = v_get_result_value();

    v_set_result_type(ttag);
    v_set_result_value(vtag);

    v_compile_expr_call(vis, self, alst, tr, vr);
}

//---------------------------------------------------------------------
#do (v_make_handler(v_compile_op_postfix_dot, op_postfix_dot_helper))

//---------------------------------------------------------------------
private
op_postfix_dot_agent: (vis: *voidc_visitor_t, self: *v_ast_base_t,
                       t0: *v_type_t, v0: LLVMValueRef
                      ) ~> void
{
    args = v_ast_expr_call_get_arg_list((self: *v_ast_expr_t));

    op = v_list_get_item(args, 0);

    void_obj = v_ast_generic_get_object(op);

    obj = *(void_obj: *v_ast_operator_unary_t);

    pay = (&obj[2] : *v_ast_generic_list_t);


    etmp = (v_list_get_item(pay, 0) : *v_ast_expr_t);

    name = v_ast_expr_identifier_get_name(etmp);

    alst: &*v_ast_expr_list_t := 0;

    got_args = v_list_get_size(pay) > 1;

    if (got_args)  alst := (v_list_get_item(pay, 1) : *v_ast_expr_list_t);

    q0: &v_quark_t := v_undef();
    q1: &v_quark_t := v_undef();

    {   sstr = v_alloca(v_std_string_t);
        v_initialize(sstr);
        defer v_terminate(sstr);

        v_std_string_set(sstr, ".");

        v_std_string_append(sstr, name);

        q0 := v_quark_from_string(v_std_string_get(sstr));

        v_std_string_append(sstr, "()");

        q1 := v_quark_from_string(v_std_string_get(sstr));
    }

    v_compile_op_postfix_dot(vis, self, t0, v0, q0, q1, alst);
}


//---------------------------------------------------------------------
//- ...
//---------------------------------------------------------------------
private
op_postfix_dot_intrinsic: (*void, vis: *voidc_visitor_t, self: *v_ast_base_t) ~> void
{
    args = v_ast_expr_call_get_arg_list((self: *v_ast_expr_t));

    ttag = v_get_result_type();
    vtag = v_get_result_value();

    arg = v_list_get_item(args, 1);

    v_set_result_type(INVIOLABLE_TAG);
    v_set_result_value(0);

    v_ast_accept_visitor((arg: *v_ast_base_t), vis);

    t0 = v_get_result_type();
    v0 = v_get_result_value();

    v_set_result_type(ttag);
    v_set_result_value(vtag);

    op_postfix_dot_agent(vis, self, t0, v0);
}


//---------------------------------------------------------------------
//- ...
//---------------------------------------------------------------------
private
op_postfix_to_helper: (*void, vis: *voidc_visitor_t, self: *v_ast_base_t,
                       t0: *v_type_t, v0: LLVMValueRef
                      ) ~> void
{
    ttag = v_get_result_type();
    vtag = v_get_result_value();

    t0: &*v_type_t    := t0;
    v0: &LLVMValueRef := v0;

    q = q_op_postfix_to;

    ctx: &*void := v_undef();

    if (fun = v_util_lookup_overload(q, t0, &ctx))
    {
        //- Got overloading!

        expr = v_alloca(v_ast_expr_t);
        v_initialize(expr);
        defer v_terminate(expr);

        v_ast_make_expr_compiled(expr, t0, v0);

        arg_ptr: &*v_ast_expr_t := expr;                //- Value, compiled

        v_set_result_type(INVIOLABLE_TAG);
        v_set_result_value(0);

        fun(ctx, vis, self, &arg_ptr, 1);

        t0 := v_get_result_type();
        v0 := v_get_result_value();
    }

    //- Default treatment...

    if (v_type_is_reference(t0))
    {
        builder = v_target_get_builder();

        t0 := v_type_refptr_get_element_type(t0);

        v0 := LLVMBuildLoad2(builder, v_type_get_llvm_type(t0), v0, "");
    }

    {   e = v_type_refptr_get_element_type(t0);
        a = v_type_refptr_get_address_space(t0);

        t0 := v_reference_type(e, a);
    }

    v_set_result_type(ttag);
    v_set_result_value(vtag);

    op_postfix_dot_agent(vis, self, t0, v0);
}

//---------------------------------------------------------------------
#do (v_make_handler(v_compile_op_postfix_to, op_postfix_to_helper))

//---------------------------------------------------------------------
private
op_postfix_to_intrinsic: (*void, vis: *voidc_visitor_t, self: *v_ast_base_t) ~> void
{
    args = v_ast_expr_call_get_arg_list((self: *v_ast_expr_t));

    ttag = v_get_result_type();
    vtag = v_get_result_value();

    arg = v_list_get_item(args, 1);

    v_set_result_type(INVIOLABLE_TAG);
    v_set_result_value(0);

    v_ast_accept_visitor((arg: *v_ast_base_t), vis);

    t0 = v_get_result_type();
    v0 = v_get_result_value();

    v_set_result_type(ttag);
    v_set_result_value(vtag);

    v_compile_op_postfix_to(vis, self, t0, v0);
}


//---------------------------------------------------------------------
private
some_static_type_projection_intrinsic: (aux: *void, vis: *voidc_visitor_t, self: *v_ast_base_t,
                                        args: **v_ast_expr_t, count: unsigned
                                        ) ~> void
{
    type  = (v_ast_expr_compiled_get_value(args[0]) : *v_type_t);
    type_ = v_type_get_llvm_type(type);

    f = (aux: *((LLVMTypeRef) ~> LLVMValueRef));

    v = f(type_);

    v_adopt_result(v_find_type_q(size_t_q), v);
}


//---------------------------------------------------------------------
//- Enable projections
//---------------------------------------------------------------------
export
voidc_enable_projections_compiler: () ~> void
{
    q = v_quark_from_string;

    vis = v_get_compiler();

    v_return_if(v_get_intrinsic_q(q_op_postfix_dot, 0));

    //-------------------------------------------------------------
    v_add_intrinsic_q(q_op_postfix_dot, op_postfix_dot_intrinsic, 0);
    v_add_intrinsic_q(q_op_postfix_to,  op_postfix_to_intrinsic,  0);

    //-------------------------------------------------------------
    v_set_handler(v_compile_op_postfix_dot, op_postfix_dot_helper, 0);
    v_set_handler(v_compile_op_postfix_to,  op_postfix_to_helper,  0);

    //-------------------------------------------------------------
    q_static_type_sizeof  = q("v.static_type_sizeof");
    q_static_type_alignof = q("v.static_type_alignof");

    static_type_t = v_find_type_q(q_static_type_t);

    v_export_overload_q(q(".sizeof"),  static_type_t, q_static_type_sizeof);
    v_export_overload_q(q(".alignof"), static_type_t, q_static_type_alignof);

    v_export_intrinsic_q(q_static_type_sizeof,  some_static_type_projection_intrinsic, LLVMSizeOf);
    v_export_intrinsic_q(q_static_type_alignof, some_static_type_projection_intrinsic, LLVMAlignOf);
}


//---------------------------------------------------------------------
//- Enable projections
//---------------------------------------------------------------------
export
voidc_enable_projections: () ~> void
{
    voidc_enable_projections_compiler();


    gr0 = v_peg_get_grammar();

    v_return_if_not(gr0);
    v_return_if(v_peg_grammar_get_action(gr0, "mk_projection", 0));


    any = v_alloca(v_std_any_t, 2);
    v_initialize(any, 2);
    defer v_terminate(any, 2);

    any_dot = any + 0;
    any_to  = any + 1;

    v_std_any_set_pointer(any_dot, &op_postfix_dot);
    v_std_any_set_pointer(any_to,  &op_postfix_to);

    grammar gr0
    {
    values:
        op_postfix_dot = any_dot;
        op_postfix_to  = any_to;

    actions:
        mk_projection = mk_projection_grammar_action;

    parsers:
        postfix += '.' _ i:identifier _'('_ al:expr_list _')'   { mk_projection(op_postfix_dot, i, al) }
                 / '.' _ i:identifier                           { mk_projection(op_postfix_dot, i,  0) }
                 / "->"_ i:identifier _'('_ al:expr_list _')'   { mk_projection(op_postfix_to,  i, al) }
                 / "->"_ i:identifier                           { mk_projection(op_postfix_to,  i,  0) }
                 ;
    }
}


//=====================================================================
{   voidc_unit_end_module(); }


//---------------------------------------------------------------------
//- ...
//---------------------------------------------------------------------
{
    v_initialize(&op_postfix_dot);
    v_initialize(&op_postfix_to);

    v_ast_make_operator_unary(&op_postfix_dot, q_op_postfix_dot, 30000, 0);         //- "Postfix" priority
    v_ast_make_operator_unary(&op_postfix_to,  q_op_postfix_to,  30000, 0);         //- "Postfix" priority

    v_add_cleaner(voidc_projections_internal_globals_cleaner, 0);
}


