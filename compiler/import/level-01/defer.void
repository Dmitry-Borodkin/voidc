//---------------------------------------------------------------------
//- Copyright (C) 2020-2025 Dmitry Borodkin <borodkin.dn@gmail.com>
//- SDPX-License-Identifier: LGPL-3.0-or-later
//---------------------------------------------------------------------
{   voidc_guard_target("defer.void  must be imported into the voidc target only!");

    v_import("level-00");

    v_import("llvm-c/Core.void");

    v_import("function_hack.void");
    v_import("if_then_else.void");
    v_import("block.void");
    v_import("grammar.void");
    v_import("expression.void");
}

{   voidc_enable_statement_if_then_else();
    voidc_enable_statement_block();
    voidc_enable_statement_grammar();
    voidc_enable_expression();
}


//---------------------------------------------------------------------
{   v_add_type("void_ptr", v_pointer_type(void, 0));

    q_ref = v_reference_type(v_quark_t, 0);

    q = v_quark_ptr_from_string;

    v_add_symbol("voidc_internal_branch_target_leave_q", q_ref, q("voidc.internal_branch_target_leave"));

    v_add_symbol("q_static_type_t", q_ref, q("v_static_type_t"));
    v_add_symbol("size_t_q",        q_ref, q("size_t"));

    v_add_symbol("q_alloca",     q_ref, q("v_alloca"));
    v_add_symbol("q_initialize", q_ref, q("v_initialize"));
    v_add_symbol("q_terminate",  q_ref, q("v_terminate"));
}

//---------------------------------------------------------------------
{   typ = v_alloca(v_type_ptr);


    //-----------------------------------------------------------------
    //- ast_stmt_defer_t ...
    //-----------------------------------------------------------------
    ast_stmt_defer_t = v_struct_type_named("struct.v_ast_stmt_defer");

    typ[0] := v_ast_stmt_t;

    v_type_struct_set_body(ast_stmt_defer_t, typ, 1, false);

    ast_stmt_defer_ptr = v_pointer_type(ast_stmt_defer_t, 0);

    v_export_type("v_ast_stmt_defer_t", ast_stmt_defer_t);

    v_add_type("ast_stmt_defer_t",   ast_stmt_defer_t);
    v_add_type("ast_stmt_defer_ptr", ast_stmt_defer_ptr);

    size_v = LLVMSizeOf(v_type_get_llvm_type(ast_stmt_defer_t));

    v_add_constant("ast_stmt_defer_t_size", size_t, size_v);
}


//=====================================================================
{   voidc_unit_begin_module("voidc_stmt_defer_module"); }


//---------------------------------------------------------------------
//- quark ...
//---------------------------------------------------------------------
{
    quark_name = "stmt_defer";

    quark_p = v_quark_ptr_from_string(quark_name);

    v_add_symbol("quark", v_reference_type(v_quark_t, 0), quark_p);
}


//---------------------------------------------------------------------
//- init ...
//---------------------------------------------------------------------
{
    //-----------------------------------------------------------------
    f = v_function_hack("voidc_ast_stmt_defer_init", v_ast_generic_init_fun_t);

    LLVMSetLinkage(f, LLVMPrivateLinkage);

    v_add_parameter_name(f, 0, "void_obj", void_ptr);
}
{
    obj = v_cast(void_obj, ast_stmt_defer_ptr);

    stmt = v_getelementptr(obj, 0, 0);

    v_initialize(stmt);
}


//---------------------------------------------------------------------
//- term ...
//---------------------------------------------------------------------
{
    //-----------------------------------------------------------------
    f = v_function_hack("voidc_ast_stmt_defer_term", v_ast_generic_term_fun_t);

    LLVMSetLinkage(f, LLVMPrivateLinkage);

    v_add_parameter_name(f, 0, "void_obj", void_ptr);
}
{
    obj = v_cast(void_obj, ast_stmt_defer_ptr);

    stmt = v_getelementptr(obj, 0, 0);

    v_terminate(stmt);
}


//---------------------------------------------------------------------
//- vtable ...
//---------------------------------------------------------------------
{
    module = v_get_module();

    val = v_alloca(LLVMValueRef, 3);

    init_f   = v_obtain_function("voidc_ast_stmt_defer_init", 0);
    term_f   = v_obtain_function("voidc_ast_stmt_defer_term", 0);
    quark_u  = LLVMGetUndef(v_type_get_llvm_type(v_quark_t));

    val[0] := init_f;
    val[1] := term_f;
    val[2] := quark_u;

    v_ast_generic_vtable_ = v_type_get_llvm_type(v_ast_generic_vtable);

    vtable_v = LLVMConstNamedStruct(v_ast_generic_vtable_, val, 3);

    vtable_g = LLVMAddGlobal(module, v_ast_generic_vtable_, "voidc_ast_stmt_defer_vtable");

    LLVMSetLinkage(vtable_g, LLVMPrivateLinkage);

    LLVMSetInitializer(vtable_g, vtable_v);

    //-----------------------------------------------------------------
    voidc_clear_unit_buffer();
}

{   v_add_symbol("voidc_ast_stmt_defer_vtable", v_ast_generic_vtable, 0);
}


//---------------------------------------------------------------------
//- v_ast_make_stmt_defer - ...
//---------------------------------------------------------------------
{
    typ = v_alloca(v_type_ptr, 2);

    typ[0] := v_ast_stmt_ptr;
    typ[1] := v_ast_stmt_ptr;

    ft = v_function_type(void, typ, 2, false);

    //-----------------------------------------------------------------
    f = v_function_hack("v_ast_make_stmt_defer", ft);

    v_add_parameter_name(f, 0, "ret",  v_ast_stmt_ptr);
    v_add_parameter_name(f, 1, "stmt", v_ast_stmt_ptr);
}
{
    stmp = v_alloca(v_ast_stmt_t);
    v_initialize(stmp);

    v_ast_make_generic(stmp, voidc_ast_stmt_defer_vtable, ast_stmt_defer_t_size);

    void_obj = v_ast_generic_get_object(stmp);

    obj = v_cast(void_obj, ast_stmt_defer_ptr);

    obj_stmt = v_getelementptr(obj, 0, 0);

    v_copy(obj_stmt, stmt);

    v_copy(ret, stmp);

    v_terminate(stmp);
}


//---------------------------------------------------------------------
//- mk_stmt_defer - grammar action
//---------------------------------------------------------------------
{
    //-----------------------------------------------------------------
    f = v_function_hack("mk_stmt_defer_grammar_action", v_peg_grammar_action_fun_t);

    LLVMSetLinkage(f, LLVMPrivateLinkage);

    v_add_parameter_name(f, 0, "ret",       v_std_any_ptr);
    v_add_parameter_name(f, 1, "aux",       void_ptr);
    v_add_parameter_name(f, 2, "any0",      v_std_any_ptr);
    v_add_parameter_name(f, 3, "any_count", size_t);
}
{
    stmt = v_std_any_get_pointer(v_ast_stmt_t, any0);

    stmt_ret = v_alloca(v_ast_stmt_t);
    v_initialize(stmt_ret);

    v_ast_make_stmt_defer(stmt_ret, stmt);

    v_std_any_set_pointer(ret, stmt_ret);

    v_terminate(stmt_ret);
}


//---------------------------------------------------------------------
//- compile_ast_stmt_defer_t - visitor method
//---------------------------------------------------------------------
{
    //-----------------------------------------------------------------
    f = v_function_hack("compile_ast_stmt_defer_t", voidc_visitor_method_t);

    LLVMSetLinkage(f, LLVMPrivateLinkage);

    v_add_parameter_name(f, 0, "aux",     void_ptr);
    v_add_parameter_name(f, 1, "visitor", voidc_visitor_ptr);
    v_add_parameter_name(f, 2, "self",    v_ast_base_ptr);
}
{
    void_obj = v_ast_generic_get_object(v_cast(self, v_ast_stmt_ptr));

    obj = v_cast(void_obj, ast_stmt_defer_ptr);

    stmt = v_getelementptr(obj, 0, 0);

    builder = v_target_get_builder();

    cur_b = LLVMGetInsertBlock(builder);

    cur_f = LLVMGetBasicBlockParent(cur_b);

    llvm_ctx = v_target_get_llvm_ctx();

    defer_b  = LLVMAppendBasicBlockInContext(llvm_ctx, cur_f, "defer_b");
    defer_bv = LLVMBasicBlockAsValue(defer_b);


    v_save_builder_ip();

    LLVMPositionBuilderAtEnd(builder, defer_b);


    base_ast = v_cast(stmt, v_ast_base_ptr);

    v_ast_accept_visitor(base_ast, visitor);


    leave_bv = v_get_variable_value_q(voidc_internal_branch_target_leave_q);
    leave_b  = LLVMValueAsBasicBlock(leave_bv);

    LLVMBuildBr(builder, leave_b);


    v_add_variable_q(voidc_internal_branch_target_leave_q, 0, defer_bv);        //- Sic!


    v_restore_builder_ip();
}


//---------------------------------------------------------------------
//- v_make_object - utility...
//---------------------------------------------------------------------
{
    //-----------------------------------------------------------------
    f = v_function_hack("v_make_object_intrinsic", voidc_intrinsic_t);

    LLVMSetLinkage(f, LLVMPrivateLinkage);

    v_add_parameter_name(f, 0, "aux",  void_ptr);
    v_add_parameter_name(f, 1, "vis",  voidc_visitor_ptr);
    v_add_parameter_name(f, 2, "self", v_ast_base_ptr);
}
{
    args = v_ast_expr_call_get_arg_list(v_cast(self, *v_ast_expr_t));

    ttag = v_get_result_type();
    vtag = v_get_result_value();

    static_type_t = v_find_type_q(q_static_type_t);

    builder = v_target_get_builder();

    v_set_result_type(static_type_t);
    v_set_result_value(0);

    arg0 = v_list_get_item(args, 0);                //- Type

    v_ast_accept_visitor(v_cast(arg0, *v_ast_base_t), vis);

    type = v_cast(v_get_result_value(), *v_type_t);

    size_t_type = v_find_type_q(size_t_q);

    count = v_reference(v_alloca(LLVMValueRef));

    if (v_list_get_size(args) > 1)
    {
        v_set_result_type(v_find_type_q(size_t_q));
        v_set_result_value(0);

        arg1 = v_list_get_item(args, 1);                //- Counter

        v_ast_accept_visitor(v_cast(arg1, *v_ast_base_t), vis);

        count := v_get_result_value();
    }
    else
    {
        count := LLVMConstInt(v_type_get_llvm_type(size_t_type), 1, false);
    }

    expr = v_alloca(v_ast_expr_t, 2);
    v_initialize(expr, 2);

    v_ast_make_expr_compiled(expr+0, static_type_t, v_cast(type, LLVMValueRef));

    v_ast_make_expr_compiled(expr+1, size_t_type, count);

    list = v_alloca(v_ast_expr_list_t);
    v_initialize(list);

    v_make_list(list, expr, 2);

    v_ast_make_expr_identifier_q(expr+0, q_alloca);

    v_ast_make_expr_call(expr+0, expr+0, list);         //- v_alloca

    v_set_result_type(INVIOLABLE_TAG);
    v_set_result_value(0);

    v_ast_accept_visitor(v_cast(expr+0, *v_ast_base_t), vis);

    t = v_get_result_type();
    v = v_get_result_value();

    v_ast_make_expr_compiled(expr+0, t, v);

    v_make_list(list, expr, 2);

    v_ast_make_expr_identifier_q(expr+0, q_initialize);

    v_ast_make_expr_call(expr+0, expr+0, list);         //- v_initialize

    v_set_result_type(INVIOLABLE_TAG);
    v_set_result_value(0);

    v_ast_accept_visitor(v_cast(expr+0, *v_ast_base_t), vis);

    v_ast_make_expr_identifier_q(expr+0, q_terminate);

    v_ast_make_expr_call(expr+0, expr+0, list);         //- v_terminate

    stmt = v_alloca(v_ast_stmt_t);
    v_initialize(stmt);

    v_ast_make_stmt(stmt, "", expr);

    v_ast_make_stmt_defer(stmt, stmt);                  //- defer

    v_set_result_type(INVIOLABLE_TAG);
    v_set_result_value(0);

    v_ast_accept_visitor(v_cast(stmt, *v_ast_base_t), vis);

    v_set_result_type(ttag);
    v_set_result_value(vtag);

    v_adopt_result(t, v);

    v_terminate(stmt);
    v_terminate(list);
    v_terminate(expr, 2);
}


//---------------------------------------------------------------------
//- Enable statement defer compiler
//---------------------------------------------------------------------
{
    ft = v_function_type(void, 0, 0, false);

    //-----------------------------------------------------------------
    v_function_hack("voidc_enable_statement_defer_compiler", ft);
}
{
    vis = v_get_compiler();

    v_return_if(voidc_visitor_get_method(vis, quark, 0));


    v_store(quark, v_getelementptr(voidc_ast_stmt_defer_vtable, 0, 2));

    voidc_visitor_set_method(vis, vis, quark, compile_ast_stmt_defer_t, 0);

    v_add_intrinsic("v_make_object", v_make_object_intrinsic, 0);
}


//---------------------------------------------------------------------
//- Enable statement defer
//---------------------------------------------------------------------
{
    ft = v_function_type(void, 0, 0, false);

    //-----------------------------------------------------------------
    v_function_hack("voidc_enable_statement_defer", ft);
}
{
    voidc_enable_statement_defer_compiler();


    gr0 = v_peg_get_grammar();

    v_return_if_not(gr0);
    v_return_if(v_peg_grammar_get_parser(gr0, "stmt_defer", 0));


    grammar gr0
    {
    actions:
        mk_stmt_defer = mk_stmt_defer_grammar_action;

    parsers:
        stmt_defer = "defer" !ident_cont _ s:stmt    { mk_stmt_defer(s) };

        stmt += stmt_defer;
    }
}


//=====================================================================
{   voidc_unit_end_module(); }


//---------------------------------------------------------------------
{
    v_make_export_symbol("v_ast_make_stmt_defer");

    v_make_export_symbol("voidc_enable_statement_defer_compiler");
    v_make_export_symbol("voidc_enable_statement_defer");
}


