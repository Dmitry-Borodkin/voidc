//---------------------------------------------------------------------
//- Copyright (C) 2020-2021 Dmitry Borodkin <borodkin-dn@yandex.ru>
//- SDPX-License-Identifier: LGPL-3.0-or-later
//---------------------------------------------------------------------
{   voidc_guard_import("arithmetic.void"); }

{   v_import("function_hack.void");
    v_import("if_then_else.void");
}

{   voidc_enable_statement_if_then_else();
}


//---------------------------------------------------------------------
//- ...
//---------------------------------------------------------------------
{
    module = LLVMModuleCreateWithName("voidc_arithmetic_intrinsic_module");

    v_set_module(module);       //- Sic!

    void_ptr = v_pointer_type(void, 0);
    v_add_local_symbol("void_ptr", voidc_opaque_type, void_ptr);
}


//---------------------------------------------------------------------
//- v_binop
//---------------------------------------------------------------------
{
    //-----------------------------------------------------------------
    f = v_function_hack("voidc_binop_intrinsic", voidc_intrinsic_t);

    v_add_parameter_name(f, 0, "visitor",  voidc_visitor_ptr);
    v_add_parameter_name(f, 1, "aux",      void_ptr);
    v_add_parameter_name(f, 2, "arg_list", v_ast_arg_list_ptr);
}
{
    builder = v_target_get_builder();

    arg_base = v_cast(arg_list, v_ast_base_ptr);

    v_ast_accept_visitor(arg_base, visitor, aux);

    arg0 = v_get_argument_value(0);

    opv = LLVMConstIntGetZExtValue(arg0);

    opc = v_cast(opv, LLVMOpcode);

    lhs = v_get_argument_value(1);
    rhs = v_get_argument_value(2);

    ret = LLVMBuildBinOp(builder, opc, lhs, rhs, "");

    v_set_return_value(ret);

    t = v_get_argument_type(1);
    v_set_return_type(t);

    v_clear_arguments();
}


//---------------------------------------------------------------------
//- Some utility...
//---------------------------------------------------------------------
{
    typ0 = v_alloca(v_type_ptr);

    v_store(v_type_ptr, typ0);

    ft = v_function_type(v_type_ptr, typ0, 1, false);

    //-----------------------------------------------------------------
    f = v_function_hack("make_return_type", ft);

    v_add_parameter_name(f, 0, "type", v_type_ptr);
}
{
    bool_type = v_find_type("bool");


    f = v_type_is_vector(type);

    if (f)  ;
    else    v_return(bool_type);

    sz = v_type_vector_get_size(type);
    sc = v_type_vector_is_scalable(type);

    fv_type = v_fvector_type(bool_type, sz);

    if (sc) ;
    else    v_return(fv_type);

    sv_type = v_svector_type(bool_type, sz);

    v_return(sv_type);
}


//---------------------------------------------------------------------
//- v_icmp
//---------------------------------------------------------------------
{
    //-----------------------------------------------------------------
    f = v_function_hack("voidc_icmp_intrinsic", voidc_intrinsic_t);

    v_add_parameter_name(f, 0, "visitor",  voidc_visitor_ptr);
    v_add_parameter_name(f, 1, "aux",      void_ptr);
    v_add_parameter_name(f, 2, "arg_list", v_ast_arg_list_ptr);
}
{
    builder = v_target_get_builder();

    arg_base = v_cast(arg_list, v_ast_base_ptr);

    v_ast_accept_visitor(arg_base, visitor, aux);

    arg0 = v_get_argument_value(0);

    opv = LLVMConstIntGetZExtValue(arg0);

    opc = v_cast(opv, LLVMIntPredicate);

    lhs = v_get_argument_value(1);
    rhs = v_get_argument_value(2);

    ret = LLVMBuildICmp(builder, opc, lhs, rhs, "");

    v_set_return_value(ret);

    t = v_get_argument_type(1);

    t = make_return_type(t);

    v_set_return_type(t);

    v_clear_arguments();
}


//---------------------------------------------------------------------
//- v_fcmp
//---------------------------------------------------------------------
{
    //-----------------------------------------------------------------
    f = v_function_hack("voidc_fcmp_intrinsic", voidc_intrinsic_t);

    v_add_parameter_name(f, 0, "visitor",  voidc_visitor_ptr);
    v_add_parameter_name(f, 1, "aux",      void_ptr);
    v_add_parameter_name(f, 2, "arg_list", v_ast_arg_list_ptr);
}
{
    builder = v_target_get_builder();

    arg_base = v_cast(arg_list, v_ast_base_ptr);

    v_ast_accept_visitor(arg_base, visitor, aux);

    arg0 = v_get_argument_value(0);

    opv = LLVMConstIntGetZExtValue(arg0);

    opc = v_cast(opv, LLVMRealPredicate);

    lhs = v_get_argument_value(1);
    rhs = v_get_argument_value(2);

    ret = LLVMBuildFCmp(builder, opc, lhs, rhs, "");

    v_set_return_value(ret);

    t = v_get_argument_type(1);

    t = make_return_type(t);

    v_set_return_type(t);

    v_clear_arguments();
}


//---------------------------------------------------------------------
//- ...
//---------------------------------------------------------------------
{
    v_make_function_private("make_return_type");

    module = v_get_module();

    v_set_module(0);

    //-----------------------------------------------------------------
//  v_debug_print_module(1);

    voidc_prepare_module_for_jit(module);

    //-----------------------------------------------------------------
    PH = v_alloca(LLVMOrcModuleHandle);

    LLVMOrcAddEagerlyCompiledIR(voidc_jit, PH, module,
                                voidc_resolver, 0
                               );
}

{   v_add_intrinsic("v_binop", voidc_binop_intrinsic);
    v_add_intrinsic("v_icmp",  voidc_icmp_intrinsic);
    v_add_intrinsic("v_fcmp",  voidc_fcmp_intrinsic);
}


