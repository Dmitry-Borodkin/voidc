//---------------------------------------------------------------------
//- Copyright (C) 2020-2022 Dmitry Borodkin <borodkin.dn@gmail.com>
//- SDPX-License-Identifier: LGPL-3.0-or-later
//---------------------------------------------------------------------
{   voidc_guard_target("loops_etc.void  must be imported into the voidc target only!");

    v_import("level-00");
    v_import("level-01");

    v_import("llvm-c/Core.void");

    v_import("unit_module.void");

    v_import("printf.void");
}

{   v_enable_level_01(); }


//---------------------------------------------------------------------
{   voidc_unit_begin_module("loops_etc_module"); }


//---------------------------------------------------------------------
mk_stmt_while_grammar_action: (ret: v_std_any_ptr, any: v_std_any_ptr, size_t) -> void
{
    cond = v_std_any_get_pointer(v_ast_stmt_list_t, any+0);
    stmt = v_std_any_get_pointer(v_ast_stmt_t,      any+1);

    //- loop
    //- {
    //-     <cond>...
    //-
    //-     if (<cond>)     //- Kinda...
    //-     <stmt>
    //-     else  v_break();
    //- }


    cond_len: &int := v_list_get_size(cond);

    cstm = v_alloca(v_ast_stmt_t);
    v_initialize(cstm);
    defer v_terminate(cstm);

    v_list_get_items(cond, cond_len-1, cstm);


    cexp = v_alloca(v_ast_expr_t);
    v_initialize(cexp);
    defer v_terminate(cexp);

    name = v_ast_stmt_get_name(cstm);

    if (name[0])
    {
        v_ast_make_expr_identifier(cexp, name);
    }
    else
    {
        v_ast_stmt_get_expr(cstm, cexp);

        --cond_len;
    }


    //- cstm - break

    v_ast_make_stmt_call(cstm, "", "v_break", 0, 0);


    stmp_len = cond_len + 1;


    stmp = v_alloca(v_ast_stmt_t, stmp_len);
    v_initialize(stmp, stmp_len);
    defer v_terminate(stmp, stmp_len);


    stmp_cond = stmp + cond_len;

    //- stmp_cond - if

    v_ast_make_stmt_if_then_else(stmp_cond, cexp, stmt, cstm);


    //- stmp+0 - loop

    if (cond_len)
    {
        //- stmp+0 - block

        v_list_get_items(cond, 0, stmp+0, cond_len);

        slst = v_alloca(v_ast_stmt_list_t);
        v_initialize(slst);
        defer v_terminate(slst);

        v_make_list(slst, stmp+0, cond_len+1);

        v_ast_make_stmt_block(stmp+0, slst, false);
    }


    v_ast_make_stmt_loop(stmp+0, stmp+0);

    v_std_any_set_pointer(ret, stmp+0);
}


//---------------------------------------------------------------------
v_internal_copy_variable_intrinsic: (vis: voidc_visitor_ptr, *void,
                                     args: v_ast_expr_list_ptr) -> void
{
    expr = v_alloca(v_ast_expr_t, 2);
    v_initialize(expr, 2);
    defer v_terminate(expr, 2);

    v_list_get_items(args, 0, expr, 2);

    name_dst = v_ast_expr_string_get_string(expr+0);
    name_src = v_ast_expr_string_get_string(expr+1);

    src_type  = v_get_variable_type(name_src);
    src_value = v_get_variable_value(name_src);

    v_add_variable(name_dst, src_type, src_value);
}

//---------------------------------------------------------------------
mk_stmt_for_grammar_action: (ret: v_std_any_ptr, any: v_std_any_ptr, size_t) -> void
{
    init = v_std_any_get_pointer(v_ast_stmt_list_t, any+0);
    cond = v_std_any_get_pointer(v_ast_stmt_list_t, any+1);
    incr = v_std_any_get_pointer(v_ast_stmt_list_t, any+2);
    stmt = v_std_any_get_pointer(v_ast_stmt_t,      any+3);

    //- {   <init>...
    //-
    //-     loop
    //-     {
    //-         v.internal.copy_variable("voidc.internal_branch_target_break_for_variable",
    //-                                  "voidc.internal_branch_target_break_variable");
    //-
    //-         v.internal.copy_variable("voidc.internal_branch_target_break_for",
    //-                                  "voidc.internal_branch_target_break");
    //-
    //-         <cond>...
    //-
    //-         if (<cond>)     //- Kinda...
    //-         block
    //-         {
    //-             v.internal.copy_variable("voidc.internal_branch_target_continue_variable",
    //-                                      "voidc.internal_branch_target_break_variable");
    //-
    //-             v.internal.copy_variable("voidc.internal_branch_target_continue",
    //-                                      "voidc.internal_branch_target_break");
    //-
    //-             v.internal.copy_variable("voidc.internal_branch_target_break_variable",
    //-                                      "voidc.internal_branch_target_break_for_variable");
    //-
    //-             v.internal.copy_variable("voidc.internal_branch_target_break",
    //-                                      "voidc.internal_branch_target_break_for");
    //-
    //-             <stmt>
    //-         }
    //-         else v_break();
    //-
    //-         <incr>...
    //-     }
    //- }

    etmp = v_alloca(v_ast_expr_t, 9);
    v_initialize(etmp, 9);
    defer v_terminate(etmp, 9);

    e_break_for_var = etmp+2;
    e_break_for     = etmp+3;
    e_break_var     = etmp+4;
    e_break         = etmp+5;
    e_continue_var  = etmp+6;
    e_continue      = etmp+7;
    e_cond          = etmp+8;

    v_ast_make_expr_string(e_break_for_var, "voidc.internal_branch_target_break_for_variable");
    v_ast_make_expr_string(e_break_for,     "voidc.internal_branch_target_break_for");
    v_ast_make_expr_string(e_break_var,     "voidc.internal_branch_target_break_variable");
    v_ast_make_expr_string(e_break,         "voidc.internal_branch_target_break");
    v_ast_make_expr_string(e_continue_var,  "voidc.internal_branch_target_continue_variable");
    v_ast_make_expr_string(e_continue,      "voidc.internal_branch_target_continue");


    init_len = v_list_get_size(init);

    cond_len: &int := v_list_get_size(cond);

    cstm = v_alloca(v_ast_stmt_t);
    v_initialize(cstm);
    defer v_terminate(cstm);

    if (cond_len)
    {
        v_list_get_items(cond, cond_len-1, cstm);

        name = v_ast_stmt_get_name(cstm);

        if (name[0])
        {
            v_ast_make_expr_identifier(e_cond, name);
        }
        else
        {
            v_ast_stmt_get_expr(cstm, e_cond);

            --cond_len;
        }
    }
    else
    {
        v_ast_make_expr_identifier(e_cond, "true");
    }


    //- cstm - break

    v_ast_make_stmt_call(cstm, "", "v_break", 0, 0);


    incr_len = v_list_get_size(incr);


    stmp_len: &int := 4 + 1;        //- Inner block ("if then")

    {   l = 2 + cond_len + 1 + incr_len;

        if (stmp_len < l)   stmp_len := l;      //- Loop's block

        l = init_len + 1;

        if (stmp_len < l)   stmp_len := l;      //- Outer block
    }


    stmp = v_alloca(v_ast_stmt_t, stmp_len);
    v_initialize(stmp, stmp_len);
    defer v_terminate(stmp, stmp_len);


    //- stmp_cond+0 - if

    stmp_cond = stmp + 2 + cond_len;


    v_copy(etmp+0, e_continue_var);
    v_copy(etmp+1, e_break_var);

    v_ast_make_stmt_call(stmp+0, "", "v.internal.copy_variable", etmp+0, 2);

    v_copy(etmp+0, e_continue);
    v_copy(etmp+1, e_break);

    v_ast_make_stmt_call(stmp+1, "", "v.internal.copy_variable", etmp+0, 2);

    v_copy(etmp+0, e_break_var);
    v_copy(etmp+1, e_break_for_var);

    v_ast_make_stmt_call(stmp+2, "", "v.internal.copy_variable", etmp+0, 2);

    v_copy(etmp+0, e_break);
    v_copy(etmp+1, e_break_for);

    v_ast_make_stmt_call(stmp+3, "", "v.internal.copy_variable", etmp+0, 2);


    v_copy(stmp+4, stmt);


    slst = v_alloca(v_ast_stmt_list_t);
    v_initialize(slst);
    defer v_terminate(slst);

    v_make_list(slst, stmp+0, 5);

    v_ast_make_stmt_block(stmp+0, slst, true);

    v_ast_make_stmt_if_then_else(stmp_cond+0, e_cond, stmp+0, cstm);


    //- stmp_init+0 - loop

    stmp_init = stmp + init_len;


    v_copy(etmp+0, e_break_for_var);
    v_copy(etmp+1, e_break_var);

    v_ast_make_stmt_call(stmp+0, "", "v.internal.copy_variable", etmp+0, 2);

    v_copy(etmp+0, e_break_for);
    v_copy(etmp+1, e_break);

    v_ast_make_stmt_call(stmp+1, "", "v.internal.copy_variable", etmp+0, 2);


    if (cond_len) v_list_get_items(cond, 0, stmp+2, cond_len);


    if (incr_len) v_list_get_items(incr, 0, stmp_cond+1, incr_len);


    v_make_list(slst, stmp+0, 2+cond_len+1+incr_len);

    v_ast_make_stmt_block(stmp+0, slst, false);

    v_ast_make_stmt_loop(stmp_init+0, stmp+0);


    if (init_len) v_list_get_items(init, 0, stmp+0, init_len);


    v_make_list(slst, stmp+0, init_len+1);

    v_ast_make_stmt_block(stmp+0, slst, false);


    v_std_any_set_pointer(ret, stmp+0);
}


//---------------------------------------------------------------------
mk_stmt_if_then_else_2_grammar_action: (ret: v_std_any_ptr, any: v_std_any_ptr, size_t) -> void
{
    cond = v_std_any_get_pointer(v_ast_stmt_list_t, any+0);
    tstm = v_std_any_get_pointer(v_ast_stmt_t,      any+1);
    estm = v_std_any_get_pointer(v_ast_stmt_t,      any+2);

    //- {   <cond>...
    //-
    //-     if (<cond>)     //- Kinda...
    //-     <tstm>
    //-     else <estm>
    //- }


    cond_len: &int := v_list_get_size(cond);

    cstm = v_alloca(v_ast_stmt_t);
    v_initialize(cstm);
    defer v_terminate(cstm);

    v_list_get_items(cond, cond_len-1, cstm);


    cexp = v_alloca(v_ast_expr_t);
    v_initialize(cexp);
    defer v_terminate(cexp);

    name = v_ast_stmt_get_name(cstm);

    if (name[0])
    {
        v_ast_make_expr_identifier(cexp, name);
    }
    else
    {
        v_ast_stmt_get_expr(cstm, cexp);

        --cond_len;
    }


    stmp_len = cond_len + 1;


    stmp = v_alloca(v_ast_stmt_t, stmp_len);
    v_initialize(stmp, stmp_len);
    defer v_terminate(stmp, stmp_len);


    //- stmp_cond+0 - if

    stmp_cond = stmp + cond_len;


    else_stmt = stmp_cond+0;    //- Temporary...

    if (estm)   v_copy(else_stmt, estm);

    v_ast_make_stmt_if_then_else(stmp_cond+0, cexp, tstm, else_stmt);


    v_list_get_items(cond, 0, stmp+0, cond_len);

    slst = v_alloca(v_ast_stmt_list_t);
    v_initialize(slst);
    defer v_terminate(slst);

    v_make_list(slst, stmp+0, cond_len+1);

    v_ast_make_stmt_block(stmp+0, slst, false);


    v_std_any_set_pointer(ret, stmp+0);
}


//---------------------------------------------------------------------
mk_stmt_switch_2_grammar_action: (ret: v_std_any_ptr, any: v_std_any_ptr, size_t) -> void
{
    estm = v_std_any_get_pointer(v_ast_stmt_list_t,    any+0);
    glst = v_std_any_get_pointer(v_ast_generic_list_t, any+1);

    //- {   <estm>...
    //-
    //-     switch (<estm>)     //- Kinda...
    //-     <glst>
    //- }


    estm_len: &int := v_list_get_size(estm);

    etmp = v_alloca(v_ast_expr_t);
    v_initialize(etmp);
    defer v_terminate(etmp);

    {   stmt = v_alloca(v_ast_stmt_t);
        v_initialize(stmt);
        defer v_terminate(stmt);

        v_list_get_items(estm, estm_len-1, stmt);

        name = v_ast_stmt_get_name(stmt);

        if (name[0])
        {
            v_ast_make_expr_identifier(etmp, name);
        }
        else
        {
            v_ast_stmt_get_expr(stmt, etmp);

            --estm_len;
        }
    }


    stmp_len = estm_len + 1;


    stmp = v_alloca(v_ast_stmt_t, stmp_len);
    v_initialize(stmp, stmp_len);
    defer v_terminate(stmp, stmp_len);


    //- stmp_estm+0 - switch

    stmp_estm = stmp + estm_len;


    v_ast_make_stmt_switch(stmp_estm+0, etmp, glst);


    if (estm_len)   v_list_get_items(estm, 0, stmp+0, estm_len);        //- if?


    slst = v_alloca(v_ast_stmt_list_t);
    v_initialize(slst);
    defer v_terminate(slst);

    v_make_list(slst, stmp+0, stmp_len);

    v_ast_make_stmt_block(stmp+0, slst, false);


    v_std_any_set_pointer(ret, stmp+0);
}


//---------------------------------------------------------------------
//- Enable loops etc
//---------------------------------------------------------------------
export
voidc_enable_loops_etc_compiler: () -> void
{
    vis = voidc_compiler;

    v_return_if(voidc_visitor_get_intrinsic(vis, "v.internal.copy_variable", 0));

    voidc_visitor_set_intrinsic(vis, vis, "v.internal.copy_variable", v_internal_copy_variable_intrinsic, 0);

    voidc_enable_statement_block_compiler();
    voidc_enable_statement_loop_compiler();

    voidc_enable_statement_if_then_else_compiler();
    voidc_enable_statement_switch_compiler();
}


//---------------------------------------------------------------------
//- Enable loops etc
//---------------------------------------------------------------------
export
voidc_enable_loops_etc: () -> void
{
    voidc_enable_loops_etc_compiler();


    v_return_if_not(voidc_has_grammar());
    v_return_if(voidc_grammar_has_parser("stmt_while"));


    voidc_enable_statement_block();
    voidc_enable_statement_loop();

    voidc_enable_statement_if_then_else();
    voidc_enable_statement_switch();


    gr0 = v_alloca(v_peg_opaque_grammar_sptr);
    v_initialize(gr0);
    defer v_terminate(gr0);

    v_peg_get_grammar(gr0);

    grammar gr0
    {
    actions:
        mk_stmt_while = mk_stmt_while_grammar_action;
        mk_stmt_for   = mk_stmt_for_grammar_action;

        mk_stmt_if_then_else_2 = mk_stmt_if_then_else_2_grammar_action;
        mk_stmt_switch_2       = mk_stmt_switch_2_grammar_action;

    parsers:
        simple_stmt = i:identifier _':'_ t: expr _'='_ v:expr       { mk_stmt_val_defn(i, t, v) }
                    / i:identifier _':'_ tv: expr                   { mk_stmt_var_defn(i, tv) }         //- Sic!!!
                    / i:identifier _ '=' _ v:expr                   { mk_stmt(i, v) }
                    / v:expr                                        { mk_stmt(0, v) }
                    ;

        simple_stmt_list_lr = l:simple_stmt_list_lr _','_ s:simple_stmt     { mk_stmt_list(l, s) }
                            / s:simple_stmt                                 { mk_stmt_list(0, s) }
                            ;

        simple_stmt_list_lr is left-recursive;

        simple_stmt_list = simple_stmt_list_lr
                         /                          { mk_stmt_list(0, 0) }
                         ;


        stmt_while = "while" _'('_ c:simple_stmt_list_lr _')'_ s:stmt   { mk_stmt_while(c, s) };


        stmt_for = "for" _'('_ i:simple_stmt_list _';'_
                               c:simple_stmt_list _';'_
                               r:simple_stmt_list
                         _')'_ s:stmt                       { mk_stmt_for(i, c, r, s) }
                 ;

        stmt += stmt_while / stmt_for;


        stmt_if_then_else =+ "if" _'('_ c:simple_stmt_list_lr _')'_ t:stmt
                             e:(_ "else" !ident_cont _ stmt)?           { mk_stmt_if_then_else_2(c, t, e) };


        stmt_switch =+ "switch" _'('_ e:simple_stmt_list_lr _')'_
                       '{'_ l:case_list _'}'    { mk_stmt_switch_2(e, l) };
    }

    v_peg_set_grammar(gr0);
}


//---------------------------------------------------------------------
{   voidc_unit_end_module(); }


