//---------------------------------------------------------------------
//- Copyright (C) 2020-2024 Dmitry Borodkin <borodkin.dn@gmail.com>
//- SDPX-License-Identifier: LGPL-3.0-or-later
//---------------------------------------------------------------------
{   voidc_guard_target("condcomp.void  must be imported into the voidc target only!");

    v_import("level-00");
    v_import("level-01");

    v_import("llvm-c/Core.void");

    v_import("loops_etc.void");

//  v_import("printf.void");
}

{   v_enable_level_01();

    voidc_enable_loops_etc();
}


//---------------------------------------------------------------------
{
    q_ref = v_reference_type(v_quark_t, 0);

    q = v_quark_ptr_from_string;

    v_add_symbol("unit_ccif_list_q", q_ref, q("unit_ccif_list"));
    v_add_symbol("stmt_ccif_list_q", q_ref, q("stmt_ccif_list"));
    v_add_symbol("expr_ccif_list_q", q_ref, q("expr_ccif_list"));

    v_add_symbol("cc_message_q", q_ref, q("cc_message"));
    v_add_symbol("cc_do_q",      q_ref, q("cc_do"));
    v_add_symbol("cc_assert_q",  q_ref, q("cc_assert"));

    v_add_symbol("pos_start_q", q_ref, q("pos_start"));
    v_add_symbol("pos_end_q",   q_ref, q("pos_end"));

    v_add_symbol("char_q",     q_ref, q("char"));
    v_add_symbol("unsigned_q", q_ref, q("unsigned"));

    v_add_symbol("q_assert", q_ref, q("v_assert"));
}


//=====================================================================
{   voidc_unit_begin_module("condcomp_module_N1"); }


//---------------------------------------------------------------------
//- Grammar actions ...
//---------------------------------------------------------------------
private
mk_unit_ccif_grammar_action: (ret: *v_std_any_t, *void, any: *v_std_any_t, size_t) ~> void
{
    list = v_std_any_get_pointer(v_ast_generic_list_t, any+0);
    cond = v_std_any_get_pointer(v_ast_expr_t,         any+1);
    undl = v_std_any_get_pointer(v_ast_generic_list_t, any+2);

    if (!undl)
    {
        v_std_any_set_pointer(ret, (list: *v_ast_base_t));

        v_return();
    }

    expr = v_alloca(v_ast_expr_t);
    v_initialize(expr);
    defer v_terminate(expr);

    if (cond)   v_copy(expr, cond);

    ccif = v_alloca(v_ast_generic_list_t);
    v_initialize(ccif);
    defer v_terminate(ccif);

    if (list)   v_list_append(ccif, list, (expr: *v_ast_base_t));
    else        v_make_list(ccif, unit_ccif_list_q, (expr: *v_ast_base_t));

    v_list_append(ccif, ccif, (undl: *v_ast_base_t));

    v_std_any_set_pointer(ret, ccif);
}

//---------------------------------------------------------------------
private
mk_stmt_ccif_grammar_action: (ret: *v_std_any_t, *void, any: *v_std_any_t, size_t) ~> void
{
    list = v_std_any_get_pointer(v_ast_generic_list_t, any+0);
    cond = v_std_any_get_pointer(v_ast_expr_t,         any+1);
    slst = v_std_any_get_pointer(v_ast_stmt_list_t,    any+2);

    if (!slst)
    {
        v_std_any_set_pointer(ret, (list: *v_ast_stmt_t));

        v_return();
    }

    expr = v_alloca(v_ast_expr_t);
    v_initialize(expr);
    defer v_terminate(expr);

    if (cond)   v_copy(expr, cond);

    ccif = v_alloca(v_ast_generic_list_t);
    v_initialize(ccif);
    defer v_terminate(ccif);

    if (list)   v_list_append(ccif, list, (expr: *v_ast_base_t));
    else        v_make_list(ccif, stmt_ccif_list_q, (expr: *v_ast_base_t));

    v_list_append(ccif, ccif, (slst: *v_ast_base_t));

    v_std_any_set_pointer(ret, ccif);
}

//---------------------------------------------------------------------
private
mk_expr_ccif_grammar_action: (ret: *v_std_any_t, *void, any: *v_std_any_t, size_t) ~> void
{
    list = v_std_any_get_pointer(v_ast_generic_list_t, any+0);
    cond = v_std_any_get_pointer(v_ast_expr_t,         any+1);
    expr = v_std_any_get_pointer(v_ast_expr_t,         any+2);

    if (!expr)
    {
        v_std_any_set_pointer(ret, (list: *v_ast_expr_t));

        v_return();
    }

    etmp = v_alloca(v_ast_expr_t);
    v_initialize(etmp);
    defer v_terminate(etmp);

    if (cond)   v_copy(etmp, cond);

    ccif = v_alloca(v_ast_generic_list_t);
    v_initialize(ccif);
    defer v_terminate(ccif);

    if (list)   v_list_append(ccif, list, (etmp: *v_ast_base_t));
    else        v_make_list(ccif, expr_ccif_list_q, (etmp: *v_ast_base_t));

    v_list_append(ccif, ccif, (expr: *v_ast_base_t));

    v_std_any_set_pointer(ret, ccif);
}


//---------------------------------------------------------------------
//- Compiler methods...
//---------------------------------------------------------------------
private
compile_unit_ccif_list: (aux: *void, vis: *voidc_visitor_t, self: *v_ast_base_t) ~> void
{
    list = (self: *v_ast_generic_list_t);

    size = v_list_get_size(list);

    builder = v_target_get_builder();

    for (i: &int := 0; i < size; i += 2)
    {
        cond = v_list_get_item(list, i);

        if (!v_empty(cond))
        {
            v_set_result_type(UNREFERENCE_TAG);
            v_set_result_value(0);

            v_ast_accept_visitor(cond, vis);

            v = v_get_result_value();

            v = LLVMBuildIsNotNull(builder, v, "");

            if (!LLVMConstIntGetZExtValue(v))  v_continue();
        }

        undl = (v_list_get_item(list, i+1) : *v_ast_generic_list_t);

        n = v_list_get_size(undl);

        for (j: &int := 0; j < n; ++j)
        {
            item = v_list_get_item(undl, j);

            v_ast_accept_visitor(item, vis);
        }

        v_return();
    }
}

//---------------------------------------------------------------------
private
compile_stmt_ccif_list: (aux: *void, vis: *voidc_visitor_t, self: *v_ast_base_t) ~> void
{
    list = (self: *v_ast_generic_list_t);

    size = v_list_get_size(list);

    builder = v_target_get_builder();

    for (i: &int := 0; i < size; i += 2)
    {
        cond = v_list_get_item(list, i);

        if (!v_empty(cond))
        {
            v_set_result_type(UNREFERENCE_TAG);
            v_set_result_value(0);

            v_ast_accept_visitor(cond, vis);

            v = v_get_result_value();

            v = LLVMBuildIsNotNull(builder, v, "");

            if (!LLVMConstIntGetZExtValue(v))  v_continue();
        }

        slst = v_list_get_item(list, i+1);

        v_ast_accept_visitor(slst, vis);

        v_return();
    }
}

//---------------------------------------------------------------------
private
compile_expr_ccif_list: (aux: *void, vis: *voidc_visitor_t, self: *v_ast_base_t) ~> void
{
    ttag = v_get_result_type();
    vtag = v_get_result_value();

    list = (self: *v_ast_generic_list_t);

    size = v_list_get_size(list);

    builder = v_target_get_builder();

    for (i: &int := 0; i < size; i += 2)
    {
        cond = v_list_get_item(list, i);

        if (!v_empty(cond))
        {
            v_set_result_type(UNREFERENCE_TAG);
            v_set_result_value(0);

            v_ast_accept_visitor(cond, vis);

            v = v_get_result_value();

            v = LLVMBuildIsNotNull(builder, v, "");

            if (!LLVMConstIntGetZExtValue(v))  v_continue();
        }

        expr = v_list_get_item(list, i+1);

        v_set_result_type(ttag);
        v_set_result_value(vtag);

        v_ast_accept_visitor(expr, vis);

        v_return();
    }
}


//---------------------------------------------------------------------
//- v_defined intrinsic
//---------------------------------------------------------------------
private
v_defined_intrinsic: (*void, vis: *voidc_visitor_t, self: *v_ast_base_t) ~> void
{
    args = v_ast_expr_call_get_arg_list((self: *v_ast_expr_t));

    name = v_ast_expr_identifier_get_name(v_list_get_item(args, 0));

    ok = v_find_constant(name, 0, 0);

    bool_ = v_uint_type(1);

    ok = LLVMConstInt(v_type_get_llvm_type(bool_), ok, false);

    v_adopt_result(bool_, ok);
}


//---------------------------------------------------------------------
//- Enable conditional compilation #1
//---------------------------------------------------------------------
voidc_enable_condcomp_n1_compiler: () ~> void
{
    vis = v_get_compiler();

    v_return_if(voidc_visitor_get_method(vis, unit_ccif_list_q, 0));

    voidc_visitor_set_method(vis, vis, unit_ccif_list_q, compile_unit_ccif_list, 0);
    voidc_visitor_set_method(vis, vis, stmt_ccif_list_q, compile_stmt_ccif_list, 0);
    voidc_visitor_set_method(vis, vis, expr_ccif_list_q, compile_expr_ccif_list, 0);

    v_add_intrinsic("v_defined", v_defined_intrinsic, 0);
}

//---------------------------------------------------------------------
//- Enable conditional compilation #1
//---------------------------------------------------------------------
voidc_enable_condcomp_n1: () ~> void
{
    voidc_enable_condcomp_n1_compiler();

    v_return_if_not(voidc_has_grammar());
    v_return_if(voidc_grammar_has_parser("unit_ccif"));

    gr0 = v_alloca(v_peg_grammar_t);
    v_initialize(gr0);
    defer v_terminate(gr0);

    v_peg_get_grammar(gr0);

    grammar gr0
    {
    actions:
        mk_unit_ccif = mk_unit_ccif_grammar_action;
        mk_stmt_ccif = mk_stmt_ccif_grammar_action;
        mk_expr_ccif = mk_expr_ccif_grammar_action;

    parsers:
        //-------------------------------------------------------------

        unit_ccif_lr = l:unit_ccif_lr _ "#elif" _'('_ c:expr _')'_ u:unit_defn_list     { mk_unit_ccif(l, c, u) }
                     / "#if" _'('_ c:expr _')'_ u:unit_defn_list                        { mk_unit_ccif(0, c, u) }
                     ;

        unit_ccif_lr is left-recursive;


        unit_ccif_list = l:unit_ccif_lr _ "#else" !ident_cont _ u:unit_defn_list        { mk_unit_ccif(l, 0, u) }
                       / unit_ccif_lr
                       ;

        unit_ccif = l:unit_ccif_list _ "#endif" !ident_cont     { mk_unit_ccif(l, 0, 0) };

        unit_defn =+ unit_ccif;

        //-------------------------------------------------------------

        stmt_ccif_lr = l:stmt_ccif_lr _ "#elif" _'('_ c:expr _')'_ s:stmt_list  { mk_stmt_ccif(l, c, s) }
                     / "#if" _'('_ c:expr _')'_ s:stmt_list                     { mk_stmt_ccif(0, c, s) }
                     ;

        stmt_ccif_lr is left-recursive;


        stmt_ccif_list = l:stmt_ccif_lr _ "#else" !ident_cont _ s:stmt_list     { mk_stmt_ccif(l, 0, s) }
                       / stmt_ccif_lr
                       ;

        stmt_ccif = l:stmt_ccif_list _ "#endif" !ident_cont     { mk_stmt_ccif(l, 0, 0) };

        stmt =+ stmt_ccif;

        //-------------------------------------------------------------

        expr_ccif_lr = l:expr_ccif_lr _ "#elif" _'('_ c:expr _')'_ e:expr       { mk_expr_ccif(l, c, e) }
                     / "#if" _'('_ c:expr _')'_ e:expr                          { mk_expr_ccif(0, c, e) }
                     ;

        expr_ccif_lr is left-recursive;


        expr_ccif_list = l:expr_ccif_lr _ "#else" !ident_cont _ e:expr          { mk_expr_ccif(l, 0, e) }
                       //- Sic !!!
                       ;

        expr_ccif = l:expr_ccif_list _ "#endif" !ident_cont     { mk_expr_ccif(l, 0, 0) };

        expr =+ expr_ccif;
    }

    v_peg_set_grammar(gr0);
}


//=====================================================================
{   voidc_unit_end_module(); }

//---------------------------------------------------------------------
{   voidc_enable_condcomp_n1(); }


//=====================================================================
{   voidc_unit_begin_module("condcomp_module_N2"); }


//---------------------------------------------------------------------
//- Grammar actions ...
//---------------------------------------------------------------------
private
mk_cc_message_grammar_action: (ret: *v_std_any_t, *void, any: *v_std_any_t, size_t) ~> void
{
    pos  = v_std_any_get_value(size_t, any+0);
    tstr = v_std_any_get_pointer(v_std_string_t,    any+1);
    elst = v_std_any_get_pointer(v_ast_expr_list_t, any+2);

    list = v_alloca(v_ast_generic_list_t);
    v_initialize(list);
    defer v_terminate(list);

    v_make_list_nil(list, cc_message_q);

    expr = v_alloca(v_ast_expr_t);
    v_initialize(expr);
    defer v_terminate(expr);

    filename = v_target_local_ctx_get_filename(v_target_get_local_ctx());

    v_ast_make_expr_string(expr, filename);
    v_list_append(list, list, (expr: *v_ast_base_t));

    line:   &size_t := v_undef();
    column: &size_t := v_undef();

    v_peg_get_line_column(pos, line, column);

    v_ast_make_expr_integer(expr, (line: intptr_t));
    v_list_append(list, list, (expr: *v_ast_base_t));

    v_ast_make_expr_string(expr, v_std_string_get(tstr));
    v_list_append(list, list, (expr: *v_ast_base_t));

    v_list_append(list, list, (elst: *v_ast_base_t));

    kind = v_std_any_get_value(intptr_t, any+3);

    switch(kind)
    {
    case 0:   v_std_any_set_pointer(ret, (list : *v_ast_base_t));   v_break();      //- unit_defn
    case 1:   v_std_any_set_pointer(ret, (list : *v_ast_stmt_t));   v_break();      //- stmt
    case 2:   v_std_any_set_pointer(ret, (list : *v_ast_expr_t));   v_break();      //- expr
    }
}

//---------------------------------------------------------------------
private
mk_cc_do_grammar_action: (ret: *v_std_any_t, *void, any: *v_std_any_t, size_t) ~> void
{
    elst = v_std_any_get_pointer(v_ast_expr_list_t, any+0);

    list = v_alloca(v_ast_generic_list_t);
    v_initialize(list);
    defer v_terminate(list);

    v_make_list(list, cc_do_q, (elst: *v_ast_base_t));

    kind = v_std_any_get_value(intptr_t, any+1);

    if (kind) v_std_any_set_pointer(ret, (list : *v_ast_stmt_t));       //- stmt
    else      v_std_any_set_pointer(ret, (list : *v_ast_base_t));       //- unit_defn
}

//---------------------------------------------------------------------
private
mk_cc_assert_grammar_action: (ret: *v_std_any_t, *void, any: *v_std_any_t, size_t) ~> void
{
    expr = v_std_any_get_pointer(v_ast_expr_t, any+0);

    list = v_alloca(v_ast_generic_list_t);
    v_initialize(list);
    defer v_terminate(list);

    v_make_list(list, cc_assert_q, (expr: *v_ast_base_t));

    etmp = v_alloca(v_ast_expr_t);
    v_initialize(etmp);
    defer v_terminate(etmp);

    filename = v_target_local_ctx_get_filename(v_target_get_local_ctx());

    ps = v_std_any_get_value(size_t, v_ast_get_property((expr: *v_ast_base_t), pos_start_q));
    pe = v_std_any_get_value(size_t, v_ast_get_property((expr: *v_ast_base_t), pos_end_q));

    line:   &size_t := v_undef();
    column: &size_t := v_undef();

    v_peg_get_line_column(ps, line, column);

    sstr = v_alloca(v_std_string_t);
    v_initialize(sstr);
    defer v_terminate(sstr);

    v_peg_take_string(sstr, ps, pe);

    v_ast_make_expr_string(etmp, v_std_string_get(sstr));
    v_list_append(list, list, (etmp: *v_ast_base_t));

    v_ast_make_expr_string(etmp, filename);
    v_list_append(list, list, (etmp: *v_ast_base_t));

    v_ast_make_expr_integer(etmp, (line: intptr_t));
    v_list_append(list, list, (etmp: *v_ast_base_t));

    kind = v_std_any_get_value(intptr_t, any+1);

    if (kind) v_std_any_set_pointer(ret, (list : *v_ast_stmt_t));       //- stmt
    else      v_std_any_set_pointer(ret, (list : *v_ast_base_t));       //- unit_defn
}

//---------------------------------------------------------------------
{   typ = v_alloca(v_type_ptr, 2);

    typ[0] := v_pointer_type(v_peg_grammar_action_fun_t, 0);
    typ[1] := v_pointer_type(void, 0);

    v_add_type("my_expr_make_postfix_ctx_t", v_struct_type(typ, 2, false));
}

private
my_expr_make_postfix_grammar_action: (ret: *v_std_any_t, aux: *void, any: *v_std_any_t, sz: size_t) ~> void
{
    ctx = *(aux: *my_expr_make_postfix_ctx_t);

    ctx[0](ret, ctx[1], any, sz);

    if (v_empty(ret))   v_return();

    ast = v_std_any_get_pointer(v_ast_expr_t, ret);

    if (v_ast_base_get_tag((ast: *v_ast_base_t)) != v_ast_expr_call_tag)  v_return();

    call = (ast: *v_ast_expr_t);

    efun = v_ast_expr_call_get_fun_expr(call);
    elst = v_ast_expr_call_get_arg_list(call);

    if (v_ast_base_get_tag((efun: *v_ast_base_t)) != v_ast_expr_identifier_tag) v_return();

    if (v_ast_expr_identifier_get_name_q(efun) != q_assert) v_return();

    args = v_alloca(v_ast_expr_list_t);
    v_initialize(args);
    defer v_terminate(args);

    arg0 = v_list_get_item(elst, 0);

    v_make_list(args, arg0);        //- Sic!

    expr = v_alloca(v_ast_expr_t);
    v_initialize(expr);
    defer v_terminate(expr);

    filename = v_target_local_ctx_get_filename(v_target_get_local_ctx());

    ps = v_std_any_get_value(size_t, v_ast_get_property((arg0: *v_ast_base_t), pos_start_q));
    pe = v_std_any_get_value(size_t, v_ast_get_property((arg0: *v_ast_base_t), pos_end_q));

    line:   &size_t := v_undef();
    column: &size_t := v_undef();

    v_peg_get_line_column(ps, line, column);

    sstr = v_alloca(v_std_string_t);
    v_initialize(sstr);
    defer v_terminate(sstr);

    v_peg_take_string(sstr, ps, pe);

    v_ast_make_expr_string(expr, v_std_string_get(sstr));
    v_list_append(args, args, expr);

    v_ast_make_expr_string(expr, filename);
    v_list_append(args, args, expr);

    v_ast_make_expr_integer(expr, (line: intptr_t));
    v_list_append(args, args, expr);

    v_ast_make_expr_call(expr, efun, args);

    v_std_any_set_pointer(ret, expr);
}


//---------------------------------------------------------------------
//- Compiler methods...
//---------------------------------------------------------------------
private
compile_cc_message: (*void, vis: *voidc_visitor_t, self: *v_ast_base_t) ~> void
{
    list = (self: *v_ast_generic_list_t);

    efname = (v_list_get_item(list, 0) : *v_ast_expr_t);
    eline  = (v_list_get_item(list, 1) : *v_ast_expr_t);
    etag   = (v_list_get_item(list, 2) : *v_ast_expr_t);
    elst   = (v_list_get_item(list, 3) : *v_ast_expr_list_t);

    //-----------------------------------------------------------------
    filename = v_ast_expr_string_get_string(efname);

    line = v_ast_expr_integer_get_number(eline);

    //-----------------------------------------------------------------
    tag = v_ast_expr_string_get_string(etag);

    tag = (tag[0] : char32_t);

    stag: &*const char := "?!?";

    switch(tag)
    {
    case 'e':   stag := "Error";    v_break();
    case 'w':   stag := "Warning";  v_break();
    case 'n':   stag := "Note";     v_break();
    case 'f':   stag := "Fail";     v_break();
    }

    msg = v_ast_expr_string_get_string(v_list_get_item(elst, 0));      //- Sic!

    //-----------------------------------------------------------------
    function: &*const char := 0;

    {   builder = v_target_get_builder();

        if (cur_b = LLVMGetInsertBlock(builder))
        {
            fun = LLVMGetBasicBlockParent(cur_b);

            len: &size_t := v_undef();

            function := LLVMGetValueName2(fun, &len);
        }
    }

    //-----------------------------------------------------------------
    printf: (*const char, ...) ~> int;                  //- ?!?!?!?!?!?

    if (function) printf("%s:%zd: %s: %s: %s\n", filename, line, function, stag, msg);
    else          printf("%s:%zd: %s: %s\n", filename, line, stag, msg);

    //-----------------------------------------------------------------
    if ('n' != tag != 'w')      //- i.e.  'e' or 'f' ...
    {
        abort: () ~> void;

        abort();
    }
}

//---------------------------------------------------------------------
private
compile_cc_do: (*void, vis: *voidc_visitor_t, self: *v_ast_base_t) ~> void
{
    list = (self: *v_ast_generic_list_t);

    elst = (v_list_get_item(list, 0) : *v_ast_expr_list_t);

    n = v_list_get_size(elst);

    for (i: &int := 0; i < n; ++i)
    {
        expr = v_list_get_item(elst, i);

        v_set_result_type(INVIOLABLE_TAG);
        v_set_result_value(0);

        v_ast_accept_visitor((expr: *v_ast_base_t), vis);
    }
}

//---------------------------------------------------------------------
private
compile_cc_assert: (*void, vis: *voidc_visitor_t, self: *v_ast_base_t) ~> void
{
    if (v_find_constant("NDEBUG", 0, 0))   v_return();

    list = (self: *v_ast_generic_list_t);

    expr   = (v_list_get_item(list, 0) : *v_ast_expr_t);
    emsg   = (v_list_get_item(list, 1) : *v_ast_expr_t);
    efname = (v_list_get_item(list, 2) : *v_ast_expr_t);
    eline  = (v_list_get_item(list, 3) : *v_ast_expr_t);

    v_set_result_type(UNREFERENCE_TAG);
    v_set_result_value(0);

    v_ast_accept_visitor((expr: *v_ast_base_t), vis);

    v = v_get_result_value();

    builder = v_target_get_builder();

    v = LLVMBuildIsNotNull(builder, v, "");

    if (LLVMConstIntGetZExtValue(v))  v_return();       //- Ok!

    //- Game over!

    msg      = v_ast_expr_string_get_string(emsg);
    filename = v_ast_expr_string_get_string(efname);
    line     = v_ast_expr_integer_get_number(eline);

    //-----------------------------------------------------------------
    function: &*const char := 0;

    if (cur_b = LLVMGetInsertBlock(builder))
    {
        fun = LLVMGetBasicBlockParent(cur_b);

        len: &size_t := v_undef();

        function := LLVMGetValueName2(fun, &len);
    }

    //-----------------------------------------------------------------
    printf: (*const char, ...) ~> int;                  //- ?!?!?!?!?!?

    if (function) printf("%s:%zd: %s: Static assertion `%s\' failed!\n", filename, line, function, msg);
    else          printf("%s:%zd: Static assertion `%s\' failed!\n", filename, line, msg);

    //-----------------------------------------------------------------
    abort: () ~> void;

    abort();
}


//---------------------------------------------------------------------
//- v_assert intrinsic
//---------------------------------------------------------------------
private
v_assert_intrinsic: (*void, vis: *voidc_visitor_t, self: *v_ast_base_t) ~> void
{
    if (v_find_constant("NDEBUG", 0, 0))   v_return();

    call = (self: *v_ast_expr_t);

    args = v_ast_expr_call_get_arg_list(call);

    arg0 = v_list_get_item(args, 0);

    emessage = v_list_get_item(args, 1);
    filename = v_list_get_item(args, 2);
    eline    = v_list_get_item(args, 3);

    //-----------------------------------------------------------------
#if (!v_defined(_WIN32))

    function: &*const char := 0;

    {   builder = v_target_get_builder();

        if (cur_b = LLVMGetInsertBlock(builder))
        {
            fun = LLVMGetBasicBlockParent(cur_b);

            len: &size_t := v_undef();

            function := LLVMGetValueName2(fun, &len);
        }
    }

#endif

    list = v_alloca(v_ast_expr_list_t);
    v_initialize(list);
    defer v_terminate(list);

    v_make_list_nil(list);

    v_list_append(list, list, emessage);
    v_list_append(list, list, filename);
    v_list_append(list, list, eline);

    expr = v_alloca(v_ast_expr_t);
    v_initialize(expr);
    defer v_terminate(expr);

#if (v_defined(_WIN32))

    v_ast_make_expr_identifier(expr, "_assert");

#else

    v_ast_make_expr_string(expr, function);

    v_list_append(list, list, expr);

    v_ast_make_expr_identifier(expr, "__assert_fail");

#endif

    v_ast_make_expr_call(expr, expr, list);

    stmt = v_alloca(v_ast_stmt_t, 2);
    v_initialize(stmt, 2);
    defer v_terminate(stmt, 2);

    v_ast_make_stmt(stmt+1, "", expr);

    v_ast_make_stmt_if_then_else(stmt+0, arg0, stmt+0, stmt+1);

    v_ast_accept_visitor((stmt+0: *v_ast_base_t), vis);
}


//---------------------------------------------------------------------
//- Enable conditional compilation
//---------------------------------------------------------------------
export
voidc_enable_condcomp_compiler: () ~> void
{
    voidc_enable_condcomp_n1_compiler();

    vis = v_get_compiler();

    v_return_if(voidc_visitor_get_method(vis, cc_message_q, 0));

    voidc_visitor_set_method(vis, vis, cc_message_q, compile_cc_message, 0);
    voidc_visitor_set_method(vis, vis, cc_do_q,      compile_cc_do,      0);
    voidc_visitor_set_method(vis, vis, cc_assert_q,  compile_cc_assert,  0);

    _char_ptr = v_pointer_type(v_find_type_q(char_q), 0);
    _unsigned = v_find_type_q(unsigned_q);

#if (v_defined(_WIN32))

    typ: &(*v_type_t)[3] := v_undef();

    typ[0] := _char_ptr;            //- message
    typ[1] := _char_ptr;            //- filename
    typ[2] := _unsigned;            //- line

    ft = v_function_type(v_void_type(), typ, 3, false);

    fn = "_assert";

#else

    typ: &(*v_type_t)[4] := v_undef();

    typ[0] := _char_ptr;            //- assertion
    typ[1] := _char_ptr;            //- filename
    typ[2] := _unsigned;            //- line
    typ[3] := _char_ptr;            //- function

    ft = v_function_type(v_void_type(), typ, 4, false);

    fn = "__assert_fail";

#endif

    v_add_symbol(fn, ft, 0);

    v_add_intrinsic("v_assert", v_assert_intrinsic, 0);
}

//---------------------------------------------------------------------
//- Enable conditional compilation
//---------------------------------------------------------------------
export
voidc_enable_condcomp: () ~> void
{
    voidc_enable_condcomp_compiler();

    v_return_if_not(voidc_has_grammar());

    voidc_enable_condcomp_n1();

    gr0 = v_alloca(v_peg_grammar_t);
    v_initialize(gr0);
    defer v_terminate(gr0);

    v_peg_get_grammar(gr0);

    v_return_if(v_peg_grammar_get_action(gr0, "mk_cc_message", 0));

    ctx = *v_malloc(my_expr_make_postfix_ctx_t);

    free: (*void) ~> void;
    v_add_local_cleaner(free, &ctx);

    ctx[0] := v_peg_grammar_get_action(gr0, "expr_make_postfix", &ctx[1]);

    pctx = &ctx;

    grammar gr0
    {
    actions:
        mk_cc_message = mk_cc_message_grammar_action;
        mk_cc_do      = mk_cc_do_grammar_action;
        mk_cc_assert  = mk_cc_assert_grammar_action;

        expr_make_postfix = my_expr_make_postfix_grammar_action, pctx;

    parsers:
        unit_defn =+ '#' t:("error"/"warning"/"note") _'('_ l:expr_list _')'    { mk_cc_message($0s, t, l, 0) };
        stmt      =+ '#' t:("error"/"warning"/"note") _'('_ l:expr_list _')'    { mk_cc_message($0s, t, l, 1) };
        expr      =+ '#' t:"fail"                     _'('_ l:expr_list _')'    { mk_cc_message($0s, t, l, 2) };

        unit_defn =+ "#do" _'('_ l:expr_list _ (','_)? ')'    { mk_cc_do(l, 0) };
        stmt      =+ "#do" _'('_ l:expr_list _ (','_)? ')'    { mk_cc_do(l, 1) };

        unit_defn =+ "#assert" _'('_ e:expr _')'    { mk_cc_assert(e, 0) };
        stmt      =+ "#assert" _'('_ e:expr _')'    { mk_cc_assert(e, 1) };
    }

    v_peg_set_grammar(gr0);
}


//=====================================================================
{   voidc_unit_end_module(); }


