//---------------------------------------------------------------------
//- Copyright (C) 2020-2024 Dmitry Borodkin <borodkin.dn@gmail.com>
//- SDPX-License-Identifier: LGPL-3.0-or-later
//---------------------------------------------------------------------
{   voidc_guard_target("overloading.void  must be imported into the voidc target only!");

    v_import("level-00");
    v_import("level-01");

    v_import("llvm-c/Core.void");

    v_import("loops_etc.void");
    v_import("condcomp.void");
    v_import("aggregates.void");
    v_import("handlers_etc.void");

//  v_import("printf.void");
}

{   v_enable_level_01();

    voidc_enable_loops_etc();
    voidc_enable_condcomp();
    voidc_enable_aggregates();
    voidc_enable_handlers_etc();
}


//---------------------------------------------------------------------
{   q_ref = v_reference_type(v_quark_t, 0);

    q = v_quark_ptr_from_string;

    v_add_symbol("expr_call_q",       q_ref, q("expr_call"));
    v_add_symbol("expr_identifier_q", q_ref, q("expr_identifier"));

    v_add_symbol("q_op_postfix_call",  q_ref, q("v.op_postfix_call"));
    v_add_symbol("q_op_postfix_index", q_ref, q("v.op_postfix_index"));

    v_add_symbol("q_op_infix_lt", q_ref, q("v.op_infix_lt"));
    v_add_symbol("q_op_infix_le", q_ref, q("v.op_infix_le"));
    v_add_symbol("q_op_infix_gt", q_ref, q("v.op_infix_gt"));
    v_add_symbol("q_op_infix_ge", q_ref, q("v.op_infix_ge"));
    v_add_symbol("q_op_infix_eq", q_ref, q("v.op_infix_eq"));
    v_add_symbol("q_op_infix_ne", q_ref, q("v.op_infix_ne"));

    v_add_symbol("voidc_incomplete_array_type_tag", q_ref, q("voidc.incomplete_array_type_tag"));

    v_add_symbol("bool_q", q_ref, q("bool"));
}


//=====================================================================
{   voidc_unit_begin_module("op_table_module"); }


//---------------------------------------------------------------------
opk_assign   = 0;
opk_logical  = 1;
opk_relation = 2;
opk_binary   = 3;
opk_unary    = 4;

op_table_item_t = v_struct("struct.v_op_table_item",
{
    *const char,    //- 0 - Name
    int,            //- 1 - Kind

    v_quark_t,      //- 2 - Quark (left)
    v_quark_t,      //- 3 - Quark (right)
    v_quark_t       //- 4 - Helper quark ...
});

op_table: &op_table_item_t[] :=
{
    {"v.op_infix_assign",      opk_assign   },
    {"v.op_infix_add_assign",  opk_assign   },
    {"v.op_infix_sub_assign",  opk_assign   },
    {"v.op_infix_mul_assign",  opk_assign   },
    {"v.op_infix_div_assign",  opk_assign   },
    {"v.op_infix_rem_assign",  opk_assign   },
    {"v.op_infix_shl_assign",  opk_assign   },
    {"v.op_infix_shr_assign",  opk_assign   },
    {"v.op_infix_and_assign",  opk_assign   },
    {"v.op_infix_xor_assign",  opk_assign   },
    {"v.op_infix_or_assign",   opk_assign   },

//  {"v.op_infix_logical_or",  opk_logical  },
//  {"v.op_infix_logical_and", opk_logical  },

    {"v.op_infix_lt",          opk_relation },
    {"v.op_infix_le",          opk_relation },
    {"v.op_infix_gt",          opk_relation },
    {"v.op_infix_ge",          opk_relation },
    {"v.op_infix_eq",          opk_relation },
    {"v.op_infix_ne",          opk_relation },

    {"v.op_infix_or",          opk_binary   },
    {"v.op_infix_xor",         opk_binary   },
    {"v.op_infix_and",         opk_binary   },

    {"v.op_infix_shl",         opk_binary   },
    {"v.op_infix_shr",         opk_binary   },
    {"v.op_infix_add",         opk_binary   },
    {"v.op_infix_sub",         opk_binary   },
    {"v.op_infix_mul",         opk_binary   },
    {"v.op_infix_div",         opk_binary   },
    {"v.op_infix_rem",         opk_binary   },

    {"v.op_prefix_inc",        opk_unary    },
    {"v.op_prefix_dec",        opk_unary    },
    {"v.op_prefix_plus",       opk_unary    },
    {"v.op_prefix_minus",      opk_unary    },
    {"v.op_prefix_star",       opk_unary    },
    {"v.op_prefix_amp",        opk_unary    },
    {"v.op_prefix_bang",       opk_unary    },
    {"v.op_prefix_tilde",      opk_unary    },

    {"v.op_prefix_const",      opk_unary    },
    {"v.op_prefix_volatile",   opk_unary    },

    {"v.op_postfix_inc",       opk_unary    },
    {"v.op_postfix_dec",       opk_unary    },

//  {"v.op_postfix_index",     opk_unary    },
};

//---------------------------------------------------------------------
{   t = v_find_symbol_type("op_table");

    n = v_type_array_get_length(v_type_refptr_get_element_type(t));

    int_ = v_type_get_llvm_type(int);

    v_add_constant("op_table_length", int, LLVMConstInt(int_, n, 0));

    //-----------------------------------------------------------------
    LLVMDisposeMemoryBuffer(voidc_get_unit_buffer());
    voidc_set_unit_buffer(0);
}

//---------------------------------------------------------------------
op_table_ctor: () ~> void
{
    sstr = v_alloca(v_std_string_t);
    v_initialize(sstr);
    defer v_terminate(sstr);

    int_ = v_type_get_llvm_type(int);

    for (i: &int := 0; i < op_table_length; ++i)
    {
        op_item = op_table[i];

        name = op_item[0];

        op_item[2] := v_quark_from_string(name);

        if (k = op_item[1],  k == opk_binary  ||  k == opk_relation)
        {
            v_std_string_set(sstr, name);

            v_std_string_append(sstr, ".right");

            op_item[3] := v_quark_from_string(v_std_string_get(sstr));
        }

        v_std_string_set(sstr, "id");

        v_std_string_append(sstr, name+4);          //- Skip "v.op"

        v_add_constant(v_std_string_get(sstr), int, LLVMConstInt(int_, i, 0));

        v_std_string_set(sstr, "voidc.expression_helper");

        v_std_string_append(sstr, name+4);          //- Skip "v.op"

        op_item[4] := v_quark_from_string(v_std_string_get(sstr));
    }
}


//=====================================================================
{   voidc_unit_end_module(); }


//---------------------------------------------------------------------
{   op_table_ctor();
}


//=====================================================================
{   voidc_unit_begin_module("overloading_module"); }


//---------------------------------------------------------------------
//- llvm_build_call_helper ...
//---------------------------------------------------------------------
private
llvm_build_call_helper: (*void, vis: *voidc_visitor_t, self: *v_ast_base_t,
                         ft: *v_type_t, fv: LLVMValueRef,
                         arg_ptrs: **v_ast_expr_t, arg_count: unsigned
                        ) ~> void
{
    par_count = v_type_function_get_param_count(ft);
    par_types = v_type_function_get_param_types(ft);

    values = v_malloc(LLVMValueRef, arg_count);
    defer v_free(values);

    ttag = v_get_result_type();
    vtag = v_get_result_value();

    for (i: &unsigned := 0; i < arg_count; ++i)
    {
        if (i < par_count)  v_set_result_type(par_types[i]);
        else                v_set_result_type(UNREFERENCE_TAG);

        v_set_result_value(0);

        v_ast_accept_visitor((arg_ptrs[i]: *v_ast_base_t), vis);

        values[i] := v_get_result_value();
    }

    builder = v_target_get_builder();

    ft_ = v_type_get_llvm_type(ft);

    vr = LLVMBuildCall2(builder, ft_, fv, values, arg_count, "");

    tr = v_type_function_get_return_type(ft);

    v_set_result_type(ttag);
    v_set_result_value(vtag);

    v_adopt_result(tr, vr);
}

//---------------------------------------------------------------------
{   v_make_handler(v_llvm_build_call, llvm_build_call_helper); }


//---------------------------------------------------------------------
intrinsic_p1_t = (aux: *void, vis: *voidc_visitor_t, self: *v_ast_base_t,
                  t0: *v_type_t, v0: LLVMValueRef
                 ) ~> void;

intrinsic_p2_t = (aux: *void, vis: *voidc_visitor_t, self: *v_ast_base_t,
                  t0: *v_type_t, v0: LLVMValueRef,
                  t1: *v_type_t, v1: LLVMValueRef
                 ) ~> void;


//---------------------------------------------------------------------
private
op_assign_helper: (*void, vis: *voidc_visitor_t, self: *v_ast_base_t,
                   t0: *v_type_t, v0: LLVMValueRef, qh: v_quark_t, q: v_quark_t,
                   rhs: *v_ast_expr_t) ~> void
{
    ttag = v_get_result_type();
    vtag = v_get_result_value();

    void_fun: &*void := 0;
    void_aux: &*void := 0;

    ft: &*v_type_t    := 0;
    fv: &LLVMValueRef := 0;

    if (v_util_lookup_overload(q, t0, &void_fun, &void_aux, &ft, &fv))
    {
        //- Got overloading!

        if (fun = (void_fun: *intrinsic_p1_t))
        {
            v_set_result_type(ttag);
            v_set_result_value(vtag);

            fun(void_aux, vis, self, t0, v0);
        }
        else
        {
            expr = v_alloca(v_ast_expr_t);
            v_initialize(expr);
            defer v_terminate(expr);

            v_ast_make_expr_compiled(expr, t0, v0);

            arg_ptrs: &(*v_ast_expr_t)[2] :=
            {
                expr,           //- "Place", compiled
                rhs,            //- "Value"
            };

            v_set_result_type(ttag);
            v_set_result_value(vtag);

            v_llvm_build_call(vis, self, ft, fv, arg_ptrs, 2);
        }

        v_return();
    }

    //- Default treatment...

    expr = v_alloca(v_ast_expr_t);
    v_initialize(expr);
    defer v_terminate(expr);

    v_ast_make_expr_compiled(expr, t0, v0);

    ast0 = expr;                //- "Place", compiled
    ast1 = rhs;                 //- "Value"

    v_set_result_type(ttag);
    v_set_result_value(vtag);

    compile: &*void := v_undef();

    helper = v_get_intrinsic_q(qh, &compile);
    helper = (helper: *((*voidc_visitor_t, *v_ast_expr_t, *v_ast_expr_t, *void) ~> void));

    helper(vis, ast0, ast1, compile);
}

//---------------------------------------------------------------------
{   v_make_handler(v_compile_op_assign, op_assign_helper); }


//---------------------------------------------------------------------
private
op_assign_intrinsic: (aux: *void, vis: *voidc_visitor_t, self: *v_ast_base_t) ~> void
{
    args = v_ast_expr_call_get_arg_list((self: *v_ast_expr_t));

    ttag = v_get_result_type();
    vtag = v_get_result_value();

    v_set_result_type(INVIOLABLE_TAG);
    v_set_result_value(0);

    lhs = v_list_get_item(args, 1);             //- "Place"

    v_ast_accept_visitor((lhs : *v_ast_base_t), vis);

    t0 = v_get_result_type();
    v0 = v_get_result_value();

    id = (aux: int);

    op_data = op_table[id];

    q  = op_data[2];    //- Quark
    qh = op_data[4];    //- Helper quark

    rhs = v_list_get_item(args, 2);             //- "Value"

    v_set_result_type(ttag);
    v_set_result_value(vtag);

    v_compile_op_assign(vis, self, t0, v0, qh, q, rhs);
}


//---------------------------------------------------------------------
private
op_binary_helper: (*void, vis: *voidc_visitor_t, self: *v_ast_base_t,
                   t0: *v_type_t, v0: LLVMValueRef, qh: v_quark_t, q0: v_quark_t, q1: v_quark_t,
                   rhs: *v_ast_expr_t) ~> void
{
    ttag = v_get_result_type();
    vtag = v_get_result_value();

    void_fun: &*void := 0;
    void_aux: &*void := 0;

    ft: &*v_type_t    := 0;
    fv: &LLVMValueRef := 0;

    if (v_util_lookup_overload(q0, t0, &void_fun, &void_aux, &ft, &fv))
    {
        //- Got overloading!

        if (fun = (void_fun: *intrinsic_p1_t))
        {
            v_set_result_type(ttag);
            v_set_result_value(vtag);

            fun(void_aux, vis, self, t0, v0);
        }
        else
        {
            expr = v_alloca(v_ast_expr_t);
            v_initialize(expr);
            defer v_terminate(expr);

            v_ast_make_expr_compiled(expr, t0, v0);

            arg_ptrs: &(*v_ast_expr_t)[2] :=
            {
                expr,           //- "LHS", compiled
                rhs,            //- "RHS"
            };

            v_set_result_type(ttag);
            v_set_result_value(vtag);

            v_llvm_build_call(vis, self, ft, fv, arg_ptrs, 2);
        }

        v_return();
    }

    //- Second argument

    v_set_result_type(INVIOLABLE_TAG);
    v_set_result_value(0);

    v_ast_accept_visitor((rhs : *v_ast_base_t), vis);

    t1 = v_get_result_type();
    v1 = v_get_result_value();

    if (v_util_lookup_overload(q1, t1, &void_fun, &void_aux, &ft, &fv))
    {
        //- Got overloading!

        if (fun = (void_fun: *intrinsic_p2_t))
        {
            v_set_result_type(ttag);
            v_set_result_value(vtag);

            fun(void_aux, vis, self, t0, v0, t1, v1);
        }
        else
        {
            expr = v_alloca(v_ast_expr_t, 2);
            v_initialize(expr, 2);
            defer v_terminate(expr, 2);

            v_ast_make_expr_compiled(expr+0, t0, v0);
            v_ast_make_expr_compiled(expr+1, t1, v1);

            arg_ptrs: &(*v_ast_expr_t)[2] :=
            {
                expr+0,         //- "LHS", compiled
                expr+1,         //- "RHS", compiled
            };

            v_set_result_type(ttag);
            v_set_result_value(vtag);

            v_llvm_build_call(vis, self, ft, fv, arg_ptrs, 2);
        }

        v_return();
    }

    //- Default treatment...

    expr = v_alloca(v_ast_expr_t, 2);
    v_initialize(expr, 2);
    defer v_terminate(expr, 2);

    v_ast_make_expr_compiled(expr+0, t0, v0);
    v_ast_make_expr_compiled(expr+1, t1, v1);

    v_set_result_type(ttag);
    v_set_result_value(vtag);

    compile: &*void := v_undef();

    helper = v_get_intrinsic_q(qh, &compile);
    helper = (helper: *((*voidc_visitor_t, *v_ast_expr_t, *v_ast_expr_t, *void) ~> void));

    helper(vis, expr+0, expr+1, compile);
}

//---------------------------------------------------------------------
{   v_make_handler(v_compile_op_binary, op_binary_helper); }


//---------------------------------------------------------------------
private
op_binary_intrinsic: (aux: *void, vis: *voidc_visitor_t, self: *v_ast_base_t) ~> void
{
    args = v_ast_expr_call_get_arg_list((self: *v_ast_expr_t));

    ttag = v_get_result_type();
    vtag = v_get_result_value();

    //- First argument

    v_set_result_type(INVIOLABLE_TAG);
    v_set_result_value(0);

    lhs = v_list_get_item(args, 1);

    v_ast_accept_visitor((lhs : *v_ast_base_t), vis);

    t0 = v_get_result_type();
    v0 = v_get_result_value();

    id = (aux: int);

    op_data = op_table[id];

    q0 = op_data[2];        //- Quark (left)
    q1 = op_data[3];        //- Quark (right)
    qh = op_data[4];        //- Helper quark ...

    v_set_result_type(ttag);
    v_set_result_value(vtag);

    rhs = v_list_get_item(args, 2);

    v_compile_op_binary(vis, self, t0, v0, qh, q0, q1, rhs);
}


//---------------------------------------------------------------------
private
op_relation_helper: (*void, vis: *voidc_visitor_t,
                     ast: **v_ast_expr_t,
                     qh: v_quark_t, q0: v_quark_t, q1: v_quark_t,
                     t0:  *v_type_t, v0:  LLVMValueRef,
                     t1: &*v_type_t, v1: &LLVMValueRef
                    ) ~> LLVMValueRef
{
    ttag = v_get_result_type();
    vtag = v_get_result_value();

    r: &LLVMValueRef := 0;

    void_fun: &*void := 0;
    void_aux: &*void := 0;

    ft: &*v_type_t    := 0;
    fv: &LLVMValueRef := 0;

    if (v_util_lookup_overload(q0, t0, &void_fun, &void_aux, &ft, &fv))
    {
        //- Got overloading!

        intrinsic_p12_t = (aux: *void, vis: *voidc_visitor_t,
                           ast: **v_ast_expr_t,
                           t0:  *v_type_t, v0:  LLVMValueRef,
                           t1: &*v_type_t, v1: &LLVMValueRef
                          ) ~> void;

        if (fun = (void_fun: *intrinsic_p12_t))
        {
            v_set_result_type(ttag);        //- Sic !?!
            v_set_result_value(vtag);       //- Sic !?!

            fun(void_aux, vis, ast, t0, v0, t1, v1);                        //- Without "self" !?!?!?!?!
        }
        else
        {
            expr = v_alloca(v_ast_expr_t, 2);
            v_initialize(expr, 2);
            defer v_terminate(expr, 2);

            v_ast_make_expr_compiled(expr+0, t0, v0);

            v_ast_make_expr_compile_spy(expr+1, ast[1], t1, v1);

            arg_ptrs: &(*v_ast_expr_t)[2] :=
            {
                expr+0,             //- "LHS", compiled
                expr+1,             //- "RHS", compile spy
            };

            v_set_result_type(ttag);
            v_set_result_value(vtag);

            v_llvm_build_call(vis, 0, ft, fv, arg_ptrs, 2);                 //- Without "self" !?!?!?!?!
        }

        r := v_get_result_value();
    }
    else
    {
        //- Second argument

        v_set_result_type(INVIOLABLE_TAG);
        v_set_result_value(0);

        v_ast_accept_visitor((ast[1] : *v_ast_base_t), vis);

        t1 := v_get_result_type();
        v1 := v_get_result_value();

        if (v_util_lookup_overload(q1, t1, &void_fun, &void_aux, &ft, &fv))
        {
            //- Got overloading!

            intrinsic_p22_t = (aux: *void, vis: *voidc_visitor_t,
                               ast: **v_ast_expr_t,
                               t0: *v_type_t, v0: LLVMValueRef,
                               t1: *v_type_t, v1: LLVMValueRef
                              ) ~> void;

            if (fun = (void_fun: *intrinsic_p22_t))
            {
                v_set_result_type(ttag);        //- Sic !?!
                v_set_result_value(vtag);       //- Sic !?!

                fun(void_aux, vis, ast, t0, v0, t1, v1);                        //- Without "self" !?!?!?!?!
            }
            else
            {
                expr = v_alloca(v_ast_expr_t, 2);
                v_initialize(expr, 2);
                defer v_terminate(expr, 2);

                v_ast_make_expr_compiled(expr+0, t0, v0);
                v_ast_make_expr_compiled(expr+1, t1, v1);

                arg_ptrs: &(*v_ast_expr_t)[2] :=
                {
                    expr+0,             //- "LHS", compiled
                    expr+1,             //- "RHS", compiled
                };

                v_set_result_type(ttag);
                v_set_result_value(vtag);

                v_llvm_build_call(vis, 0, ft, fv, arg_ptrs, 2);                 //- Without "self" !?!?!?!?!
            }

            r := v_get_result_value();
        }
    }

    if (!r)
    {
        //- Default treatment...

        expr = v_alloca(v_ast_expr_t);
        v_initialize(expr);
        defer v_terminate(expr);

        v_ast_make_expr_compiled(expr, t1, v1);

        compile: &*void := v_undef();

        helper = v_get_intrinsic_q(qh, &compile);
        helper = (helper: *((*voidc_visitor_t, *void, *v_type_t, LLVMValueRef,
                             *v_ast_expr_t, &*v_type_t, &LLVMValueRef) ~> LLVMValueRef));

        typ: &*v_type_t    := t1;
        val: &LLVMValueRef := v1;

        r := helper(vis, compile, t0, v0, expr, typ, val);
    }

    v_return(r);
}

//---------------------------------------------------------------------
{   v_make_handler(v_compile_op_relation, op_relation_helper); }

//---------------------------------------------------------------------
private
check_relation: (arg: *v_ast_expr_t, lhs: **v_ast_expr_t, rhs: **v_ast_expr_t) ~> int
{
    get_tag = v_ast_base_get_visitor_method_tag;

    if (get_tag((arg: *v_ast_base_t)) != expr_call_q)  v_return(0);

    etmp = v_ast_expr_call_get_fun_expr(arg);

    if (get_tag((etmp: *v_ast_base_t)) != expr_identifier_q)  v_return(0);

    qname = v_ast_expr_identifier_get_name_q(etmp);

    rk: &int := v_undef();

    block
    {
        if (qname == q_op_infix_eq)  { rk := id_infix_eq; v_break(); }
        if (qname == q_op_infix_ne)  { rk := id_infix_ne; v_break(); }
        if (qname == q_op_infix_lt)  { rk := id_infix_lt; v_break(); }
        if (qname == q_op_infix_le)  { rk := id_infix_le; v_break(); }
        if (qname == q_op_infix_gt)  { rk := id_infix_gt; v_break(); }
        if (qname == q_op_infix_ge)  { rk := id_infix_ge; v_break(); }

        v_return(0);
    }

    if (lhs) ;
    else if (rhs) ;
    else
    {
        v_return(rk);
    }

    lst = v_ast_expr_call_get_arg_list(arg);

    if (lhs)  *lhs := v_list_get_item(lst, 1);
    if (rhs)  *rhs := v_list_get_item(lst, 2);

    v_return(rk);
}

//---------------------------------------------------------------------
private
op_relation_intrinsic: (aux: *void, vis: *voidc_visitor_t, self: *v_ast_base_t) ~> void
{
//printf("op_relation_intrinsic start\n");
//defer printf("op_relation_intrinsic stop\n");

    args = v_ast_expr_call_get_arg_list((self: *v_ast_expr_t));

    N: &int := 2;

    {   ast: &*v_ast_expr_t := v_list_get_item(args, 1);

        while(check_relation(ast, &ast, 0)) ++N;
    }

    ast = v_malloc(*v_ast_expr_t, N);
    defer v_free(ast);

    Nm1 = N - 1;

    rel = v_malloc(int, Nm1);
    defer v_free(rel);

    id = (aux: int);

    {   i = N - 2;

        ast_i: &**v_ast_expr_t := ast + i;
        rel_i: &*int           := rel + i;

        ast_i[0] := v_list_get_item(args, 1);
        ast_i[1] := v_list_get_item(args, 2);

        *rel_i := id;

        while(ast_i1 = ast_i-1, rk = check_relation(*ast_i, ast_i1, ast_i))
        {
            ast_i := ast_i1;
            rel_i := rel_i - 1;

            *rel_i := rk;
        }
    }


    ttag = v_get_result_type();
    vtag = v_get_result_value();

    builder = v_target_get_builder();

    cur_b = LLVMGetInsertBlock(builder);

    cur_f: &LLVMValueRef := v_undef();

    if (cur_b)  cur_f := LLVMGetBasicBlockParent(cur_b);
    else        cur_f := 0;

    cmp_result_v: &LLVMValueRef := 0;

    branch_if_false_b: &LLVMBasicBlockRef := v_undef();

    _bool = v_find_type_q(bool_q);

    bool_ = v_type_get_llvm_type(_bool);

    llvm_ctx = v_target_get_llvm_ctx();

    v_set_result_type(INVIOLABLE_TAG);
    v_set_result_value(0);

    v_ast_accept_visitor((ast[0] : *v_ast_base_t), vis);

    typ: &*v_type_t    := v_get_result_type();
    val: &LLVMValueRef := v_get_result_value();

    t0: &*v_type_t    := v_undef();
    v0: &LLVMValueRef := v_undef();
    t1: &*v_type_t    := v_undef();
    v1: &LLVMValueRef := v_undef();

    r: &LLVMValueRef := v_undef();

    for (i: &int := 0; i < Nm1; ++i)
    {
        rel_i = rel[i];

        //- First argument

        t0 := typ;
        v0 := val;

        op_data = op_table[rel_i];

        q0 = op_data[2];                    //- Quark (left)
        q1 = op_data[3];                    //- Quark (right)
        qh = op_data[4];                    //- Helper quark

        v_set_result_type(ttag);
        v_set_result_value(vtag);

        r := v_compile_op_relation(vis, ast+i, qh, q0, q1, t0, v0, t1, v1);

        typ := t1;
        val := v1;

        if (!cmp_result_v)
        {
            if (LLVMIsAConstantInt(r))
            {
                f = LLVMConstIntGetZExtValue(r);

                if (f)  v_continue();
                else    v_break();          //- Sic!!!
            }

            if (N != 2)
            {
                cmp_result_v := LLVMBuildAlloca(builder, bool_, "");

                LLVMBuildStore(builder, LLVMConstNull(bool_), cmp_result_v);

                branch_if_false_b := LLVMAppendBasicBlockInContext(llvm_ctx, cur_f, "branch_if_false_b");
            }
        }

        if (cmp_result_v)
        {
            if (i < N-2)
            {
                branch_if_true_b = LLVMAppendBasicBlockInContext(llvm_ctx, cur_f, "branch_if_true_b");

                LLVMBuildCondBr(builder, r, branch_if_true_b, branch_if_false_b);

                LLVMPositionBuilderAtEnd(builder, branch_if_true_b);
            }
            else        //- Last ...
            {
                LLVMBuildStore(builder, r, cmp_result_v);

                LLVMBuildBr(builder, branch_if_false_b);

                cur_b = LLVMGetInsertBlock(builder);

                LLVMMoveBasicBlockAfter(branch_if_false_b, cur_b);

                LLVMPositionBuilderAtEnd(builder, branch_if_false_b);

                r := LLVMBuildLoad2(builder, bool_, cmp_result_v, "");
            }
        }
    }


    if (v_type_is_vector(t0))                   //- ?!?!?!?!?!?
    {
        sz = v_type_vector_get_size(t0);

        if (v_type_vector_is_scalable(t0))
        {
            t0 := v_svector_type(_bool, sz);
        }
        else
        {
            t0 := v_vector_type(_bool, sz);
        }
    }
    else
    {
        t0 := _bool;
    }


    v_set_result_type(ttag);
    v_set_result_value(vtag);

    v_adopt_result(t0, r);
}


//---------------------------------------------------------------------
private
op_unary_helper: (*void, vis: *voidc_visitor_t, self: *v_ast_base_t,
                  t0: *v_type_t, v0: LLVMValueRef, qh: v_quark_t, q: v_quark_t
                 ) ~> void
{
    ttag = v_get_result_type();
    vtag = v_get_result_value();

    void_fun: &*void := 0;
    void_aux: &*void := 0;

    ft: &*v_type_t    := 0;
    fv: &LLVMValueRef := 0;

    if (v_util_lookup_overload(q, t0, &void_fun, &void_aux, &ft, &fv))
    {
        //- Got overloading!

        v_set_result_type(ttag);
        v_set_result_value(vtag);

        if (fun = (void_fun: *intrinsic_p1_t))
        {
            fun(void_aux, vis, self, t0, v0);
        }
        else
        {
            expr = v_alloca(v_ast_expr_t);
            v_initialize(expr);
            defer v_terminate(expr);

            v_ast_make_expr_compiled(expr, t0, v0);

            arg_ptr: &*v_ast_expr_t := expr;                //- Value, compiled

            v_llvm_build_call(vis, self, ft, fv, &arg_ptr, 1);
        }

        v_return();
    }

    //- Default treatment...

    expr = v_alloca(v_ast_expr_t);
    v_initialize(expr);
    defer v_terminate(expr);

    v_ast_make_expr_compiled(expr, t0, v0);

    compile: &*void := v_undef();

    helper = v_get_intrinsic_q(qh, &compile);
    helper = (helper: *((*voidc_visitor_t, *v_ast_expr_t, *void) ~> void));

    v_set_result_type(ttag);
    v_set_result_value(vtag);

    if (helper) helper(vis, expr, compile);
}

//---------------------------------------------------------------------
{   v_make_handler(v_compile_op_unary, op_unary_helper); }

//---------------------------------------------------------------------
private
op_unary_intrinsic: (aux: *void, vis: *voidc_visitor_t, self: *v_ast_base_t) ~> void
{
    args = v_ast_expr_call_get_arg_list((self: *v_ast_expr_t));

    ttag = v_get_result_type();
    vtag = v_get_result_value();

    v_set_result_type(INVIOLABLE_TAG);
    v_set_result_value(0);

    arg = v_list_get_item(args, 1);

    v_ast_accept_visitor((arg: *v_ast_base_t), vis);

    t0 = v_get_result_type();
    v0 = v_get_result_value();

    id = (aux: int);

    op_data = op_table[id];

    q  = op_data[2];            //- Quark
    qh = op_data[4];            //- Helper quark

    v_set_result_type(ttag);
    v_set_result_value(vtag);

    v_compile_op_unary(vis, self, t0, v0, qh, q);
}


//---------------------------------------------------------------------
private
static_type_prefix_star_intrinsic: (aux: *void, vis: *voidc_visitor_t, self: *v_ast_base_t,
                                    t0: *v_type_t, v0: LLVMValueRef
                                   ) ~> void
{
    v_assert(t0 == v_static_type_t);

    typ = (v0: *v_type_t);

    typ = v_pointer_type(typ, 0);           //- ...

    v_set_result_type(v_static_type_t);
    v_set_result_value((typ: LLVMValueRef));
}

//---------------------------------------------------------------------
private
static_type_prefix_amp_intrinsic: (aux: *void, vis: *voidc_visitor_t, self: *v_ast_base_t,
                                   t0: *v_type_t, v0: LLVMValueRef
                                  ) ~> void
{
    v_assert(t0 == v_static_type_t);

    typ = (v0: *v_type_t);

    typ = v_reference_type(typ, 0);         //- ...

    v_set_result_type(v_static_type_t);
    v_set_result_value((typ: LLVMValueRef));
}

//---------------------------------------------------------------------
private
static_type_prefix_cv_intrinsic: (aux: *void, vis: *voidc_visitor_t, self: *v_ast_base_t,
                                  t0: *v_type_t, v0: LLVMValueRef
                                 ) ~> void
{
    v_assert(t0 == v_static_type_t);

    typ = (v0: *v_type_t);

    //-----------------------------------------------------------------
    //- Just "ignore" it...
    //-----------------------------------------------------------------

    v_set_result_type(v_static_type_t);
    v_set_result_value((typ: LLVMValueRef));
}


//---------------------------------------------------------------------
private
compile_expr_call_helper: (*void, vis: *voidc_visitor_t, self: *v_ast_base_t,
                           args: *v_ast_expr_list_t,
                           t: *v_type_t, v: LLVMValueRef
                          ) ~> void
{
    ttag = v_get_result_type();
    vtag = v_get_result_value();

    void_fun: &*void := 0;
    void_aux: &*void := v_undef();

    q = q_op_postfix_call;

    ft: &*v_type_t    := 0;
    fv: &LLVMValueRef := 0;

    if (v_util_lookup_overload(q, t, &void_fun, &void_aux, &ft, &fv))
    {
        //- Got overloading!

        intrinsic_t = (aux: *void, vis: *voidc_visitor_t,
                       self: *v_ast_base_t,
                       args: *v_ast_expr_list_t,
                       t0: *v_type_t, v0: LLVMValueRef
                      ) ~> void;

        if (fun = (void_fun: *intrinsic_t))
        {
            v_set_result_type(ttag);
            v_set_result_value(vtag);

            fun(void_aux, vis, self, args, t, v);

            v_return();
        }
    }

    i0: &int := 0;

    if (ft)     //- Overloaded...
    {
        i0 := 1;
    }
    else        //- "Normal"...
    {
        v_set_result_type(UNREFERENCE_TAG);
        v_set_result_value(0);

        v_adopt_result(t, v);

        ft := v_get_result_type();
        fv := v_get_result_value();
    }

    i0 = v_load(&i0);       //- Sic!

    if (v_type_is_pointer(ft))  ft := v_type_refptr_get_element_type(ft);

    expr = v_alloca(v_ast_expr_t);
    if (i0) v_initialize(expr);
    defer if (i0) v_terminate(expr);

    arg_count = i0 + v_list_get_size(args);

    arg_ptrs = v_malloc(*v_ast_expr_t, arg_count);
    defer v_free(arg_ptrs);

    for (i: &int := 0; i < arg_count; ++i)
    {
        if (i == 0  &&  i0 == 1)
        {
            v_ast_make_expr_compiled(expr, t, v);

            arg_ptrs[0] := expr;
        }
        else
        {
            arg_ptrs[i] := v_list_get_item(args, i-i0);
        }
    }

    v_set_result_type(ttag);
    v_set_result_value(vtag);

    v_llvm_build_call(vis, self, ft, fv, arg_ptrs, (arg_count: unsigned));
}

//---------------------------------------------------------------------
{   v_make_handler(v_compile_expr_call, compile_expr_call_helper); }


//---------------------------------------------------------------------
private
compile_expr_call: (*void, vis: *voidc_visitor_t, self: *v_ast_base_t) ~> void
{
    fexpr = v_ast_expr_call_get_fun_expr((self: *v_ast_expr_t));
    args  = v_ast_expr_call_get_arg_list((self: *v_ast_expr_t));

    void_fun: &*void := 0;
    void_aux: &*void := v_undef();

    if (v_ast_base_get_visitor_method_tag((fexpr: *v_ast_base_t)) == v_ast_expr_identifier_visitor_method_tag)
    {
        fname_q = v_ast_expr_identifier_get_name_q(fexpr);

        void_fun := v_get_intrinsic_q(fname_q, &void_aux);
    }

    if (fun = (void_fun: *voidc_intrinsic_t))       //- Compile-time intrinsic?
    {
        fun(void_aux, vis, self);

        v_return();
    }

    //- Function call (kinda...)

    ttag = v_get_result_type();
    vtag = v_get_result_value();

    v_set_result_type(INVIOLABLE_TAG);
    v_set_result_value(0);                  //- Sic!

    v_ast_accept_visitor((fexpr: *v_ast_base_t), vis);

    t = v_get_result_type();
    v = v_get_result_value();

    v_set_result_type(ttag);
    v_set_result_value(vtag);

    v_compile_expr_call(vis, self, args, t, v);
}


//---------------------------------------------------------------------
private
op_postfix_index_helper: (*void, vis: *voidc_visitor_t, self: *v_ast_base_t,
                          agg_t: &*v_type_t, agg_v: &LLVMValueRef,
                          idx: **v_ast_expr_list_t, count: int
                         ) ~> bool
{
    ttag = v_get_result_type();
    vtag = v_get_result_value();

    q = q_op_postfix_index;

    void_fun: &*void := 0;
    void_aux: &*void := 0;

    ft: &*v_type_t    := 0;
    fv: &LLVMValueRef := 0;

    if (v_util_lookup_overload(q, agg_t, &void_fun, &void_aux, &ft, &fv))
    {
        //- Got overloading!

        intrinsic_t = (aux: *void, vis: *voidc_visitor_t,
                       self: *v_ast_base_t,
                       agg_t: *v_type_t, agg_v: LLVMValueRef,
                       idx: **v_ast_expr_list_t, count: int
                      ) ~> void;

        if (fun = (void_fun: *intrinsic_t))
        {
            v_set_result_type(ttag);
            v_set_result_value(vtag);

            fun(void_aux, vis, self, agg_t, agg_v, idx, count);

            v_return(true);     //- Sic!
        }
        else
        {
            expr = v_alloca(v_ast_expr_t);
            v_initialize(expr);
            defer v_terminate(expr);

            idx_list = *idx;

            arg_count = 1 + v_list_get_size(idx_list);

            arg_ptrs = v_malloc(*v_ast_expr_t, arg_count);
            defer v_free(arg_ptrs);

            v_ast_make_expr_compiled(expr, agg_t, agg_v);

            arg_ptrs[0] := expr;

            for (j: &int := 1; j < arg_count; ++j)
            {
                arg_ptrs[j] := v_list_get_item(idx_list, j-1);
            }

            agg_t := v_type_function_get_return_type(ft);

            v_set_result_type(agg_t);
            v_set_result_value(0);

            v_llvm_build_call(vis, self, ft, fv, arg_ptrs, (arg_count: unsigned));

            agg_v := v_get_result_value();
        }
    }
    else
    {
        //- Default treatment...

        eidx = v_list_get_item(*idx, 0);                //- Just one!

        v_set_result_type(INVIOLABLE_TAG);
        v_set_result_value(0);

        voidc_internal_op_postfix_index_helper(vis, eidx, agg_t, agg_v);

        agg_t := v_get_result_type();
        agg_v := v_get_result_value();
    }

    v_return(false);
}

//---------------------------------------------------------------------
{   v_make_handler(v_compile_op_postfix_index, op_postfix_index_helper); }

//---------------------------------------------------------------------
private
check_index: (arg: *v_ast_expr_t, agg: **v_ast_expr_t, idx: **v_ast_expr_list_t) ~> bool
{
    tag = v_ast_base_get_visitor_method_tag((arg: *v_ast_base_t));

    if (tag != expr_call_q)  v_return(false);

    etmp = v_ast_expr_call_get_fun_expr(arg);

    tag = v_ast_base_get_visitor_method_tag((etmp: *v_ast_base_t));

    if (tag != expr_identifier_q)  v_return(false);

    qname = v_ast_expr_identifier_get_name_q(etmp);

    if (qname != q_op_postfix_index)  v_return(false);

    if (agg  ||  idx)
    {
        lst = v_ast_expr_call_get_arg_list(arg);

        if (agg)  *agg := v_list_get_item(lst, 1);

        if (idx)
        {
            op = v_list_get_item(lst, 0);

            void_obj = v_ast_generic_get_object(op);

            obj = *(void_obj: *v_ast_operator_unary_t);

            *idx := (&obj[2] : *v_ast_expr_list_t);
        }
    }

    v_return(true);
}

//---------------------------------------------------------------------
private
op_postfix_index_intrinsic: (*void, vis: *voidc_visitor_t, self: *v_ast_base_t) ~> void
{
    args = v_ast_expr_call_get_arg_list((self: *v_ast_expr_t));

    N: &int := 1;

    {   ast: &*v_ast_expr_t := v_list_get_item(args, 1);

        while(check_index(ast, &ast, 0))  ++N;
    }

    agg: &*v_ast_expr_t := v_list_get_item(args, 1);

    idx = v_malloc(*v_ast_expr_list_t, N);
    defer v_free(idx);

    {   idx_i: &**v_ast_expr_list_t := idx + (N-1);

        op = v_list_get_item(args, 0);

        void_obj = v_ast_generic_get_object(op);

        obj = *(void_obj: *v_ast_operator_unary_t);

        *idx_i := (&obj[2] : *v_ast_expr_list_t);

        while(check_index(agg, &agg, --idx_i)) ;
    }


    ttag = v_get_result_type();
    vtag = v_get_result_value();


    v_set_result_type(INVIOLABLE_TAG);
    v_set_result_value(0);

    v_ast_accept_visitor((agg: *v_ast_base_t), vis);

    agg_t: &*v_type_t    := v_get_result_type();
    agg_v: &LLVMValueRef := v_get_result_value();


    for (i: &int := 0; i < N; ++i)
    {
        v_set_result_type(ttag);
        v_set_result_value(vtag);

        fin = v_compile_op_postfix_index(vis, self, agg_t, agg_v, idx+i, N-i);

        if (fin)  v_return();       //- Sic!
    }

    v_set_result_type(ttag);
    v_set_result_value(vtag);

    v_adopt_result(agg_t, agg_v);
}


//---------------------------------------------------------------------
private
static_type_postfix_index_intrinsic: (*void, vis: *voidc_visitor_t, self: *v_ast_base_t,
                                      agg_t: *v_type_t, agg_v: LLVMValueRef,
                                      idx: **v_ast_expr_list_t, count: int
                                     ) ~> void
{
    t: &*v_type_t := (agg_v: *v_type_t);

    for (i: &int := count-1; i >= 0; --i)
    {
        idx_list = idx[i];

        idx_count = v_list_get_size(idx_list);

        if (idx_count)
        {
            expr = v_list_get_item(idx_list, 0);        //- Just one!

            v_set_result_type(UNREFERENCE_TAG);
            v_set_result_value(0);

            v_ast_accept_visitor((expr: *v_ast_base_t), vis);

            idx_v = v_get_result_value();

            n = (LLVMConstIntGetZExtValue(idx_v) : uint64_t);

            t := v_array_type(t, n);
        }
        else    //- No indices!
        {
            arg = v_type_generic_type_arg(t);

            t := v_generic_type(voidc_incomplete_array_type_tag, &arg, 1);
        }
    }

    v_set_result_type(v_static_type_t);

    v_set_result_value((t: LLVMValueRef));
}


//---------------------------------------------------------------------
//- Enable overloading
//---------------------------------------------------------------------
export
voidc_enable_overloading_compiler: () ~> void
{
    vis = v_get_compiler();

    q = v_quark_from_string;

    q_static_type_prefix_star = q("v.static_type_prefix_star");

    v_return_if(v_get_intrinsic_q(q_static_type_prefix_star, 0));

    //-------------------------------------------------------------
    voidc_enable_handlers_etc();

    //-------------------------------------------------------------
    for (id: &int := 0; id < op_table_length; ++id)
    {
        void_fun: & *void := 0;

        switch(op_table[id][1])
        {
        case opk_assign:    void_fun := op_assign_intrinsic;    v_break();
        case opk_relation:  void_fun := op_relation_intrinsic;  v_break();
        case opk_binary:    void_fun := op_binary_intrinsic;    v_break();
        case opk_unary:     void_fun := op_unary_intrinsic;     v_break();
        }

        if (!void_fun)  v_continue();

        v_add_intrinsic_q(op_table[id][2], void_fun, (id: *void));
    }

    //-------------------------------------------------------------
    q_static_type_prefix_amp      = q("v.static_type_prefix_amp");
    q_static_type_prefix_const    = q("v.static_type_prefix_const");
    q_static_type_prefix_volatile = q("v.static_type_prefix_volatile");
    q_static_type_postfix_index   = q("v.static_type_postfix_index");

    v_add_overload_q(q("v.op_prefix_star"), v_static_type_t, q_static_type_prefix_star);
    v_add_overload_q(q("v.op_prefix_amp"),  v_static_type_t, q_static_type_prefix_amp);

    v_add_intrinsic_q(q_static_type_prefix_star, static_type_prefix_star_intrinsic, 0);
    v_add_intrinsic_q(q_static_type_prefix_amp,  static_type_prefix_amp_intrinsic,  0);

    v_add_overload_q(q("v.op_prefix_const"),    v_static_type_t, q_static_type_prefix_const);
    v_add_overload_q(q("v.op_prefix_volatile"), v_static_type_t, q_static_type_prefix_volatile);

    v_add_intrinsic_q(q_static_type_prefix_const,    static_type_prefix_cv_intrinsic, 0);
    v_add_intrinsic_q(q_static_type_prefix_volatile, static_type_prefix_cv_intrinsic, 0);

    voidc_visitor_set_method(vis, vis, expr_call_q, compile_expr_call, 0);

    //-------------------------------------------------------------
    v_add_intrinsic_q(q_op_postfix_index, op_postfix_index_intrinsic, 0);

    v_add_overload_q(q_op_postfix_index, v_static_type_t, q_static_type_postfix_index);

    v_add_intrinsic_q(q_static_type_postfix_index, static_type_postfix_index_intrinsic, 0);

    //-------------------------------------------------------------
    v_set_handler(v_llvm_build_call,          llvm_build_call_helper,   0);
    v_set_handler(v_compile_expr_call,        compile_expr_call_helper, 0);
    v_set_handler(v_compile_op_assign,        op_assign_helper,         0);
    v_set_handler(v_compile_op_binary,        op_binary_helper,         0);
    v_set_handler(v_compile_op_relation,      op_relation_helper,       0);
    v_set_handler(v_compile_op_unary,         op_unary_helper,          0);
    v_set_handler(v_compile_op_postfix_index, op_postfix_index_helper,  0);
}


//---------------------------------------------------------------------
//- Enable overloading
//---------------------------------------------------------------------
export
voidc_enable_overloading: () ~> void
{
    voidc_enable_overloading_compiler();
}


//=====================================================================
{   voidc_unit_end_module(); }



